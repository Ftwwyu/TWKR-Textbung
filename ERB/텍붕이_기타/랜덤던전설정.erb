@RD_MAKE
CALL RD_RESET
CALL RD_LOCATE
CALL RD_ENAMY(0,0,"적수")
LOCAL = RESULT
CALL RD_ENAMY(0,0,"보스수")
LOCAL:1 = RESULT
D_OBJECT = RAND:3
;0.회수: 던전 안에서 특정 물체와 상호작용후 안전하게 출구로 탈출 
;1.보스 처치: 특정물체와 상호작용해서 보스와 전투후 승리
;2.정찰: 특정물체와 상호작용하고 올 것(튕겨나와도 OK)
;3.회수 및 보스처치: 보스를 처치하고 물건을 찾아올것
SELECTCASE D_OBJECT
    CASE 0
        D_DIFFCULT += 1
    CASE 1
        D_BOSS = 10000 + RAND:(LOCAL:1)
        D_DIFFCULT += 2
    CASE 2
    CASE 3
        D_BOSS = 10000 + RAND:(LOCAL:1)
        D_DIFFCULT += 3
ENDSELECT
RD_TYPE = RAND:2
;0.결계: 불안정해지면 공간왜곡
;1.동굴: 적만 나옴
SELECTCASE RD_TYPE
    CASE 0

    CASE 1
        TT_DISTORTION_DISABLE = 1
ENDSELECT
D_MAP_SIGHT = 3 + RAND:8
D_DIFFCULT += MAX(0,(10 - D_MAP_SIGHT)/3)
D_MAP_STABLE = 5 + RAND:21
D_DIFFCULT += MAX(0,(25 - D_MAP_STABLE)/5)
D_ENAMY = RAND:LOCAL
CALL RD_ENAMY(D_ENAMY,3)
D_DIFFCULT += RESULT

@RD_LOCATE
SELECTCASE RAND:14
    CASE 0
        LOCAL = P25수호신의숲
    CASE 1
        LOCAL = P121묘지
    CASE 2
        LOCAL = P140지장길
    CASE 3
        LOCAL = P348안개의호수・바닥
    CASE 4
        LOCAL = P431토끼동굴
    CASE 5
        LOCAL = P433사각의죽림
    CASE 6
        LOCAL = P436무명의언덕
    CASE 7
        LOCAL = P506무연총
    CASE 8
        LOCAL = P509재고의길
    CASE 9
        LOCAL = P701수해도입구
    CASE 10
        LOCAL = P708큰두꺼비의연못
    CASE 11
        LOCAL = P738깊은숲속
    CASE 12
        LOCAL = P830현운해
    CASE 13
        LOCAL = P945강가
ENDSELECT
D_LOCATE = LOCAL

@RD_RESET
VARSET D_ENAMY
VARSET D_BOSS
VARSET D_OBJECT_INTERACTION
VARSET RD_TYPE
VARSET D_DIFFCULT
VARSET D_OBJECT
VARSET D_MAP_STABLE
VARSET D_MAP_SIGHT
VARSET D_LOCATE

@RD_JOIN
SIF D_LOCATE
    CALL 결계탐색(10002)
IF D_OBJ_SUC == 2
    RETURN 1
ENDIF
RETURN 0

@RD_OBJ_INTERACTION
SELECTCASE D_OBJECT
    CASE 0
        D_OBJ_SUC = 1
        PRINTFORMW 물건을 회수했다! 이제 나가기만 하면 된다
    CASE 1
        CALL TT_적기본세팅()
        CALL RD_ENAMY(D_BOSS)
        CALL 뉴탄막전투(MASTER,TARGET,1)
        CALLFORM TT_MAP_{TT_NUM}("적애프터", TT_ENAMY)
        LOCAL = RESULT
        DELCHARA TARGET
        TARGET = 0
        경험치인터셉트 = 0
        후처리인터셉트 = 0
        SELECTCASE LOCAL
            CASE 0
                ;타겟 패배
                D_OBJ_SUC = 2
                PRINTFORMW 토벌에 성공했다!
            CASE 1,2
                ;무승부 혹은 패배
                PRINTFORMW 토벌에 실패한 %마스터는% 던전 밖으로 추방되었다!
                D_OBJ_SUC = 0
                CALL RD_GETOUT
        ENDSELECT
    CASE 2
        D_OBJ_SUC = 2
        PRINTFORMW 적당히 주변 정보를 기록했다, 나가기만 하면 된다
    CASE 3
        CALL TT_적기본세팅()
        CALL RD_ENAMY(D_BOSS)
        CALL 뉴탄막전투(MASTER,TARGET,1)
        CALLFORM TT_MAP_{TT_NUM}("적애프터", TT_ENAMY)
        LOCAL = RESULT
        DELCHARA TARGET
        TARGET = 0
        경험치인터셉트 = 0
        후처리인터셉트 = 0
        SELECTCASE LOCAL
            CASE 0
                ;타겟 패배
                D_OBJ_SUC = 1
                PRINTFORMW 토벌에 성공했다!
            CASE 1,2
                ;무승부 혹은 패배
                PRINTFORMW 토벌에 실패한 %마스터는% 던전 밖으로 추방되었다!
                D_OBJ_SUC = 0
                CALL RD_GETOUT
        ENDSELECT
        PRINTFORMW 물건을 회수했다! 이제 나가기만 하면 된다
ENDSELECT
TT_맵:(TT_NOW:0):(TT_NOW:1) = 0

@RD_GETOUT
TT_GETOUT = 1
D_OBJ_SUC = 0

@RDSETTING_APPLY
TT_STABLE = D_MAP_STABLE
TT_SIGHT = D_MAP_SIGHT

@RD_ENAMY(ARG,ARG:1 = -1,ARGS = "")
IF ARGS == ""
    SELECTCASE ARG
        CASE 0
            SELECTCASE ARG:1
                CASE -1
                    ;적 정보
                    TT_ENAMY = 0
                    PRINTFORMW 유령이 나타났다!
                    CALLNAME:TARGET = 유령
                    NAME:TARGET = 유령
                    BASE:TARGET:체력 = 2000
                    ABL:TARGET:전투능력 = 5
                    ABL:TARGET:레벨 = 20
                    CFLAG:TARGET:힘 = 10
                    CFLAG:TARGET:지성 = 35
                    CFLAG:TARGET:속도 = 10
                    CFLAG:TARGET:인내 = 15
                    CFLAG:TARGET:운 = 30
                    CFLAG:물리내성 = 500000
                    CFLAG:정신내성 = -500
                    TALENT:TARGET:정화 = 1
                    TALENT:TARGET:탄막소사 = 1
                    TALENT:TARGET:전투스타일 = 1
                CASE 0
                    ;적 애프터-타겟 패배
                    PRINTFORMW 유령을 쓰러뜨렸다!
                    RETURN RAND:10
                CASE 1
                    ;무승부
                    PRINTFORMW 유령이 도망쳤다!
                    RETURN RAND:25
                CASE 2
                    ;마스터 패배
                    PRINTFORMW 유령한테 당했다!
                    RETURN RAND:100 + 1
                CASE 3
                    ;난이도
                    RETURN 2
            ENDSELECT
        CASE 1
            SELECTCASE ARG:1
                CASE -1
                    ;적 정보
                    TT_ENAMY = 0
                    PRINTFORMW 야생의 요정이 나타났다!
                    CALLNAME:TARGET = 요정
                    NAME:TARGET = 요정
                    BASE:TARGET:체력 = 1000
                    ABL:TARGET:전투능력 = 3
                    ABL:TARGET:레벨 = 10
                    CFLAG:TARGET:힘 = 5
                    CFLAG:TARGET:지성 = 5
                    CFLAG:TARGET:속도 = 15
                    CFLAG:TARGET:인내 = 10
                    CFLAG:TARGET:운 = 15
                    CFLAG:물리내성 = 5 * ABL:레벨
                    CFLAG:화염내성 = 10 * ABL:레벨
                    CFLAG:빙결내성 = 5 * ABL:레벨
                    CFLAG:전격내성 = 5 * ABL:레벨
                    CFLAG:질풍내성 = -10 * ABL:레벨
                    CFLAG:마력내성 = 5 * ABL:레벨
                    CFLAG:수격내성 = 5 * ABL:레벨
                    CFLAG:정신내성 = -5 * ABL:레벨
                    TALENT:TARGET:정화 = 1
                    TALENT:TARGET:탄막소사 = 1
                    TALENT:TARGET:전투스타일 = 1
                CASE 0
                    ;적 애프터-타겟 패배
                    PRINTFORMW 요정을 쓰러뜨렸다!
                    RETURN RAND:10
                CASE 1
                    ;무승부
                    PRINTFORMW 요정이 도망쳤다!
                    RETURN RAND:25
                CASE 2
                    ;마스터 패배
                    PRINTFORMW 요정한테 당했다!
                    RETURN RAND:100 + 1
                CASE 3
                    ;난이도
                    RETURN 1
            ENDSELECT
        CASE 10000
            SELECTCASE ARG:1
                CASE -1
                    ;적 정보
                    TT_ENAMY = 0
                    PRINTFORMW 해바라기 요정이 나타났다!
                    CALLNAME:TARGET = 해바라기요정
                    NAME:TARGET = 해바라기요정
                    BASE:TARGET:체력 = 1500
                    ABL:TARGET:전투능력 = 5
                    ABL:TARGET:레벨 = 25
                    CFLAG:TARGET:힘 = 20
                    CFLAG:TARGET:지성 = 15
                    CFLAG:TARGET:속도 = 30
                    CFLAG:TARGET:인내 = 25
                    CFLAG:TARGET:운 = 35
                    CFLAG:물리내성 = 5 * ABL:레벨
                    CFLAG:화염내성 = 10 * ABL:레벨
                    CFLAG:빙결내성 = 5 * ABL:레벨
                    CFLAG:전격내성 = 5 * ABL:레벨
                    CFLAG:질풍내성 = -10 * ABL:레벨
                    CFLAG:마력내성 = 5 * ABL:레벨
                    CFLAG:수격내성 = 5 * ABL:레벨
                    CFLAG:정신내성 = -5 * ABL:레벨
                    TALENT:TARGET:정화 = 1
                    TALENT:TARGET:탄막소사 = 1
                    TALENT:TARGET:불을다루는정도의능력 = 1
                    TALENT:TARGET:전투스타일 = 1
                CASE 0
                    ;적 애프터-타겟 패배
                    PRINTFORMW 요정을 쓰러뜨렸다!
                    RETURN RAND:10
                CASE 1
                    ;무승부
                    PRINTFORMW 요정이 도망쳤다!
                    RETURN RAND:25
                CASE 2
                    ;마스터 패배
                    PRINTFORMW 요정한테 당했다!
                    RETURN RAND:100 + 1
                CASE 3
                    ;난이도
                    RETURN 1
            ENDSELECT
    ENDSELECT
ELSEIF ARGS == "적수"
    RETURN 2
ELSEIF ARGS == "보스수"
    RETURN 1
ENDIF