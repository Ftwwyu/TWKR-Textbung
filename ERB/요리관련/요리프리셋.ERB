@요리프리셋
DRAWLINE
FOR LOCAL,0,10
    IF Y_프리셋:LOCAL:0 != 0
        IF !요리프리셋가능여부(LOCAL)
            SETCOLOR 0x808080
        ENDIF
        PRINTBUTTON @"[%Y_PRE_NAME:LOCAL,40,LEFT%]",LOCAL
        SIF Y_PRE_PRICE:LOCAL
            PRINTFORM  {Y_PRE_PRICE:LOCAL}￦
        PRINTL
        RESETCOLOR
    ELSE
        ;SETCOLOR
        SETCOLOR 0x808080
        PRINTFORML [----------------------------------------]
        RESETCOLOR
    ENDIF
NEXT
DRAWLINE
PRINTFORML [996] 고유 요리
PRINTFORML [997] 등록 모드
PRINTFORML [998] 삭제 모드
PRINTFORML [999] 돌아간다
$LOOP
INPUT
IF RESULT == 999
    RETURN
ELSEIF RESULT == 996
    CALL Y_고유요리선택()
    IF RESULT == 5
        RETURN 5
    ENDIF
ELSEIF RESULT == 997
    PRINTFORML 어디에?
    FOR LOCAL,0,10
        IF Y_프리셋:LOCAL:0 != 0
            PRINTBUTTON @"[%Y_PRE_NAME:LOCAL,40,LEFT%]",LOCAL
            PRINTL
        ELSE
            PRINTBUTTON "[----------------------------------------]",LOCAL
            PRINTL
        ENDIF
    NEXT
    PRINTFORML [999] 돌아간다
    $LOOP2
    INPUT
    IF RESULT == 999

    ELSEIF RESULT > 10 || RESULT < 0
        GOTO LOOP2
    ELSE
        LOCAL = RESULT
        IF Y_프리셋:LOCAL:0 == 1
            PRINTFORML 이미 있던 것을 삭제하고 대체합니까?
            CALL ASK_YN()
            IF RESULT
                RESTART
            ELSE
                CALL 요리프리셋삭제(LOCAL)
                CALL 요리프리셋생성(LOCAL)
            ENDIF
        ELSE
            CALL 요리프리셋생성(LOCAL)
        ENDIF
    ENDIF
ELSEIF RESULT == 998
    PRINTFORML 무엇을?
    FOR LOCAL,0,10
        IF Y_프리셋:LOCAL:0 != 0
            PRINTBUTTON @"[%Y_PRE_NAME:LOCAL,40,LEFT%]",LOCAL
            PRINTL
        ELSE
            ;SETCOLOR
            SETCOLOR 0x808080
            PRINTFORML [----------------------------------------]
            RESETCOLOR
        ENDIF
    NEXT
    PRINTFORML [999] 돌아간다
    $LOOP3
    INPUT
    IF RESULT == 999

    ELSEIF RESULT > 10 || RESULT < 0
        GOTO LOOP3
    ELSE
        CALL 요리프리셋삭제(RESULT)
    ENDIF
ELSEIF RESULT > 10 || RESULT < 0
    GOTO LOOP
ELSE
    LOCAL = RESULT
    IF !요리프리셋가능여부(LOCAL)
        PRINTFORMW 재료가 부족합니다
        RESTART
    ENDIF
    CALL 요리프리셋호출(LOCAL)
    RETURN
ENDIF
RESTART

@요리프리셋생성(ARG)
IF !요리프리셋빈자리(ARG)
    RETURN -1
ENDIF
Y_프리셋:ARG:0 = 1
Y_프리셋:ARG:1 = 주재료
Y_프리셋:ARG:2 = 주재료요리법
SELECTCASE DISH_TYPE
    CASE "주식"
        Y_프리셋:ARG:3 = 1
    CASE "경식"
        Y_프리셋:ARG:3 = 2
    CASE "디저트"
        Y_프리셋:ARG:3 = 3
    CASEELSE
        Y_프리셋:ARG:3 = 1
ENDSELECT

FOR LOCAL,0,10
    Y_PRE_SUB:ARG:LOCAL:0 = 부재료:LOCAL
    Y_PRE_SUB:ARG:LOCAL:1 = 부재료요리법:LOCAL
    Y_PRE_SPICE:ARG:LOCAL = 양념:LOCAL
NEXT
IF DISH_NAME != ""
    Y_PRE_NAME:ARG = %DISH_NAME%
ELSE
    Y_PRE_NAME:ARG '= 자동요리이름()
ENDIF
Y_PRE_DRG:ARG:0 = DISH_DRG
Y_PRE_DRG:ARG:1 = DISH_DRG_2
Y_PRE_PRICE:ARG = 기본제공품사용료

@요리프리셋호출(ARG)
#DIM DYNAMIC 기본제공품
주재료 = Y_프리셋:ARG:1
주재료요리법 = Y_프리셋:ARG:2 
SELECTCASE Y_프리셋:ARG:3
    CASE 1
        DISH_TYPE '= "주식"
    CASE 2
        DISH_TYPE '= "경식"
    CASE 3
        DISH_TYPE '= "디저트"
    CASEELSE
        DISH_TYPE '= "주식"
ENDSELECT

FOR LOCAL,0,10
    부재료:LOCAL = Y_PRE_SUB:ARG:LOCAL:0
    부재료요리법:LOCAL = Y_PRE_SUB:ARG:LOCAL:1
    양념:LOCAL = Y_PRE_SPICE:ARG:LOCAL
NEXT
DISH_NAME = %Y_PRE_NAME:ARG% 
DISH_DRG = Y_PRE_DRG:ARG:0
DISH_DRG_2 = Y_PRE_DRG:ARG:1
IF 기본제공품사용료 <= 0
    CALL 기본재료표시함수("주재료",1)
    기본제공품 = RESULT
    IF INRANGE(주재료,1,기본제공품)
        기본제공품사용료 += 500
    ENDIF
    CALL 기본재료표시함수("부재료",1)
    기본제공품 = RESULT
    FOR LOCAL,0,10
        IF INRANGE(부재료:LOCAL,1,기본제공품)
            기본제공품사용료 += 500
        ENDIF
    NEXT
    CALL 기본재료표시함수("양념",1)
    기본제공품 = RESULT
    FOR LOCAL,0,10
        IF INRANGE(양념:LOCAL,1,기본제공품)
            기본제공품사용료 += 100
        ENDIF
    NEXT
    Y_PRE_PRICE:ARG = 기본제공품사용료
    IF MONEY < 기본제공품사용료
        PRINTFORMW 재료 구매비용이 부족합니다
        CALL 요리제작초기화
        RETURN
    ENDIF
ENDIF
기본제공품사용료 = Y_PRE_PRICE:ARG

@요리프리셋삭제(ARG)
;이것만으로도 빈칸 취급되는 그윽함
Y_프리셋:ARG:0 = 0

@요리프리셋빈자리(ARG)
#FUNCTION
IF Y_프리셋:ARG:0 == 0
    RETURNF 1
ENDIF
RETURNF 0

@요리프리셋가능여부(ARG)
#FUNCTION
IF 요리프리셋빈자리(ARG)
    RETURNF 0
ENDIF

;간단히 20미만은 기본제공재료라 생각하자
IF Y_프리셋:ARG:1 > 20
    IF ITEM:(Y_프리셋:ARG:1) <= 0
        RETURNF 0
    ENDIF
ENDIF

FOR LOCAL,0,10
    IF Y_PRE_SUB:ARG:LOCAL:0 > 20 && ITEM:(Y_PRE_SUB:ARG:LOCAL:0) <= 0
        RETURNF 0
    ENDIF
    IF Y_PRE_SPICE:ARG:LOCAL > 20 && ITEM:(Y_PRE_SPICE:ARG:LOCAL) <= 0
        RETURNF 0
    ENDIF
NEXT
IF Y_PRE_DRG:ARG:0 && ITEM:(Y_PRE_DRG:ARG:0) <= 0
    RETURNF 0
ENDIF
IF Y_PRE_DRG:ARG:1 && ITEM:(Y_PRE_DRG:ARG:1) <= 0
    RETURNF 0
ENDIF
RETURNF 1

@요리제작초기화
VARSET 주재료
VARSET 주재료요리법
VARSET 부재료
VARSET 부재료요리법
VARSET 양념
VARSET 기본제공품사용료


@Y_PRE_ERROR_CORRECT
#DIM DYNAMIC 카운터
#DIM DYNAMIC 안전횟수
FOR LOCAL,0,10
    안전횟수 = 0
    WHILE Y_PRESET_VER:LOCAL < Y_PRESET_VER_NOW && 안전횟수 <= 10
        IF Y_PRESET_VER:LOCAL == 0
            ;Y_감자가 사라지고 그 자리가 콩이 되버린걸 수정
            IF Y_프리셋:LOCAL:1 == 6
                Y_프리셋:LOCAL:1 = 1039
            ENDIF
            IF Y_프리셋:LOCAL:1 == 7
                Y_프리셋:LOCAL:1 = 6
            ENDIF
            IF Y_프리셋:LOCAL:1 == 8
                Y_프리셋:LOCAL:1 = 7
            ENDIF
            IF Y_프리셋:LOCAL:1 == 9
                Y_프리셋:LOCAL:1 = 8
            ENDIF
            IF Y_프리셋:LOCAL:1 == 10
                Y_프리셋:LOCAL:1 = 9
            ENDIF
            IF Y_프리셋:LOCAL:1 == 11
                Y_프리셋:LOCAL:1 = 10
            ENDIF
            FOR 카운터,0,10
                IF Y_PRE_SUB:LOCAL:카운터:0 == 6
                    Y_PRE_SUB:LOCAL:카운터:0 = 1039
                ENDIF
                IF Y_PRE_SUB:LOCAL:카운터:0 == 7
                    Y_PRE_SUB:LOCAL:카운터:0 = 6
                ENDIF
                IF Y_PRE_SUB:LOCAL:카운터:0 == 8
                    Y_PRE_SUB:LOCAL:카운터:0 = 7
                ENDIF
                IF Y_PRE_SUB:LOCAL:카운터:0 == 9
                    Y_PRE_SUB:LOCAL:카운터:0 = 8
                ENDIF
                IF Y_PRE_SUB:LOCAL:카운터:0 == 10
                    Y_PRE_SUB:LOCAL:카운터:0 = 9
                ENDIF
                IF Y_PRE_SUB:LOCAL:카운터:0 == 11
                    Y_PRE_SUB:LOCAL:카운터:0 = 10
                ENDIF
            NEXT
            Y_PRESET_VER:LOCAL++
        ENDIF
        안전횟수++
    WEND
NEXT


@RCP_LEARN(ARG)
VARSET 주재료
VARSET 주재료요리법
VARSET 부재료
VARSET 부재료요리법
VARSET 양념
CALL 요리레시피(ARG)
주재료 = RCP_주재료:0
IF 주재료 < 100
    기본제공품사용료 += 500
ENDIF
주재료요리법 = RCP_주재료:1
FOR LOCAL,0,9
    부재료:LOCAL = RCP_부재료:LOCAL:0
    IF 부재료:LOCAL < 100
        기본제공품사용료 += 250
    ENDIF
    부재료요리법:LOCAL = RCP_부재료:LOCAL:1
    양념:LOCAL = RCP_양념:LOCAL
    IF 양념:LOCAL < 100
        기본제공품사용료 += 100
    ENDIF
NEXT
FOR LOCAL,0,9
    SIF 요리프리셋빈자리(LOCAL)
        CALL 요리프리셋생성(LOCAL)
NEXT