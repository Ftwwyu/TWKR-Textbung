@감금실설정
PRINTFORML 감금실과 노예에 관한 여러가지 관리를 합니다
PRINTFORMLC [001] - 수감 목록
PRINTFORMLC [002] - 정책
PRINTL
PRINTFORMLC [101] - 현재 노예 목록
PRINTFORMLC [102] - 노예 정책
PRINTL
PRINTFORMLC [999] - 돌아간다
INPUT

SELECTCASE RESULT
    CASE 1
        CALL 수감자명부
    CASE 2
        CALL 감금실정책설정
    CASE 101
        CALL 당신의노예목록
        INPUT
        IF RESULT == 999
            RETURN
        ENDIF
    CASE 102
        CALL 노예정책설정
    CASE 999
        RETURN
ENDSELECT
RESTART

@당신의노예목록
FOR LOCAL,0,CHARANUM
    IF TALENT:LOCAL:당신의노예
        PRINTFORML [{LOCAL}] - %CALLNAME:LOCAL%, %노예정책내용:(노예정책:LOCAL:0)%
    ENDIF
NEXT
PRINTL [999]돌아간다

@노예정책설정
PRINTFORM 누구의 정책을 설정할까?           
PRINTBUTTON "[일괄설정]",6974
CALL 당신의노예목록
INPUT
IF RESULT == 999
    RETURN
ELSEIF RESULT == 6974
    PRINTFORML 모두의 정책을 어떻게 바꿀까?
    PRINTFORML [0] - 방치한다
    PRINTFORML [1] - 팬티를 상납하게 한다
    PRINTFORML [2] - 돈을 상납하게 한다
    INPUT
    IF RESULT > 2 || RESULT < 0
        RESULT = 0
    ENDIF
    FOR LOCAL,1,CHARANUM
        IF TALENT:LOCAL:당신의노예
            노예정책:LOCAL:0 = RESULT
        ENDIF
    NEXT
ELSEIF RESULT < CHARANUM && RESULT > 0
    IF TALENT:RESULT:당신의노예
        LOCAL = RESULT
        PRINTFORML %CALLNAME:LOCAL%의 정책을 어떻게 할까?
        PRINTFORML [0] - 방치한다
        PRINTFORML [1] - 팬티를 상납하게 한다
        PRINTFORML [2] - 돈을 상납하게 한다
        INPUT
        IF RESULT > 2 || RESULT < 0
            RESULT = 0
        ENDIF
        노예정책:LOCAL:0 = RESULT
    ENDIF
ENDIF

@노예정책수행(ARG)
SELECTCASE 노예정책:ARG:0
    CASE 0
        ;없음
    CASE 1
        IF RAND:3 == 0
            CALL PANTS_GET(ARG,"받는다")
            PRINTFORML %CALLNAME:ARG%의 팬티가 배송되었다...
        ENDIF
    CASE 2
        IF RAND:2 == 0
            MONEY += 소득수준:ARG * 80
            PRINTFORML %ARG로%부터 {소득수준:ARG * 80}원을 받았다...
        ENDIF
ENDSELECT

@수감자명부
FOR LOCAL,0,CHARANUM
    IF CFLAG:LOCAL:감금됨 == 1
        SETCOLOR 0xFF8000
        PRINTFORML [{LOCAL}] - %CALLNAME:LOCAL%
        RESETCOLOR
    ELSEIF CFLAG:LOCAL:감금됨 == 2
        SETCOLOR 0x00FFFF
        PRINTFORML [{LOCAL}] - %CALLNAME:LOCAL%
        RESETCOLOR
    ENDIF
NEXT
PRINTL [999]돌아간다
INPUT
IF RESULT == 999
    RETURN
ENDIF


@감금실정책설정
#DIMS DYNAMIC 감금정책내용, 10 = "방치","착유","성감 개발","노동"
;,"성감강화"
FOR LOCAL,0,CHARANUM - 1
    IF CFLAG:LOCAL:감금됨 == 1
        SETCOLOR 0xFF8000
        PRINTFORML [{LOCAL}] - %CALLNAME:LOCAL%, %감금정책내용:(감금실정책:LOCAL:0)%
        RESETCOLOR
    ELSEIF CFLAG:LOCAL:감금됨 == 2
        SETCOLOR 0x00FFFF
        PRINTFORML [{LOCAL}] - %CALLNAME:LOCAL%, %감금정책내용:(감금실정책:LOCAL:0)%
        RESETCOLOR
    ENDIF
NEXT
PRINTL [999]돌아간다
$LOOP
INPUT
IF RESULT == 999
    RETURN
ELSEIF RESULT > 0 && RESULT < CHARANUM
    LOCAL = RESULT
    IF !CFLAG:LOCAL:감금됨
        GOTO LOOP
    ELSE
        PRINTFORML %CALLNAME:LOCAL%에게 무엇을 시킬까?
        FOR LOCAL:1,0,10
            IF 감금정책내용:(LOCAL:1) != ""
                SIF 감금실정책조건(LOCAL:1,LOCAL)
                    PRINTFORML [{LOCAL:1}] %감금정책내용:(LOCAL:1)%
            ENDIF
        NEXT
        $LOOP2
        INPUT
        IF RESULT >= 0 && RESULT <= 10
            IF 감금정책내용:RESULT == ""
                GOTO LOOP2
            ELSEIF 감금실정책조건(RESULT,LOCAL)
                감금실정책:LOCAL:0 = RESULT
                PRINTFORML %CALLNAME:LOCAL%에 대한 정책을 '%감금정책내용:(감금실정책:LOCAL:0)%' 로 바꿉니다
            ENDIF
        ELSE
            GOTO LOOP2
        ENDIF
    ENDIF
ELSE
    GOTO LOOP
ENDIF
RESTART

@감금실정책조건(ARG,ARG:1)
#FUNCTION
SELECTCASE ARG
    CASE 0
        RETURNF 1
    CASE 1
        IF TALENT:(ARG:1):모유체질
            RETURNF 1
        ENDIF
    CASE 2
        IF ITEM:로터
            RETURNF 1
        ENDIF
    CASE 3
        RETURNF 1
ENDSELECT
RETURNF 0


@감금실정책수행(캐릭터)
#DIM DYNAMIC 정책내용
#DIM 캐릭터
IF !CFLAG:캐릭터:감금됨
    RETURN
ENDIF
정책내용 = 감금실정책:캐릭터:0

SELECTCASE 정책내용
    CASE 0
        ;방치, 처리 없음
        RETURN
    CASE 1
        ;착유
        IF TALENT:캐릭터:모유체질
            PRINTFORML %CALLNAME:캐릭터%의 가슴을 쥐어짜 모유를 생산했다...
            LOCAL = (ABL:캐릭터:B감각 * 100) + (TALENT:캐릭터:분유강화 * 150) + (TALENT:캐릭터:임신 * 150) + (TALENT:캐릭터:육아중 * 250) + (TALENT:캐릭터:B감도 * 50) + (TALENT:캐릭터:음유 * 100) + 50
            LOCAL /= 100
            IF LOCAL > 0
                PRINTFORML {LOCAL}병의 모유를 생산했다
                ITEM:우유 += LOCAL
            ENDIF
        ELSE
            PRINTFORML %CALLNAME:캐릭터%의 가슴을 쥐어짜냈지만, 아무런 일도 일어나지 않았다...
            PRINTFORML 결국 착유는 그만두기로 했다
            감금실정책:캐릭터:0 = 0
        ENDIF
    CASE 2
        ;성감 강화
        PRINTFORML %CALLNAME:캐릭터%의 몸 구석구석에 부착된 로터가 성감을 개발하고 있다...
        CALL JUEL_UP_SPECIAL(캐릭터,"쾌C","C감각", 3, 1)
        CALL JUEL_UP_SPECIAL(캐릭터,"쾌A","A감각", 3, 1)
        CALL JUEL_UP_SPECIAL(캐릭터,"쾌B","B감각", 3, 1)
        CALL JUEL_UP_SPECIAL(캐릭터,"욕정","욕망", 3, 1)
        CALL JUEL_UP_SPECIAL(캐릭터,"치정","자위중독", 3, 1)
    CASE 3
        ;노동
        PRINTFORML %조사처리(CALLNAME:캐릭터,"은")% 열심히 %TEXTR("인형에 눈알을 붙이고 있다/삯바느질을 하고 있다/베를 짜고 있다/볏짚으로 무언갈 만들고 있다")%...
        SELECTCASE ABL:캐릭터:기교
            CASE 0
                PRINTFORML 잘 안되는 모양이다...
                MONEY += 250
                PRINTFORML 250원을 벌었다
            CASE 1 TO 2
                PRINTFORML 느리지만 착실히 되고 있다...
                MONEY += 300 * ABL:캐릭터:기교
                PRINTFORML {300 * ABL:캐릭터:기교}원을 벌었다
            CASE 3 TO 5
                PRINTFORML 상당히 능숙하게 해 나가고 있다
                MONEY += 350 * ABL:캐릭터:기교
                PRINTFORML {350 * ABL:캐릭터:기교}원을 벌었다
            CASEELSE
                PRINTFORML 순식간에 대량의 일을 처리하고 있다...
                MONEY += MIN(400 * ABL:캐릭터:기교, 4000)
                PRINTFORML {MIN(400 * ABL:캐릭터:기교, 4000)}원을 벌었다
        ENDSELECT
ENDSELECT
PRINTL