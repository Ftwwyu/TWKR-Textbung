@신캐릭터추가함수
#DIM CONST 현재캐릭수 = 136
#DIM C_ID

IF CHARANUM < 현재캐릭수
    FOR C_ID,0,CHARANUM ;일단 모브 삭제
        IF TALENT:C_ID:스침
            DELCHARA C_ID
            BREAK
        ENDIF
    NEXT
    FOR C_ID,1,CHARANUM ;CSV의 이름을 참고해 NO를 고친다
        VARSET LOCAL
        FOR LOCAL,1,캐릭터수상한
            SIF !EXISTCSV(LOCAL)
                CONTINUE
            IF NAME:C_ID == CSVNAME(LOCAL)
                NO:C_ID = LOCAL
                BREAK
            ENDIF
        NEXT
    NEXT

    FOR C_ID,1,캐릭터수상한 ;끝나면 새로운 캐릭터 추가
        IF GETCHARA(C_ID) == -1 && EXISTCSV(C_ID)
            ADDCHARA C_ID
            TARGET = C_ID
            RESULT = 0
        ENDIF
        IF TALENT:C_ID:스침
            RANDOM_CHARANUM = C_ID
            BREAK
        ENDIF
    NEXT

    FOR C_ID, 1, CHARANUM
        IF GETCHARA(C_ID) == -1
            CONTINUE
        ENDIF
        IF NO:C_ID != C_ID && NO:C_ID != 0 ;만약 어긋나면 다시 열거한다
            IF NO:C_ID > C_ID ;CSVNO가 지금의 NO보다 뒤라면 SWAP해서 계속
                SWAPCHARA C_ID,NO:C_ID
                FOR LOCAL,0,CHARANUM
                    SWAP RELATION:LOCAL:C_ID,RELATION:LOCAL:(NO:C_ID)
                NEXT
            ELSE
                SWAPCHARA C_ID,NO:C_ID ;그렇지 않으면 SWAP해서 처음부터 다시 나열한다
                FOR LOCAL,0,CHARANUM
                    SWAP RELATION:LOCAL:C_ID,RELATION:LOCAL:(NO:C_ID)
                NEXT
                C_ID = 1
            ENDIF
        ENDIF
        IF TALENT:C_ID:스침
            RANDOM_CHARANUM = C_ID
            BREAK
        ENDIF
    NEXT
ELSE
    RETURN
ENDIF