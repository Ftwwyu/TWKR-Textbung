@채무생성(ARG,ARG:1)
#DIM 대출량
#DIM 예상이율
#DIM 이율감소
#DIM 예상만기일
대출량 = ARG
예상이율 = 0
이율감소 = 0
예상만기일 = ARG:1
;---
FLAG:채무 += 대출량

IF FLAG:만기일 == 0
	예상이율 += 2
	예상이율 += MIN(20,MONEY/200000)
	예상이율 += MARK:69:반발각인
	예상이율 += FLAG:채무/100000
	SELECTCASE 예상만기일
		CASE 7 TO 14
			이율감소 += 3
		CASE 15 TO 21
			이율감소 += 2
		CASE 22 TO 28
			이율감소 += 1
		CASE 29 TO 35
			;그대로
		CASE 36 TO 42
			예상이율 += 1
		CASE 43 TO 49
			예상이율 += 2
		CASE 50 TO 56
			예상이율 += 3
		CASE 57 TO 63
			예상이율 += 4
		CASE 64 TO 90
			예상이율 += 5
		CASEELSE
			예상이율 += 6
	ENDSELECT
	IF TALENT:69:연인
		예상감소 += 7
	ENDIF
	IF 이율감소 > 예상이율
		예상이율 = 0
	ELSE
		예상이율 -= 이율감소
	ENDIF
	FLAG:만기일 = 예상만기일
	FLAG:이율 = 예상이율
ENDIF

@다누키은행
#DIM 한도
#DIM 한도감소
#DIM 대출량
#DIM 예상이율
#DIM 이율감소
#DIM 예상만기일
#DIM 상환
PRINTFORML 
PRINTFORMW 「머시여, 손님인감」
PRINTFORML 
$다누키인터페이스
한도 = 0
한도감소 = 0
대출량 = 0
예상이율 = 0
이율감소 = 0
예상만기일 = 0
상환 = 0
PRINTFORML 
PRINTFORML 「뭘 도와주까?」
PRINTLC  [1] 대출한다
PRINTLC  [2] 상환한다
PRINTFORML 
PRINTLC  [3] 입금한다
SIF DAY:3 < 5
	SETCOLOR C_GRAY
PRINTLC  [4] 출금한다
RESETCOLOR
PRINTFORML
PRINTLC [998] 설명
PRINTLC [999] 돌아간다
PRINTFORML 
INPUT
IF RESULT == 1
	PRINTFORML 「대출이라아...흐으음...어디보자」
	IF FLAG:채무 > 0
		IF FLAG:연체일 > 0
			PRINTFORML 「...그짝은 연체자라서 대출이 안되는디?」
			PRINTFORML 「아쉽지만 도와줄수 없갔구마이...잉...」
			GOTO 다누키인터페이스
		ENDIF
		PRINTFORML 「으잉? 자네는 이미 빚이 있잖여, 여기서 더 빌릴라꼬?」
		PRINTFORML 「감당할수 있겄어?」
		PRINTFORML 
	ELSE
	ENDIF
	;한도계산
	한도 = 0
	한도 += 20000000
	한도 += (MONEY * 2 /10)
	IF TALENT:69:연인
		한도 += 10000000
	ENDIF
	한도감소 += (MARK:69:반발각인) * 500000
	한도감소 += FLAG:채무
	;이율 산정
	예상이율 += 2
	예상이율 += MIN(20,MONEY/200000)
	예상이율 += MARK:69:반발각인
	예상이율 += FLAG:채무/500000
	IF TALENT:69:연인
		예상감소 += 7
	ENDIF
	IF 한도 < 한도감소
		PRINTFORMW 「...잉? 그짝은 더 빌릴수가 없다 나오는디?」
		PRINTFORMW 「하이구, 미안하게 되았서, 지금으로썬 빌려줄수가 읍겠어」
		PRINTFORMW 「다음 기회에 빌리러 오든가...잉...」
		RETURN 1
	ELSE
		한도 -= 한도감소
		PRINTFORML 「얼마나 빌릴라 카는디?」
		PRINTFORML 
		PRINTFORML    대출 한도 : {한도}
		PRINTFORML 
		PRINTFORML  얼마나 빌릴까...?(0이하의 값으로 취소)
		INPUT
		IF RESULT <= 0
			PRINTFORMW 「그만둘라꼬? 그려」
			GOTO 다누키인터페이스
		ELSEIF RESULT > 한도
			PRINTFORML 「한도 이상으론 못 줘」
			PRINTFORML 「한도까지만 할텨?」
			PRINTFORML  [1] 네
			PRINTFORML  [2] 아니오
			INPUT
			IF RESULT == 1
				PRINTFORML 「그려, 알았어」
				대출량 = 한도
			ELSE
				PRINTFORMW 「이잉...번거롭게시리...」
				GOTO 다누키인터페이스
			ENDIF
		ELSE
			대출량 = RESULT
			PRINTFORML 「{대출량}만큼 주면 되나?」
			PRINTFORML  [1] 네
			PRINTFORML  [2] 아니오
			INPUT
			IF RESULT == 1
				PRINTFORML 「그려, 알았어」
			ELSE
				PRINTFORMW 「이잉...번거롭게시리...」
				GOTO 다누키인터페이스
			ENDIF
		ENDIF
		IF FLAG:만기일 == 0&&FLAG:연체일 == 0&&FLAG:채무 == 0
			$다누키만기일설정
			PRINTFORML 「얼마동안 빌릴낀데?」
			PRINTFORML 「일주일부터 180일 까지 내키는대로 정하꼬 말혀」
			PRINTFORML  며칠 빌릴까?(7미만으로 취소)
			INPUT
			IF RESULT < 7
				PRINTFORML 「잉? 그만두게?」
				PRINTFORMW 「뭐어, 나중에 내키면 또 허면 되는기지」
				GOTO 다누키인터페이스
			ELSEIF RESULT >= 181
				PRINTFORMW 「하따, 180일이 한계라고 내가 말하지 않았는감?」
				GOTO 다누키만기일설정
			ELSE
				PRINTFORML 「{RESULT}일...잉...그럼 보자아....」
				SELECTCASE RESULT
					CASE 7 TO 21
						이율감소 += 3
					CASE 22 TO 36
						이율감소 += 2
					CASE 37 TO 44
						이율감소 += 1
					CASE 45 TO 51
						;그대로
					CASE 52 TO 63
						예상이율 += 1
					CASE 64 TO 75
						예상이율 += 2
					CASE 76 TO 88
						예상이율 += 3
					CASE 89 TO 100
						예상이율 += 4
					CASE 101 TO 115
						예상이율 += 5
					CASE 116 TO 130
						예상이율 += 6
					CASEELSE
						예상이율 += 7
				ENDSELECT
			ENDIF
			IF 대출량 > 20000000
				예상이율 += 2
			ENDIF
			IF 이율감소 > 예상이율
				예상이율 = 0
			ELSE
				예상이율 -= 이율감소
			ENDIF
			예상만기일 = RESULT
		ELSE
			PRINTFORML 「상환은 저번에 빌린거 갚는 날까지 허면 되는겨, 알았는감?」
			예상만기일 = FLAG:만기일
		ENDIF
		PRINTFORML 
		PRINTFORML    대출하는 금액 : {대출량}
		PRINTFORML    만   기   일 : {예상만기일}
		PRINTFORML    이        율 : {예상이율}
		PRINTFORML 
		PRINTFORML  빌립니까?
		PRINTFORML  [1] 예
		PRINTFORML  [2] 아니오
		INPUT
		IF RESULT == 1
			FLAG:채무 += 대출량
			MONEY += 대출량
			FLAG:이율 = 예상이율
			IF FLAG:만기일 == 0
				FLAG:만기일 = 예상만기일
			ENDIF
			PRINTFORML 「꼭 갚아야 혀~ 안 갚으면 큰일날줄 알어!」
			GOTO 다누키인터페이스
		ELSE
			PRINTFORML 「머여? 이제사 안빌린다꼬? 하이구...헛수고만 했구만...」
			GOTO 다누키인터페이스
		ENDIF
	ENDIF
ELSEIF RESULT == 2

	PRINTFORML 「갚는다꼬? 그래, 그짝 빚이...」
	IF FLAG:채무 == 0
		PRINTFORML 「...그짝은 빚이 읍는디?」
		PRINTFORML 「좋겠구마, 빚도 읍구, 잉...빚 없는게 좋은거시여...」
		GOTO 다누키인터페이스
	ELSE
		PRINTFORML 「흠흠...{FLAG:채무}...만큼 있구마」
		PRINTFORML 「그려, 얼마나 갚으려구?」
		PRINTFORML 
		PRINTFORML  현 재 자 금 : {MONEY}
		PRINTFORML  현 재 채 무 : {FLAG:채무}
		PRINTFORML  얼마나 갚을까...? (0이하 입력으로 취소)
		INPUT
		IF RESULT <= 0
			PRINTFORML 「잉? 지금은 아녀?」
			PRINTFORML 「뭐, 천천히 갚든가 맘대로 혀, 다만 제때 못갚으면 큰일나는건 그짝이니까」
			PRINTFORML 「항상 잊으면 안되는겨...」
			GOTO 다누키인터페이스
		ELSEIF RESULT > MONEY
			상환 = MONEY
			IF FLAG:채무 < 상환
				PRINTFORML 「보자...석삼 너시기...어이쿠, 몇장 흘렸구만」
				;무지성으로 큰 숫자 쑤셔넣으면 10%를 긴빠이 한다, 무서움!
				상환 -= 상환 / 10
			ENDIF
			MONEY -= 상환
		ELSEIF RESULT >= FLAG:채무
			IF MONEY >= FLAG:채무
				MONEY -= FLAG:채무
				FLAG:채무 = 0
				FLAG:만기일 = 0
				FLAG:연체일 = 0
				PRINTFORML 「잉? 빚을 다 갚았구마이...축하혀, 그짝은 이쟈 빚의 노예가 아녀...」
				PRINTFORML 「워뗘, 빚 없는 몸이 되니 편한감?」
				GOTO 다누키인터페이스
			ELSE
				상환 = MONEY
				MONEY = 0
			ENDIF
		ELSE
			상환 = RESULT
			MONEY -= 상환
		ENDIF
		FLAG:채무 -= 상환
		IF FLAG:채무 <= 0
			FLAG:채무 = 0
			FLAG:만기일 = 0
			FLAG:연체일 = 0
			PRINTFORML 「잉? 빚을 다 갚았구마이...축하혀, 그짝은 이쟈 빚의 노예가 아녀...」
			PRINTFORML 「워뗘, 빚 없는 몸이 되니 편한감?」
			GOTO 다누키인터페이스
		ELSE
			PRINTFORML 「보자, {상환}만큼 갚았으니 이제...」
			PRINTFORML 「{FLAG:채무}만큼 남았구마, 힘내서 퍼뜩 갚아야 되겠구마...」
			PRINTFORML 「뭐어, 힘내쇼잉?」
			GOTO 다누키인터페이스
		ENDIF
	ENDIF
ELSEIF RESULT == 3
	CALL 다누키_입금
ELSEIF RESULT == 4
	CALL 다누키_출금
ELSEIF RESULT == 998
	CALL 다누키설명
ELSEIF RESULT == 999
	PRINTFORML 「그려, 조심히 들어가-」
	RETURN 1
ELSE
	GOTO 다누키인터페이스
ENDIF
GOTO 다누키인터페이스


@다누키_입금
#DIM DYNAMIC 입금액
PRINTFORML 「얼마를 넣을겨?」
PRINTFORML 현재 잔액 : {MONEY}￦
PRINTFORML 잔　 　고 : {은행잔고}￦
PRINTFORML ※주의 : 은행 잔고는 출금하기 전까진 사용할 수 없습니다
PRINTFORM 넣을 금액(0 이하의 수로 취소) :
INPUT
IF RESULT <= 0
	PRINTFORMW 「켁, 넣기 싫다고? 변덕이 참 심하구마이」
	RETURN
ELSEIF MONEY < RESULT
	PRINTFORML 「돈이 모자란디?」
	PRINTFORML 「가진돈 전부를 넣을겨?」
	CALL ASK_YN
	IF RESULT
		RETURN
	ELSE
		입금액 = MONEY
	ENDIF
ELSE
	입금액 = RESULT
ENDIF
MONEY -= 입금액
PRINTFORMW 「{입금액}원 입금, 제대로 했으야」
은행잔고 += 입금액

@다누키_출금
#DIM DYNAMIC 출금액
IF DAY:3 < 5
	PRINTFORML 「거시기, 스키마 놈들이 점검을 한답시고 돈을 못 빼게 하고 있걸랑」
	PRINTFORML 「뭐 장부 정리를 하네 회계를 하네 기기점검을 하네...하여튼 바쁜가벼!」
	PRINTFORMW 「{5-DAY:3}일 뒤엔 다 끝난다 하니 그때쯤 다시 오면 되겠구마」 
ELSE
	PRINTFORML 「얼마를 꺼낼겨?」
	PRINTFORML 현재 잔액 : {MONEY}￦
	PRINTFORML 잔　 　고 : {은행잔고}￦
	PRINTFORM 꺼낼 금액(0 이하의 수로 취소) :
	INPUT
	IF RESULT <= 0
		PRINTFORMW 「켁, 꺼내기 싫다고? 변덕이 참 심하구마이」
		RETURN
	ELSEIF 은행잔고 < RESULT
		PRINTFORML 「돈이 모자란디?」
		PRINTFORML 「가진돈 전부를 꺼낼겨?」
		CALL ASK_YN
		IF RESULT
			RETURN
		ELSE
			출금액 = 은행잔고
		ENDIF
	ELSE
		출금액 = RESULT
	ENDIF
	MONEY += 출금액
	PRINTFORMW 「{출금액}원 출금, 제대로 했으야」
	은행잔고 -= 출금액
ENDIF

@다누키설명
PRINTFORML ┌────────────────────────────────────────────┐
PRINTFORML │다누키은행에　오신것을　환영한당께　　　　　　　　　　　　　　　　　　　　　　　　　　　│
PRINTFORML │우덜　은행에선　대출과　입출금을　할수　있으야　　　　　　　　　　　　　　　　　　　　　│
PRINTFORML │대출은　그짝의　소지금과　잔여　부채를　통틀어서　평가해서　　　　　　　　　　　　　　　│
PRINTFORML │그에　맞는　한도까지　마음껏　빌릴수　있당께　　　　　　　　　　　　　　　　　　　　　　│
PRINTFORML │이율은　기간에　비례해　증가하고　매일　부과　된당께　　　　　　　　　　　　　　　　　　│
PRINTFORML │대신에　기한안에　갚기만　하면　묻지도　따지지도　않고　빌려주니　많이　이용하랑께　　　│
PRINTFORML │입출금은　스키마사와　협업해서　돌아가는　건디　　　　　　　　　　　　　　　　　　　　　│
PRINTFORML │그짝이　돈을　넣으면　그　돈으로　스키마쪽에서　바깥세계에다　투자를　해서　수익을　내고│
PRINTFORML │그　수익금의　일부를　그짝에게　주는　것이여　　　　　　　　　　　　　　　　　　　　　　│
PRINTFORML │자세히　설명하는걸　듣자니　머리가　터질거　같지만　요점은　　　　　　　　　　　　　　　│
PRINTFORML │돈을　넣으면　다달히　이자를　그짝에　주지만　　　　　　　　　　　　　　　　　　　　　　│
PRINTFORML │운이　없으면　돈이　싸그리　날아간다　이말이여　그　책임은　우덜한테　없당께　　　　　　│
PRINTFORML │헌디　이럼　저축이　아니라　펀드에　가까운거　아녀？　난잘모르겠으야　　　　　　　　　　│
PRINTFORML │하여튼　입금과　출금은　언제든지　할수있지만　　　　　　　　　　　　　　　　　　　　　　│
PRINTFORML │월초에는　스키마놈들이　점검을　하니께　돈을　꺼낼수　읎어　　　　　　　　　　　　　　　│
PRINTFORML │최소한　닷새동안은　못꺼낸다고　보면　된당께　　　　　　　　　　　　　　　　　　　　　　│
PRINTFORML │월말에　있는　돈으로만　이자를　주니　알고있으랑께　　　　　　　　　　　　　　　　　　　│
PRINTFORML └────────────────────────────────────────────┘
PRINTW