@외출처로부터귀가
#DIM DIST_TO_HOME
#DIM ReqTSP
#DIM 道迷い時間
#DIMS 低気圧位置

低気圧位置 = %GET_LOCATION_LOW()%

ReqTSP = TIME_REQUIRED(MAIN_MAP,CFLAG:MASTER:데이트중) * 3
IF !FLAG:데이트상대 && !FLAG:70 && BASE:MASTER:TSP >= ReqTSP
	PRINTL TSP를 소비하여 시간 경과 없이 귀가할 수 있습니다.
	PRINTL TSP를 사용하겠습니까?
	CALL ASK_YN
	IF !RESULT
		IF BASE:MASTER:TSP < ReqTSP
			PRINTW TSP가 부족했다
		ELSE
			BASE:MASTER:TSP -= ReqTSP
			GOTO WARP
		ENDIF
	ENDIF
ENDIF
IF !FLAG:70
	;(天候パッチ)連続降水カウンター20以上で土砂崩れに巻き込まれる可能性
	IF CONTINUOUS_RAINFALL >= 20 && RAND:(MAX(2, (40 - CONTINUOUS_RAINFALL) / 2)) == 0
		PRINTFORML …길을 걷고 있는데 산 고지대에서 이상한 소리가 들려왔다
		PRINTFORMW ！！　산사태다！！
		
		PRINTFORML 
		PRINTFORML %마스터는% 토사에 휩쓸리면서도
		PRINTFORML 가까스로 탈출하는데 성공했다…
		PRINTL 
		CALL ASK_DIARY("산사태와 조우했다")
		BASE:MASTER:체력 = 0
		
		GOTO WARP2
	ENDIF
	;(天候パッチ)猛烈な台風の暴風域にいるとき飛来物に巻き込まれる可能性 颶風時には確率上昇
	IF (低気圧位置 == "台風接近暴風域" || 低気圧位置 == "台風通過暴風域") && IS_TYPHOON:1 >= 5 && (RAND:20 == 0 || (WIND_VELOCITY >= 3 && RAND:3 == 0))
		PRINTFORML …길을 걷고 있는데 무언가가 이쪽으로 날아왔다
		PRINTFORMW ！！　기와가 날아온다！！
		
		PRINTFORML 
		PRINTFORML 폭풍에 날려 버려진 기와가 %마스터에게% 직격했다…
		PRINTL 
		CALL ASK_DIARY("비래물에 직격했다")
		BASE:MASTER:체력 = 0
		
		GOTO WARP3
	ENDIF
	;(天候パッチ)雪崩発生フラグが立っているとき雪崩に巻き込まれる可能性
	IF POSSIBLITY_AVALANCHE == 1 && POWER_COLD_AIR >= 3 && RAND:((6 - POWER_COLD_AIR) * 10) == 0
		PRINTFORML …길을 걷고 있는데 산 고지대에서 무언가가 덮쳐왔다
		PRINTFORMW ！！　눈사태다！！
		
		PRINTFORML 
		PRINTFORML %마스터는% 눈사태에 휘말리면서도
		PRINTFORML 가까스로 탈출하는데 성공했다…
		PRINTL 
		
		POSSIBLITY_AVALANCHE = 0
		CONTINUOUS_SNOWFALL = 0
		
		CALL ASK_DIARY("눈사태와 조우했다")
		BASE:MASTER:체력 = 0
		
		GOTO WARP4
	ELSEIF POSSIBLITY_AVALANCHE == 2 && POWER_WARM_AIR >= 2 && RAND:((6 - POWER_WARM_AIR) * 10) == 0
		PRINTFORML …길을 걷고 있는데 산 고지대에서 무언가가 덮쳐왔다
		PRINTFORMW ！！　눈사태다！！
		
		PRINTFORML 
		PRINTFORML %마스터는% 눈사태에 휘말리면서도
		PRINTFORML 가까스로 탈출하는데 성공했다…
		PRINTL 
		
		SNOW_COVERAGE = 0
		CONTINUOUS_SNOWFALL = 0
		SNOW_COVERAGE = 0
		
		CALL ASK_DIARY("눈사태와 조우했다")
		BASE:MASTER:체력 = 0
		
		GOTO WARP5
	ENDIF
	;(天候パッチ)暴風雪発生時にホワイトアウト発生の可能性 颶風時には確率上昇
	IF TIME:5 == 9 && ((WIND_VELOCITY == 2 && RAND:3 == 0) || WIND_VELOCITY >= 3)
		道迷い時間 = 60 + RAND:120
		PRINTFORML 쏟아지는 눈이 맹렬한 바람에 몰아쳐
		PRINTFORMW 눈 앞이 새하얘졌다！
		PRINTFORML 
		PRINTFORML ……길을 잃고 {道迷い時間}분 쓸데없이 걸어 버렸다
		TIME:0 += 道迷い時間
		BASE:MASTER:체력 = MAX(BASE:MASTER:체력 - (20 * 道迷い時間), 0)
		
		GOTO WARP6
	ELSEIF TIME:5 == 8 && ((WIND_VELOCITY == 2 && RAND:10 == 0) || WIND_VELOCITY >= 3)
		道迷い時間 = 30 + RAND:60
		PRINTFORML 쏟아지는 눈이 맹렬한 바람에 몰아쳐
		PRINTFORMW 눈 앞이 새하얘졌다！
		PRINTFORML 
		PRINTFORML ……길을 잃고 {道迷い時間}분 쓸데없이 걸어 버렸다
		TIME:0 += 道迷い時間
		BASE:MASTER:체력 = MAX(BASE:MASTER:체력 - (15 * 道迷い時間), 0)
		
		GOTO WARP7
	ENDIF
	CALL 외출이벤트
	$WARP2
	$WARP3
	$WARP4
	$WARP5
	$WARP6
	$WARP7
	CALL 귀가의시간경과
ELSEIF BASE:MASTER:TSP < ReqTSP
	PRINTW TSP가 부족하기 때문에 시간을 멈춘 상태로는 돌아갈 수 없다
	RETURN -1
ELSE
	BASE:MASTER:TSP -= ReqTSP
ENDIF

$WARP
IF CFLAG:패배자 && TALENT:스침 == 1
	PRINTFORML %타겟을% 데리고 돌아왔습니다
ELSEIF BASE:MASTER:체력 <= 0
	PRINTFORML %마스터는% 마지막 있는 힘을 다해、%GET_MAPNAME(MAIN_MAP)%까지 돌아왔다…
	PRINTFORML 하지만 %NAME_FROM_PLACE(ENTRANCE)%에 간신히 도착하자마자 의식을 잃었다…
	PRINTFORMW 
ELSE
	PRINTFORMW \@ CFLAG:MASTER:데이트중 != CFLAG:TARGET:데이트중 || TARGET == MASTER ?  외출 # 데이트 \@에서 돌아왔습니다
ENDIF
CALL 모리야복권데이트상대의리셋()
FOR LOCAL, 1, CHARANUM
	IF CHK_DATENOW(CFLAG:LOCAL:데이트중)
		CALL DATE_EVENT(LOCAL)
		CALL SET_DATE(99, LOCAL)
	ENDIF
NEXT
SIF CHK_DATENOW(CFLAG:MASTER:데이트중)
	CALL SET_DATE(99, MASTER)
TFLAG:외출중 = 0				   

CFLAG:MASTER:현재위치 = GET_ENTRANCE(MAIN_MAP)

@귀가의시간경과
;남은 시간 산출
IF TFLAG:데이트도중
	;도중의 경우
	TIME = TIME + TIME_REQUIRED(MAIN_MAP, CFLAG:MASTER:데이트중) - TFLAG:데이트도중
ELSE
	;데이트처 도착 끝난 경우
	TIME = TIME + TIME_REQUIRED(MAIN_MAP, CFLAG:MASTER:데이트중)
ENDIF
CALL 모리야복권데이트상대의리셋()

@외출처로부터권할수있을까(ARG)
LOCAL = FLAG:데이트상대
IF CFLAG:ARG:행동 == 5
	PRINTFORMW %ARG는% 지금 일하는 중이다
	RETURN 0
ENDIF
IF LOCAL != ARG && LOCAL != MASTER
	PRINTFORM %조사처리(CALLNAME:LOCAL,"가")% %CALLNAME:MASTER%
	;심약계
	IF CFLAG:LOCAL:성격경향 == 1
		PRINTFORML 의 옷의 옷자락을 잡아 거절하고 있다
	;강경세계
	ELSEIF CFLAG:LOCAL:성격경향 == 2
		PRINTFORML %조사만처리(CALLNAME:MASTER,"를")% 번쩍빛나는 눈으로 응시해 온다
	;솔직함계
	ELSEIF CFLAG:LOCAL:성격경향 == 3
		PRINTFORML %조사만처리(CALLNAME:MASTER,"를")% 응시하면서 나만을 보았으면 좋겠다고 알려온다
	;진면목계
	ELSEIF CFLAG:LOCAL:성격경향 == 4
		PRINTFORML 에게 그것은 안 된다며 참견해 온다
	;그 외
	ELSEIF CFLAG:LOCAL:성격경향 == 5
		PRINTFORML 에게 한숨을 내쉬고, 고개를 저었다
	ENDIF
	PRINTFORMW %마스터는% %ARG를% 권하는 것을 단념했다
	RETURN 0
ENDIF
IF !VISIT(ARG)
	PRINTFORMW 지금은 안된다고 들었다
	RETURN 0
ENDIF
IF ARG == 93 && !ITEM:간이풀장
	PRINTW 그녀는 데리고 갈 수 없다…
	RETURN 0
ELSEIF BASE:ARG:기력 < 500
	PRINTFORMW %ARG는% 피곤한 것 같다
	RETURN 0
ELSEIF !CHARA_HOLIDAY(ARG) && BASE:ARG:일량 && TCVAR:ARG:일개시 < TIME + 180
	PRINTFORMW %ARG는% 지금부터 일이 있다
	RETURN 0
ELSE
	CFLAG:ARG:거점내방 = 1
	RETURN 1
ENDIF


;FileName_DATE_CMN.ERB ------------------------------- Rev1. 00
;데이트 신청할 수 있을까
;-----------------------------------------------------------
@CAN_DATE(ARG)
#FUNCTION

SIF !CHK_DATENOW(CFLAG:MASTER:데이트중)
	RETURNF 0
SIF !SHIRAHU(ARG)
	RETURNF 0
SIF CFLAG:MASTER:현재위치 == OMANEKIBEYA()
	RETURNF 0
IF !CFLAG:ARG:패배자
	SIF !CFLAG:ARG:태도 && TARGET != RANDOM_CHARANUM
		RETURNF 0
	SIF CFLAG:ARG:빡침
		RETURNF 0
ENDIF
;잠복モード中は不可
SIF GETBIT(FLAG:잠복,0)
	RETURNF 0
SIF CHK_DATENOW(CFLAG:ARG:데이트중)
	RETURNF 0
SIF (!TALENT:TARGET:사모 && !TALENT:TARGET:연모 && !TALENT:TARGET:애욕 && TARGET != RANDOM_CHARANUM)
	RETURNF 0
RETURNF 1

