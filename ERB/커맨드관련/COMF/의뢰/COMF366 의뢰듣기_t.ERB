;-------------------------------------------------
;실행 판정
;-------------------------------------------------
@COM_ABLE366
SIF !TFLAG:100
	RETURN 0
SIF TARGET <= 0
	RETURN 0
;시간정지/수면/동침/우후후 안은 불가
SIF SHIRAHU(TARGET) != 1
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(366)
	RETURN RESULT

SELECTCASE IRAI_CHARA_TO_PROGRESS(TARGET)
CASE "의뢰미수탁"
;빈슬롯이 없으면 불가
	SIF !SEARCH_IRAI_EMPTY()
		RETURN 0
;개개로 지정된 조건을 충족시키지 않을 때
	SIF !INT_DATA_IRAI(CFLAG:TARGET:의뢰내용, "의뢰 수탁 가능 조건", TARGET)
		RETURN 0
CASE "의뢰중"
;의뢰 확인/파기가 가능
CASE "의뢰 달성"
;성공 보고 가능
CASE "기한 마감", "보고 잊음", "의뢰 실패"
;실패 보고
CASE "의뢰 종료(성공)", "의뢰 종료(기한 마감)", "의뢰 종료(보고 잊음)", "의뢰 종료(실패)"
;여기는 의뢰 플래그의 소멸 기다려 어째서 손대지 않는다
		RETURN 0
CASEELSE
		RETURN 0
ENDSELECT
RETURN 1


@COM366
SELECTCASE IRAI_CHARA_TO_PROGRESS(TARGET)
CASE "의뢰미수탁"
	CALL IRAI_START(TARGET)
	RETURN RESULT
CASE "의뢰중"
	CALL IRAI_LOOK(TARGET)
	RETURN RESULT
CASE "의뢰 달성", "기한 마감", "보고 잊음", "의뢰 실패"
	CALL IRAI_REPORT(TARGET)
	RETURN 1
ENDSELECT



;-------------------------------------------------
;의뢰 수탁
;21/08/25 수락시 일부 의뢰내용을 표시하도록 변경
;-------------------------------------------------
@IRAI_START(CHARA)
#DIM CHARA
#DIM 수락		;수탁 의뢰가 들어온 슬롯 번호
#DIM 의뢰ID	;의뢰 자체 ID
;의뢰 제시시
PRINTFORMW 아무래도 %조사처리(CALLNAME:CHARA,"는")% %CALLNAME:MASTER%에게 부탁할 일이 있는 것 같다
CALL SHOW_IRAI_MESSAGE(CHARA, CFLAG:CHARA:의뢰내용, "의뢰 제시시")
CALL SHOW_IRAI_DATA(CFLAG:CHARA:의뢰내용, 0)
CALL ASK_YN("의뢰를 받는다", "거절한다")
IF RESULT
	PRINTFORML %마스터는% 정중하게 거절했다
	CALL SHOW_IRAI_MESSAGE(CHARA, CFLAG:CHARA:의뢰내용, "의뢰비수탁시")
	PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 유감스러운 얼굴을 하고 있다
	RETURN -1
ELSE
	수락 = SEARCH_IRAI_EMPTY()
	CALL SET_IRAI_SLOT(CHARA)
	의뢰ID = (CFLAG:CHARA:의뢰내용) % 1000
	PRINTFORML %마스터는% %CALLNAME:CHARA%의 의뢰를 받았다
	SELECTCASE IRAI_SLOT_TO_TASKNAME(수락)
		CASE "전령(인물)"
			;固有依頼は提示済なのでカット
			SIF 의뢰ID < 100
				PRINTFORML %CALLNAME:(IRAI_SLOT_TO_PERSON(수락))% 앞으로 온 편지를 받았다
		CASE "수색(인물)"
			IF 의뢰ID == 9
				PRINTFORML %CALLNAME:CHARA%로부터 분실물을 맡겨졌다
				PRINTFORM 물건의 특징：
			ELSE
				PRINTFORM 단서：
			ENDIF
			CALL SHOW_IRAI_HINT("수색(인물)", IRAI_SLOT_TO_PERSON(수락), IRAI_SUB:수락)
		CASE "수색(장소)"
			PRINTFORM 단서：
			CALL SHOW_IRAI_HINT("수색(장소)", IRAI_SLOT_TO_PLACE(수락), IRAI_SUB:수락)
		CASE "토벌"
			PRINTFORML 탄막 승부의 상대는 %CALLNAME:(IRAI_SLOT_TO_PERSON(수락))%(이)다
	ENDSELECT
	WAIT
	CALL SHOW_IRAI_MESSAGE(CHARA, CFLAG:CHARA:의뢰내용, "의뢰 수탁시")
	RETURN 1
ENDIF



;-------------------------------------------------
;의뢰 확인/파기
;-------------------------------------------------
@IRAI_LOOK(CHARA)
#DIM CHARA
#DIM CHK_SLOT
CHK_SLOT = SEARCH_IRAI_SLOT(CFLAG:CHARA:의뢰내용)
CALL SHOW_IRAI_MESSAGE(CHARA, CFLAG:CHARA:의뢰내용, "의뢰 확인시")
CALL SHOW_IRAI_DATA(CFLAG:CHARA:의뢰내용, CHK_SLOT)
CALL ASK_YN("확인", "파기")
SIF !RESULT
	RETURN -1
PRINTFORML 정말로?
CALL ASK_YN
SIF RESULT
	RETURN -1
CALL SHOW_IRAI_MESSAGE(CHARA, CFLAG:CHARA:의뢰내용, "의뢰 파기시")
PRINTFORMW 의뢰를 파기했습니다.
CALL CLEAR_IRAI_SLOT(CHARA, CHK_SLOT, "의뢰 종료(실패)")



;-------------------------------------------------
;의뢰 보고
;-------------------------------------------------
@IRAI_REPORT(CHARA)
#DIM CHARA
#DIM CHK_SLOT
CHK_SLOT = SEARCH_IRAI_SLOT(CFLAG:CHARA:의뢰내용)
;의뢰 성공시
;의뢰 실패시
SELECTCASE IRAI_CHARA_TO_PROGRESS(CHARA)
CASE "의뢰 달성"
	CALL SHOW_IRAI_MESSAGE(CHARA, CFLAG:CHARA:의뢰내용, "성공 보고시")
	CALL IRAI_CARRYOUT(CHARA, CFLAG:CHARA:의뢰내용, "의뢰 성공시")
CASE "기한 마감", "보고 잊음", "의뢰 실패"
	CALL SHOW_IRAI_MESSAGE(CHARA, CFLAG:CHARA:의뢰내용, "실패 보고시")
	CALL IRAI_CARRYOUT(CHARA, CFLAG:CHARA:의뢰내용, "의뢰 실패시")
ENDSELECT

SELECTCASE IRAI_CHARA_TO_PROGRESS(CHARA)
CASE "의뢰 달성"
	RESULT = 악보입수_특수조건(악보입수방법：요정의뢰성공)
	PRINTL
	SELECTCASE CHARA
	CASE 5,6,7,13,14
		RESULT = 악보입수_특수조건(악보입수방법：요정의뢰성공)
	CASE 29
		RESULT = 악보입수_특수조건(악보입수방법：아야의뢰성공)
	CASE 42
		RESULT = 악보입수_특수조건(악보입수방법：하타테의뢰성공)
	ENDSELECT
CASE "의뢰 실패"
	IF (RAND:(100) < 30)
		PRINTL
		RESULT = 악보입수_특수조건(악보입수방법：의뢰실패시)
	ENDIF
ENDSELECT

SELECTCASE IRAI_CHARA_TO_PROGRESS(CHARA)
CASE "의뢰 달성"
	CALL CLEAR_IRAI_SLOT(CHARA, CHK_SLOT, "의뢰 종료(성공)")
CASE "기한 마감"
	CALL CLEAR_IRAI_SLOT(CHARA, CHK_SLOT, "의뢰 종료(기한 마감)")
CASE "보고 잊음"
	CALL CLEAR_IRAI_SLOT(CHARA, CHK_SLOT, "의뢰 종료(보고 잊음)")
CASE "의뢰 실패"
	CALL CLEAR_IRAI_SLOT(CHARA, CHK_SLOT, "의뢰 종료(실패)")
ENDSELECT
