;**************************************************************
;積み石コンテストver0.1です
;受付台詞は瓔花にするか名無しの水子にするか迷って微妙な感じになりました
;色々ミスがあったりバランス調整も適当で構文も変…と酷い出来なので直してくださるとうれしいです
;作者が書いてる分は改変自由で作者への連絡も不要です
;作者:Cineraria
;**************************************************************
@COM633
#DIM WIN_P
PRINTL 「돌쌓기 경연에 오신 것을 환영합니다! 어느 급에 도전할래? 참가비는 일률적으로 \3000으로 경연에 참가할 수 있어」
$LOOP
DRAWLINE
{
CALL ASK_M("EASY-집 짓기 놀이급-(쉽게 쌓을 수 있는 난이도입니다.)",1,"NORMAL-젠가급-(방심하면 무너지는 난이도입니다)", 1,
"HARD-트럼프 타워급-(첫 번째 돌 쌓는 것도 긴장되는 난이도입니다)",TALENT:MASTER:돌쌓기랭커 >= 1,
"LUNATIC-피라미드급-(돌이라도 던지지 않으면 해낼 수 없습니다)", TALENT:MASTER:돌쌓기랭커 >= 2,
"PHANTASM-바벨탑급-(돌을 던져도 부족합니다)", TALENT:MASTER:돌쌓기랭커 >= 3,
"EXTRA-인간 피라미드급-(한 분은 사양하겠습니다)", CHK_DATENOW(CFLAG:12),
"HELP",1, "그만둔다",1)
}
IF RESULT == 0
	PRINTFORMW 「집 짓기 놀이급에 도전이네! 두 번 승리하면 우승이야!」
	CALL Stone_Entry(0, 2, 5000)
ELSEIF RESULT == 1
	PRINTFORMW 「젠가급에 도전이네! 이제 돌쌓기에도 익숙해졌나? 세 번 승리하면 우승이야!」
	CALL Stone_Entry(1, 3, 7000)
ELSEIF RESULT == 2
	PRINTFORMW 「트럼프 타워급에 도전이네! 모두 당신을 주목하고 있어! 네 번 승리하면 우승이야!」
	CALL Stone_Entry(2, 4, 10000)
ELSEIF RESULT == 3
	PRINTFORMW 「피라미드급에 도전! 드디어 여기까지 왔네! 다섯 번 승리하면 우승이야!」
	CALL Stone_Entry_EX(3, 5, 15000)
ELSEIF RESULT == 4
	PRINTFORMW 「바벨탑급에 도전!? 환상이라고 불리는 이 계급에 도전하는거야!? 힘내! 다섯 번 승리하면 우승이야!」
	CALL Stone_Entry_EX(4, 5, 30000)
ELSEIF RESULT == 5
	MONEY -= 3000
	TIME += 60
	WIN_P = 0
	PRINTFORMW 「인간 피라미드급에 도전! 아직 임시 실장이지만 그래도 괜찮다면!」
	PRINTFORMW 「어디 보자… 이번 대전 상대는…」
	CALL TAGMATCH
	IF RESULT == 0
		WIN_P = 1
	ELSE
		WIN_P = 0
	ENDIF
	IF WIN_P == 1
		PRINTFORMW
		PRINTFORMW 「축하합니다! 상금 \5000을 받으세요!」
		PRINTFORMW 「또 두 명이서 놀러와!」
		MONEY += 5000
		RETURN 0
	ELSE
		PRINTFORMW
		PRINTFORMW 「졌으니까 여기서 끝… 수고했어. 또 둘이서 참가해!」
		RETURN 0
	ENDIF
	RETURN 0
ELSEIF RESULT == 6
	CALL Stone_Help
	GOTO LOOP
ELSEIF RESULT == 7
	RETURN -1
ENDIF
RETURN 1

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
;石積みコンテスト
@COM_ABLE633
;一括管理
SIF GLOBAL_COMABLE(633)
	RETURN RESULT
SIF !INRANGE(TIME, 540, 1020)
	RETURN 0

IF !AT_HOME(MASTER)
	SIF CFLAG:MASTER:현재위치 != 삼도천
		RETURN 0
ELSE
	;冥界が拠点の場合は、賽の河原
	SIF CFLAG:MASTER:현재위치 != 602
		RETURN 0
ENDIF
;睡眠中
SIF CFLAG:수면
	RETURN 0
;時間停止中
SIF FLAG:70
	RETURN 0
RETURN 1

@Stone_Help
{
CALL ASK_M("돌쌓기 경연이란？",1,"난이도에 대해서 그 첫번째",1,"난이도에 대해서 그 두번째",1,"우승상금이 얼마인지 알려줘",1,
"이길 수가 없어요. 무엇을 단련해야 돼요?",1, "이 경연은 언제 개최해?",1, "돌아간다",1)
}
SELECTCASE RESULT
	CASE 0
		PRINTFORML 【돌쌓기 경연이란？】
		PRINTFORML 에비스 에이카가 주최하는 돌쌓기에 대한 여러가지 것들을 겨루는 경연대회입니다
		PRINTFORML 여러 부문으로 나뉘고 있습니다만… 당신이 참가할 수 있는 것은 빨리 쌓기 부문뿐입니다
		PRINTFORML 빨리 쌓기 부문은 제한시간이 종료될 때 쌓은 돌의 수를 겨루는 부문입니다. 열심히 해주세요
		PRINTFORMW 또한, 자신이 가진 능력은 사용해서는 안 된다는 규칙이 있습니다
		PRINTFORMW
	CASE 1
		PRINTFORML 【난이도에 대해서 그 첫번째】
		PRINTFORML 빨리 쌓기 부문은 많은 참가자들이 있으며, 그 기량에 따라 계급이 배정됩니다
		PRINTFORML 처음에는 EASY밖에 참가할 수 없지만 우승하면 다음 난이도에 도전할 수 있게 됩니다
		PRINTFORML 난이도에 따라 상금도 호화로워집니다
		PRINTFORMW 참가비는 일률적으로 \3000입니다. 분발해서 참가해 주십시오
		PRINTFORMW
	CASE 2
		PRINTFORML 【난이도에 대해서 그 두번째】
		PRINTFORML 난이도에 따라 우승까지 싸우는 횟수도 달라집니다
		PRINTFORML EASY에서는 2번 이기면 우승이지만 NORMAL에서는 3번, HARD에서는 4번… 이런 식으로 늘어납니다
		PRINTFORML 아직 미실장의 데이트중에만 참가할 수 있는 등급은 단 한 번의 승리로 상품을 받게 됩니다
		PRINTFORMW 노려라! 돌쌓기 마스터!
		PRINTFORMW
	CASE 3
		PRINTFORML 【우승상금이 얼마인지 알려줘】
		PRINTFORML EASY - ￥5000
		PRINTFORML NORMAL - ￥7000
		PRINTFORML HARD - ￥10000
		PRINTFORML LUNATIC - ￥15000
		PRINTFORMW ????? - ￥30000
		PRINTFORMW
	CASE 4
		PRINTFORML 【이길 수가 없어요. 무엇을 단련해야 돼요?】
		PRINTFORML 손재주가 있는 사람일수록 많이 쌓을 수 있습니다
		PRINTFORML 기교·손가락 기술은 물론, 조합 등의 손가락을 사용하는 기능을 단련하면 많이 쌓을 수 있습니다
		PRINTFORML 또한, 몇 가지 가지고 있으면 유리해지는 소질도 존재하고 있습니다
		PRINTFORMW 참고로 욕망의 수치가 높으면 조금 불리해집니다. 돌을 쌓는 일에는 욕망이 필요없습니다
		PRINTFORMW
	CASE 5
		PRINTFORML 【이 경연은 언제 개최해?】
		PRINTFORML 9:00 ~ 17:00까지 입니다
		PRINTFORML 그 시간에 일이 있을 텐데도 경연에 참가하는 아이가 몇몇 있습니다만……
		PRINTFORML 뭐 신경쓰지 마세요
		PRINTFORMW 장래적으로 요일을 한정하거나 하루 한 번으로 할 수도 있을지도?
		PRINTFORMW
	CASE 6
		RETURN 0
ENDSELECT
RESTART

@Stone_Entry(Rank, MaxBattle, Prize)
#DIM Rank
#DIM MaxBattle
#DIM Prize
#DIM WIN_P
#DIM difficulty

WIN_P = 0
difficulty = Rank
MONEY -= 3000
TIME += 60
FOR WIN_P, 0, MaxBattle
	IF WIN_P > 0
		PRINTFORMW
		PRINTFORMW 「{WIN_P}연승 축하해! 여기서 그만둘 수도 있는데 어떻게 할 거야? 참가비만큼은 돌려줄 건데?」
		CALL ASK_YN("계속한다","그만둔다")
		IF RESULT
			PRINTFORMW 「그렇구나… 또 참가해줘」
			MONEY += 3000
			RETURN -1
		ELSE
			PRINTFORMW 「그렇게 나와야지!」
		ENDIF
	ENDIF
	PRINTFORMW 「어디 보자… 제 {WIN_P + 1}회전 그 상대는…」
	CALL STONELEVEL(difficulty)
	IF RESULT == 1
		PRINTFORMW 「졌으니까 여기서 끝… 수고했어. 또 참가해줘!」
		RETURN 0
	ELSE
		difficulty ++
	ENDIF
NEXT
PRINTFORMW 「우승 축하해! 상금 ￥{Prize}을 받아!」
IF TALENT:MASTER:돌쌓기랭커 > Rank
	PRINTFORML 「이것보다 윗급에서도 통하는 거 아니야?」
ELSE
	PRINTFORML 「그리고 젠가급 첫 우승의 당신에게는… 새로운 칭호를 선물!」
	TALENT:MASTER:돌쌓기랭커 = Rank + 1
ENDIF
PRINTFORMW 「또 참가해~」
MONEY += Prize
RETURN 0

@Stone_Entry_EX(Rank, MaxBattle, Prize)
#DIM Rank
#DIM MaxBattle
#DIM Prize
#DIM WIN_P
#DIM difficulty
TIME += 60
WIN_P = 0
MONEY -= 3000
IF Rank == 3
	difficulty = 5
ELSEIF Rank == 4
	difficulty = 6
ENDIF

FOR WIN_P, 0, MaxBattle
	IF WIN_P > 0
		PRINTFORMW
		;決勝前
		IF WIN_P == MaxBattle - 1
			PRINTFORMW 「네 번째 판 승리 축하해! 다음 상대는 최강이니까 정신 바짝 차리고 있어! 여기서 그만둘 수도 있는데 어떻게 할 거야? 참가비만큼은 돌려줄 건데?」
		ELSE
			PRINTFORMW 「{WIN_P}연승 축하해! 여기서 그만둘 수도 있는데 어떻게 할 거야? 참가비만큼은 돌려줄 건데?」
		ENDIF
		CALL ASK_YN("계속한다","그만둔다")
		IF RESULT
			PRINTFORMW 「그렇구나… 또 참가해줘」
			MONEY += 3000
			RETURN -1
		ELSE
			PRINTFORMW 「그렇게 나와야지!」
		ENDIF
	ENDIF
	IF WIN_P == MaxBattle - 1
		PRINTFORMW 「각오는 됐어? 최종전 상대는…… 그래!」
		CALL STONELEVEL(Rank + 4)
	ELSE
		PRINTFORMW 「어디 보자…제{WIN_P + 1}회전 그 상대는…」
		CALL STONELEVEL(difficulty)
	ENDIF
	IF RESULT == 1
		IF WIN_P == MaxBattle - 1
			PRINTFORMW 「에이카와 저렇게까지 싸우다니 대단해! 아쉬웠어… 또 도전해!」
		ELSE
			PRINTFORMW 「졌으니까 여기서 끝… 수고했어. 또 참가해줘!」
		ENDIF
		RETURN 0
	ELSE
		difficulty = MIN(difficulty + 1, 6)
	ENDIF
NEXT
PRINTFORMW 「우승 축하해! 상금 ￥{Prize}을 받아!」
;ピラミッド
IF Rank == 3
	IF TALENT:MASTER:돌쌓기랭커 > Rank
		PRINTFORML 「이것보다 윗급에서도 통하는 거 아니야?」
	ELSE
		PRINTFORMW 「그리고 피라미드급 첫 우승의 당신에게는… 돌쌓기 베테랑의 칭호를 선물!」
		PRINTFORMW 「근데 말이지…」
		PRINTFORMW 「이게 끝인 줄 알았어?…… 실은 하나 더 위에 진심의 진심인 에이카와 싸울 수 있다고 하는 환상이라고 불리는 계급이 있어. 엄청 강하니까 힘내!」
		TALENT:MASTER:돌쌓기랭커 = Rank + 1
	ENDIF
;バベル
ELSEIF Rank == 4
	IF TALENT:MASTER:돌쌓기랭커 >= 5
		PRINTFORML 「또 진심인 에이카를 이겨버렸어… 대단해!」
	ELSE
		PRINTFORML 「그리고 바벨탑급 첫 우승의 당신에게는… 랄까 에이카가 먼저 말해버렸지만…… 돌쌓기 마스터의 칭호를 선물! 굉장해!」
		TALENT:MASTER:돌쌓기랭커 = 5
	ENDIF
ENDIF

PRINTFORMW 「또 참가해요~」
MONEY += Prize
RETURN 0