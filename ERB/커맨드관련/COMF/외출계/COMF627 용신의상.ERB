@COM627
#DIMS 저기압위치

저기압위치 = %GET_LOCATION_LOW()%

DRAWLINE
PRINTW 용신의 상을 바라보았다
PRINTL
PRINTL ……

IF (IS_COLD_LOW && 저기압위치 != "寒冷渦通過" && DAY:2 != 2) || (IS_COLD_LOW && DAY:2 == 2) || (IS_SoJ_LOW == 2 && 저기압위치 != "二つ玉低気圧通過") || CAN_CUMULUS:0 == 5 || CAN_CUMULUS:2 == 2
	PRINTW 눈의 색이 빨갛다！！ 이상기상이 다가오고 있다…
	PRINTL
ELSEIF GET_LOCATION_AREA_TYPHOON() || 저기압위치 == "台風勢力圏" || (저기압위치 == "台風遠方" && TIME_ENTER_TERRITORY_TYPHOON:1 == DAY:3)
	PRINTW 눈의 색이 보라색으로 되어 있다… 태풍이 다가오고 있다
	PRINTL
ELSEIF (IS_SoJ_LOW == 1 && 저기압위치 != "日本海低気圧通過") || (IS_SOUTHERN_LOW && 저기압위치 != "南岸低気圧通過") || IS_RAINY_SEASON1 || IS_RAINY_SEASON2 || 저기압위치 == "温暖前線接近" || 저기압위치 == "暖域" || 저기압위치 == "寒冷前線通過"
	PRINTW 눈의 색이 파랗게 되어 있다… 비가 가깝다
	PRINTL
ELSE
	PRINTW 눈의 색에 변화는 없다
	PRINTL
ENDIF
TIME += 1
RETURN -1

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
;龍神の像
@COM_ABLE627
;一括管理
SIF GLOBAL_COMABLE(627)
	RETURN RESULT
;龍神の像ではない場所
SIF !RYUJIN_STATUE(CFLAG:MASTER:현재위치)
	RETURN 0
;睡眠中
SIF CFLAG:수면
	RETURN 0
RETURN 1

;龍神の像を判定する関数
@RYUJIN_STATUE(ARG)
#FUNCTION
;龍神の像
SIF ARG == GET_MAP_REPLACEMENT(207)
	RETURNF 1
RETURNF 0
