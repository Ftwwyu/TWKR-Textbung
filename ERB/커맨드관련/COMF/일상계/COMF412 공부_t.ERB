;-------------------------------------------------
;공부
;일상계 커맨드, 레벨 1
;-------------------------------------------------
@COM412

;自室時かつ家具を持っている際は補正
IF CFLAG:MASTER:현재위치 == CFLAG:MASTER:초기위치 && ITEM:느티책상의자
	SELECTCASE ITEM:느티책상의자
		CASE 1
			DOWNBASE:MASTER:체력 = 90
			DOWNBASE:MASTER:기력 = 270
		CASE 2
			DOWNBASE:MASTER:체력 = 80
			DOWNBASE:MASTER:기력 = 240
		CASE 3
			DOWNBASE:MASTER:체력 = 70
			DOWNBASE:MASTER:기력 = 210
	ENDSELECT
	PRINTFORML 자세 좋게, 허리를 펴고 읽고 쓰고 셈하기는 학습의 기본！
ELSE
	DOWNBASE:MASTER:체력 = 100
	DOWNBASE:MASTER:기력 = 300
ENDIF

IF TARGET > 0 && !CFLAG:수면 && CFLAG:행동 != 5
	;고정으로 획득하는 소스
	SOURCE:환락 = 200
	TFLAG:무드상승억제 = 1
	;신뢰
	TFLAG:98 = 3
	SIF ABL:MASTER:교양 > 2
		TFLAG:98 ++ 
	IF ABL:MASTER:교양 > ABL:교양 && !FLAG:시간정지
		TFLAG:98 ++ 
		EXP:학습경험 += 2
	ENDIF
	SIF ABL:MASTER:교양 < ABL:교양 - 2
		EXP:MASTER:학습경험 ++
	;ABL:순종을 본다
	IF ABL:순종 <= 1
		SOURCE:환락 += (ABL:순종 * 60)
	ELSEIF ABL:순종 <= 3
		SOURCE:환락 += 200 + (ABL:순종 * 60)
	ELSEIF ABL:순종 <= 5
		SOURCE:환락 += 500 + (ABL:순종 * 60)
	ELSEIF ABL:순종 <= 8
		SOURCE:환락 += 750 + (ABL:순종 * 75)
	ELSEIF ABL:순종 <= 11
		SOURCE:환락 += 1000 + (ABL:순종 * 75)
	ELSE
		SOURCE:환락 += 2000 + (ABL:순종 * 30)
	ENDIF

	;호감 도를 본다
	IF !FLAG:시간정지
		IF CFLAG:2 <= 500
			SOURCE:환락 += CFLAG:2
		ELSEIF CFLAG:2 <= 5000
			SOURCE:환락 += 500 + (CFLAG:2 - 500) / 3
		ELSE
			SOURCE:환락 += 2000 + (CFLAG:2 - 5000) / 5
		ENDIF
		SIF SOURCE:환락 < 0
			SOURCE:환락 = 0
	ENDIF
	EXP:MASTER:학습경험 ++
	SIF !FLAG:시간정지
		EXP:학습경험 ++
ENDIF

IF FLAG:70
	IF IS_WISEMAN()
		PRINTFORML 번뇌가 사라진 덕분에, 효율적으로 학습이 진행되었다
		BASE:MASTER:TSP -= 135
	ELSE
		BASE:MASTER:TSP -= 180
	ENDIF
	PRINTFORML 시간정지와 학습을 병행하는건 정신적으로 많이 힘들다...
	IF RAND:5 == 0
		DOWNBASE:MASTER:기력 += 50
	ELSE
		DOWNBASE:MASTER:기력 += 25
	ENDIF
ELSE
	;(天候パッチ)大雨・豪雨・大雪・ひょう・暴風のときには効率上昇
	IF (TIME:5 == 13 && GET_HEAT_THUNDERSTORM() == 1) || TIME:5 == 14 || TIME:5 == 15
		PRINTFORML 빗소리 덕분에 집중력이 증가해, 효율적으로 학습이 진행되었다
		TIME += 30
	ELSEIF WIND_VELOCITY >= 2
		PRINTFORML 바람소리 덕분에 집중력이 증가해, 효율적으로 학습이 진행되었다
		TIME += 30
	ELSEIF TIME:5 == 9
		PRINTFORML 눈의 고요함 덕분에 집중력이 증가해, 효율적으로 학습이 진행되었다
		TIME += 30
	ELSEIF TIME:5 == 5
		PRINTFORML 빗소리 덕분에 집중력이 증가해, 효율적으로 학습이 진행되었다
		TIME += 45
	ELSEIF WIND_VELOCITY >= 1
		PRINTFORML 바람소리 덕분에 집중력이 증가해, 효율적으로 학습이 진행되었다
		TIME += 45
	ELSEIF IS_WISEMAN()
		PRINTFORML 번뇌가 사라진 덕분에, 효율적으로 학습이 진행되었다
		TIME += 45
	ELSE
		TIME += 60
	ENDIF
ENDIF
SIF CFLAG:동행 && !FLAG:시간정지
	CFLAG:동행 += 60
EXP:MASTER:학습경험 ++
IF FLAG:훈련강화
	CALL AddEXP("학습경험" , MASTER, 1)
ENDIF
SIF ITEM:103 && !RAND:2
	EXP:MASTER:학습경험 ++
SIF ITEM:214 > 0
	EXP:MASTER:학습경험 ++
SIF ITEM:느티책상의자 && !RAND:2
	EXP:MASTER:학습경험 ++

IF !FLAG:영양소시스템미사용
	LOCAL = 1
	LOCAL += (FLAG:훈련강화 * 6)
	IF ABL:MASTER:교양 < ABL:교양 - 2
		LOCAL += 3
	ENDIF
	IF ARG능력치(MASTER,"지성") >= 200
		LOCAL += 2
	ENDIF
	IF TALENT:MASTER:마법을사용하는정도의능력
		LOCAL += 3
	ENDIF
	[IF_DEBUG]
	PRINTFORML 사식의마법 발동 확률 : {LOCAL}％
	[ENDIF]
	CALL A_명중판정(LOCAL)
	[IF_DEBUG]
	PRINTFORMW {RESULT:0} {RESULT:1} {RESULT:2}
	[ENDIF]
	IF RESULT:0 && 사식의마법 < 5
		FLAG:사식의마법학습++
		PRINTFORML 무언가 깨달음을 얻은것 같다!
		IF FLAG:사식의마법학습 >= 5
			PRINTFORMW 지금까지의 깨달음을 종합해 보니, 무언가 희미한 영감이 느껴진다
			PRINTFORMW [사식의 마법]에 대한 깨달음을 얻었다
			사식의마법++
			FLAG:사식의마법학습 -= 5
		ENDIF
	ELSE
		IF ABS(RESULT:2 - RESULT:1) <= 5
			PRINTFORML 어쩐지 계속 하다보면 깨달음이 있을것 같다...
		ELSEIF ABS(RESULT:2 - RESULT:1) <= 10
			PRINTFORML 무언가 아슬아슬하게 알락말락 한 기분이 든다...
		ELSEIF ABS(RESULT:2 - RESULT:1) <= 15
			PRINTFORML 무언가 희미한 영감이 스쳐지나갔다...
		ENDIF
	ENDIF
ENDIF
RETURN 1

;-------------------------------------------------
;실행 판정
;-------------------------------------------------
@COM_ABLE412
;공부 실행 판정
SIF !TFLAG:100
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(412)
	RETURN RESULT
;정지 중에는 불가
;SIF FLAG:70 == 1
;	RETURN 0
;공부 할 수 있는 방인가
IF CFLAG:MASTER:현재위치 == CFLAG:MASTER:초기위치 && ITEM:느티책상의자
	;위의 조건을 충족하면 처리를 하지 않는다
ELSEIF !공부가능(CFLAG:MASTER:현재위치)
	RETURN 0
ENDIF
;동침중
SIF CFLAG:동침중
	RETURN 0
;장난중
SIF CFLAG:MASTER:장난
	RETURN 0
;기력 0
SIF BASE:MASTER:기력 <= 0
	RETURN 0
SIF CFLAG:우후후 == 2
	RETURN 0
RETURN 1

@IS_WISEMAN()
#FUNCTION
SIF (BASE:MASTER:정력 *10) <= MAXBASE:MASTER:정력
	RETURNF 1
SIF FLAG:발원내용 == 201
	RETURNF 1
RETURNF 0
