@ENEMY_ENCOUNT(ARG)
#DIM ENEMY
#DIMS ENEMY_RACE
#DIM 승패
#DIM LOOT
#DIM LOOT_CANDIDATE, 17 = 51, 52, 53, 55, 56, 58, 59, 81, 82, 83, 84, 121, 131, 500, 501, 502, 510
#DIM CONST CANDIDATE_N = 17
#DIM REWARD
#DIM イベント遭遇

;데이트중は発生しない
SIF FLAG:데이트상대
	RETURN

;たまにイベント
IF FLAG:일일이벤트 % 10 < 2 && イベント遭遇 != DAY
	イベント遭遇 = DAY
	IF FLAG:일일이벤트 % 2
		CALL タヌキの宴会
	ELSE
		CALL イナバの流し売り
	ENDIF
	RETURN
ENDIF


;その日物色済みだと無理
SIF GETBIT(TFLAG:일일일회, 9)
	RETURN

;とりあえず50%固定で
SIF RAND:100 > 50
	RETURN

;相手が変わるとかやりたい
SELECTCASE ARG
	;湖
	CASE 3
		ENEMY_RACE = 요정
	;竹林
	CASE 4
		IF !RAND:3
			ENEMY_RACE = 이나바
		ELSE
			ENEMY_RACE = 요정
		ENDIF
	;山麓
	CASE 7
		ENEMY_RACE = 캇파
	;山頂
	CASE 8
		ENEMY_RACE = 백랑텐구
	CASE 9
		ENEMY_RACE = 오니
	CASE 10
		ENEMY_RACE = 옥토끼
	CASEELSE
		RETURN
ENDSELECT


HANDICAP_FIXED = RAND:(ABL:ENEMY:전투능력 * 5 + 1) + 10
HANDICAP_RAND = 0
PRINTFORMW 도중, 느닷없이 %조사처리(ENEMY_RACE,"가")% 덤벼들었다!
SETBIT TFLAG:일일일회, 9
CALL MAKE_YUKIZURI(RANDOM_CHARANUM, ENEMY_RACE, 1)

CALL KOJO_MESSAGE_SEND("DANMAKU", 0, RANDOM_CHARANUM, 1, -1, "戦闘前")
TEQUIP:RANDOM_CHARANUM:상반신착의상황 = 1

CALL CHARA_STATE(RANDOM_CHARANUM)
	DRAWLINE
	CALL COLORMESSAGE(@"처녀상실이력：%CSTR:RANDOM_CHARANUM:처녀상실이력%",C_PINK,1)
	PRINTFORML 
CALL DANMAKU_OPPONENT_STATUS(RANDOM_CHARANUM)
IF HANDICAP_FIXED
	CALL COLORMESSAGE(@"지형효과：%CALLNAME:MASTER%의 주사위값이 {HANDICAP_FIXED} 저하",C_YELLOW,1)
	;【세방울의천수】の効果
	IF ITEM:세방울의천수
		HANDICAP_FIXED /= 2
		CALL COLORMESSAGE(@"토착신의 저주로 인해, 지형효과가 {HANDICAP_FIXED} 으로 반감!",C_YELLOW,1)
	ENDIF
ENDIF
$LOOP
CALL ASK_M("원수를 갚아준다",1,"도망친다",1,"규칙 설명",1)
SELECTCASE RESULT
	CASE 0
		IF BASE:MASTER:기력 < 500 || BASE:MASTER:체력 < 500
			PRINTFORML 지금은 너무 피곤하다
			GOTO LOOP
		ENDIF
	CASE 1
		PRINTFORML %마스터는% 부리나케 도망쳤다
		RETURN
	CASE 2
		CALL SHOW_DANMAKU_RULE
		GOTO LOOP
ENDSELECT
ENEMY = RANDOM_CHARANUM
TARGET = ENEMY
REWARD = MAX(ABL:ENEMY:전투능력 * 10 + RAND:5, 1)
;米とか酒とか
LOOT = LOOT_CANDIDATE:(RAND:CANDIDATE_N)
TIME += 60
CFLAG:ENEMY:안면 = 1
PRINTFORMW 승부 개시!!
탄막전투화상무시 = 1
후처리인터셉트 = 1
CALL DANMAKU_BATTLE(TARGET)

승패 = RESULT
CALL RECOVER(MASTER, -500, "체력")
CALL RECOVER(MASTER, -500, "기력")
CALL KOJO_MESSAGE_SEND("DANMAKU", 승패, TARGET, 1, -1, "戦闘後")
SIF 승패 < 3
	EXP:MASTER:전투경험 += ABL:ENEMY:전투능력 * 3
SIF 승패 < 1
	EXP:MASTER:전투경험 += ABL:ENEMY:전투능력
IF 승패 > 0
	;CALL KOJO_MESSAGE_SEND("DANMAKU", 勝敗, TARGET, 1, -1, "逆レ開始")
	SETCOLOR C_RED
	IF TCVAR:ENEMY:발정 && HAS_PENIS(MASTER)
		PRINTFORML %조사처리(CALLNAME:ENEMY,"는")% 움직일 수 없게 된 %마스터를% 깔고 앉았다
		PRINTFORML %CALLNAME:MASTER%의 숨통을 미지근한 한숨이 불어 지나갔다…
		PRINTFORML 이젠 여기까지라고 생각한 다음 순간, 성기가 뜨거운 감촉에 휩싸였다
		PRINTFORML 조심조심 아랫배를 내려다보니, 히죽히죽 미소를 짓는 %CALLNAME:ENEMY%의 균열에 %CALLNAME:MASTER%의 페니스가 뿌리까지 들어가 있다
		PRINTFORML 만월의 밤에 발정기를 맞이한 %조사처리(CALLNAME:ENEMY,"는")% 짐승같은 갈증을 풀기 위해, %마스터를% 엉망으로 범하며, 괴롭히며, 유린해 갔다…
		PRINTFORMW ………
		PRINTFORMW ……
		PRINTFORMW …
		PRINTFORMW 속이 시원해질 때까지 %마스터를% 있는대로 탐한 %조사처리(CALLNAME:ENEMY,"는")%, 축 늘어진 %마스터를% 내팽개치고 떠났다
		FLAG:기절중단 = 100 + CFLAG:MASTER:현재위치 / 100
		CFLAG:MASTER:데이트중 = CFLAG:MASTER:현재위치 / 100
		CALL LOST_GYAKURE(ENEMY)
	ELSEIF ITEM:LOOT
		PRINTFORMW 진 %마스터는% %조사처리(ITEMNAME:LOOT,"를")% 빼앗기고 말았다…
		ITEM:LOOT --
	ELSEIF MONEY:2 >= REWARD
		PRINTFORMW 진 %마스터는% {REWARD}카리스마를 빼앗기고 말았다…
		MONEY:2 -= REWARD
	ELSE
		REWARD *= 300
		PRINTFORMW 진 %마스터는% \@ MONEY > REWARD ? \\{REWARD}# 가진 돈 전부를\@ 빼앗기고 말았다…
		MONEY = MAX(MONEY - REWARD, 0)
	ENDIF
	RESETCOLOR
	RETURN
ENDIF

;source操作他
TCVAR:ENEMY:탄막승부불능 = 1
CFLAG:ENEMY:탄막승부승리 = 1
CFLAG:ENEMY:패배자 = 1
CFLAG:ENEMY:자주가는지역 = ARG

PRINTFORML %마스터는% 땅바닥에 무릎을 꿇은 %CALLNAME:ENEMY%에게…
CALL ASK_M("제물로 한다",1,"갈취",1,"몸으로 치른다",1, "미라다의돌", ITEM:미라다의돌)
SELECTCASE RESULT
	CASE 0
		PRINTFORML %마스터가% %CALLNAME:ENEMY%의 손을 감싸쥐고 기도의 말을 외치자、비치는 옷 사이로 %CALLNAME:ENEMY%의 속옷이 희미하게 빛나기 시작했다
		PRINTFORML 이윽고 초자연적인 불꽃이 피어오르고, %CALLNAME:ENEMY%도 옷도 해치지 않고 속옷만 다 타버렸다
		PRINTFORML 신의 위력을 목전에서 본 %조사처리(CALLNAME:ENEMY,"는")% 겁에 질려 도망쳤다
		CALL BUFF_BASE(MASTER,BASE_TSP,500, 1)
		CALL BUFF_BASE(MASTER,BASE_정력,500, 1)
		FLAG:독신 += 10
		;DELCHARA RANDOM_CHARANUM
		;ADDCHARA RANDOM_CHARANUM
		TARGET = 0
		RETURN
	CASE 1
		PRINTFORMW 오랏! 점프해라 점프!
		MONEY:2 += REWARD
		PRINTFORMW %마스터는% %조사처리(CALLNAME:ENEMY,"으로")%부터 {REWARD}카리스마를 빼앗았다.
		IF RAND:5 < ABL:ENEMY:전투능력
			PRINTFORMW 더욱이 %ITEMNAME:LOOT%도 빼앗았다.
			ITEM:LOOT ++
		ENDIF
		TARGET = 0
		;DELCHARA RANDOM_CHARANUM
		;ADDCHARA RANDOM_CHARANUM
		RETURN
	CASE 2
		MARK:ENEMY:반발각인 = 1
		PRINTFORML %CALLNAME:ENEMY%에게, 무녀에게 끌려가고 싶지 않으면 오늘 하루 %마스터와% 함께하도록 명령했다.
		CALL COLORMESSAGE(@"%CALLNAME:ENEMY%에게 반발각인이 붙었지만, 남 앞이 아니라면 언제든지 밀어 넘어뜨릴 수 있게 되었다",C_YELLOW,1)
		PRINTFORMW %마스터는%%조사처리(CALLNAME:ENEMY,"를")% 데리고 의기양양하게 귀로에 올랐다
	CASE 3
		PRINTFORML 매섭게 째려보는 %조사처리(CALLNAME:ENEMY,"를")% 되돌아보면서, %마스터는% 미라다의돌을 움켜쥐었다
		PRINTFORML 그러자 %조사처리(CALLNAME:ENEMY,"는")% 시선을 돌려 고개를 숙이고 말았다
		CFLAG:ENEMY:몸목적 = 1
		ITEM:미라다의돌 --
		ABL:ENEMY:친밀 = MAX(3, ABL:ENEMY:친밀)
		ABL:ENEMY:욕망 = MAX(3, ABL:ENEMY:욕망)
		CFLAG:ENEMY:욕구불만도 = 1000
		PRINTFORML %마스터는%%CALLNAME:ENEMY%의 어께에 손을 얹고, 오늘 하루 함께하도록 명령했다.
		PRINTFORML %조사처리(CALLNAME:ENEMY,"는")% 자포자기해 받아들였다
		CALL COLORMESSAGE(@"%조사처리(CALLNAME:ENEMY,"를")% 남 앞이 아니라면 언제든지 밀어 넘어뜨릴 수 있게 되었다",C_YELLOW,1)
		PRINTFORMW %마스터는%%조사처리(CALLNAME:ENEMY,"를")% 데리고 의기양양하게 귀로에 올랐다
ENDSELECT

FLAG:데이트상대 = TARGET
CFLAG:TARGET:동행 = 180
CFLAG:MASTER:동행 = 180
CFLAG:ENEMY:졸음쫒기강도 = 600
CFLAG:거점내방 = 1
CSTR:ENEMY:모브코프로필2 = 꼴사납게도 원수를 갚으려다 도리어 당해 지금은 얌전하다

RETURN 1

@LOST_GYAKURE(ARG)
BASE:MASTER:체력 = 0
BASE:MASTER:기력 = 0
BASE:MASTER:정력 = -500
CFLAG:ARG:욕구불만도 = 0
CALL SET_SLEEP(1,MASTER,0)
CFLAG:MASTER:취침시간 = DAY * 1440 + TIME + 120 + RAND:120
TFLAG:106 = 0
BEGIN AFTERTRAIN

;IF TIME:2 >= 5 && TIME:2 <= 7 && TIME:5 < 2
;	CALL COLORMESSAGE(@"満月の光が%CALLNAME:TARGET%の本能をかき立てる…！",C_YELLOW,1)
;	CALL COLORMESSAGE(@"%CALLNAME:TARGET%の戦闘力がアップ！",C_YELLOW,1)
;ENDIF


;21/09/20 イベント販売員追加
;固有コマンド:薬品販売とデイリー販売員イナバからの流用
;デイリーイナバが愛されキャラなのでこっちは腹黒うざキャラで。販売価格もぼったくり
@イナバの流し売り
#DIM 거래
#DIM 할인
#DIM 가격
#DIM 돌판매중
돌판매중 = (GETBIT(FLAG_DAILY_EV23, 0) && !ITEM:(GETNUM(ITEM, "미라다의돌")))
할인 = -50
가격 = ITEMPRICE:(GETNUM(ITEM, "미라다의돌")) / 10
SETCOLOR 0XFFFFFF
PRINTFORMDW 道中、不意にイナバが襲い掛かってきた…！？
PRINTFORML
PRINTFORML 「あああ！！いけません！！お客さま！！！ああっ！困りますっ！！」
PRINTFORMW 「あーっ！！お客さま！！私は弾幕勝負なんてやりませんから！！どうか待ってください！！！」
PRINTFORML 「ああっ！いけません！！どうか落ち着いて！！話を聞いてくださいっ！！あーっ！！！」
PRINTFORMDW 大きな荷物を抱えたイナバが一人で勝手に騒いでいる… 
CALL PRINT_FACE, 0, "通常", "服", "20"
PRINTFORML 「えー、お見苦しいところをすみません」
PRINTFORMW 「私少々慌ててしまいまして…」
PRINTFORML 「でも、ここで出会ったのもなにかのご縁」
PRINTFORM 「お客さまはわたくしどもの
CALL COLORMESSAGE("素晴らしい", C_YELLOW, 0, 1)
PRINTFORMW 商品に興味はありませんか？」
RESETCOLOR
CALL ASK_M("無視して帰る", 1, "ちょっと見てみる", 1)
SETCOLOR 0XFFFFFF
IF !RESULT
	PRINTFORMDL うさんくさいセールスはお断りだ
	PRINTFORML 「そんなー、せめてお話だけでもー」
	PRINTFORMDL なおも食い下がる販売員を無視して%CALLNAME:MASTER%は帰路についた
	PRINTFORMW
	RESETCOLOR
	RETURN
ENDIF
CALL PRINT_FACE, 0, "笑顔", "服", "20"
PRINTFORMW 「ありがとうございます！」
PRINTFORM 「では、他所では見られない
CALL COLORMESSAGE("秘蔵", C_AQUA, 0, 1)
PRINTFORML の一品！」
PRINTFORM 「多少
CALL COLORMESSAGE("秘蔵", C_M_GREEN, 0, 1)
PRINTFORM は張りますが、その効果は
CALL COLORMESSAGE("折り紙", C_RED, 0, 1)
PRINTFORMW 付き！！」
PRINTFORM 「お客さまのような
CALL COLORMESSAGE("特別", C_YELLOW, 0, 1)
PRINTFORM な方にふさわしい
CALL COLORMESSAGE("特別", C_YELLOW, 0, 1)
PRINTFORML な商品の数々！！」
PRINTFORMW 「さあこちらをご覧ください！！！」
RESETCOLOR
DRAWLINE
$INPUT_LOOP
RESETCOLOR
CALL ITEM_SHOPPING(GETNUM(ITEM, "선향옥토"), "", 할인)
CALL ITEM_SHOPPING(GETNUM(ITEM, "국사무쌍약"), "", 할인)
CALL ITEM_SHOPPING(GETNUM(ITEM, "가싱가든"), "", 할인)
CALL ITEM_SHOPPING(GETNUM(ITEM, "포이즌바디"), "", 할인)
IF 돌판매중
	PRINTFORM [{GETNUM(ITEM, "미라다의돌")}] 
	CALL COLORMESSAGE("愛欲を独占するミラダの石ミレニアム", C_D_PINK, 1, 1)
	CALL COLORMESSAGE("　　　特別価格90％OFF！", C_RED, 0, 1)
	PRINTFORML %금액표시(가격), 13%
ENDIF
DRAWLINE
SIF 돌판매중
	PRINTL [990] 「미라다의돌」って何だっけ？
PRINTFORML [999] やっぱり買わない
PRINTFORML 　　　現在の所持金:%금액표시(MONEY)%
$INPUT_LOOP2
INPUT
거래 = RESULT
SETCOLOR 0XFFFFFF
SELECTCASE 거래
	CASE GETNUM(ITEM, "선향옥토") TO GETNUM(ITEM, "포이즌바디"), GETNUM(ITEM, "미라다의돌")
		IF 거래 == GETNUM(ITEM, "미라다의돌") && !돌판매중
			CLEARLINE 1
			GOTO INPUT_LOOP2
		ENDIF
		LOCAL = 거래 == GETNUM(ITEM, "미라다의돌") ? 90 # 할인
		CALL SHOP_ASK(RESULT, LOCAL)
		IF RESULT
			PRINTFORMW 「えー、では他の品物なんてどうでしょうか？」
			CLEARLINE 12 + RESULT + (돌판매중 * 3)
			GOTO INPUT_LOOP
		ENDIF
		CALL GET_ITEM(ITEMNAME:거래, 1, 2)
	;あのセリフたまに聞きたくなるので
	CASE 990
		IF !돌판매중
			CLEARLINE 1
			GOTO INPUT_LOOP2
		ENDIF
		PRINTFORMW 「おや？　お客様さまはご存知かと思いましたが…」
		PRINTFORML 「ああでも懐かしいですね」
		PRINTFORML 「私も仕事を始めたての頃、あの商品説明を必死に覚えたものです…」
		PRINTFORML 「では私の確認がてら、お客さまにも改めて聞いて頂きましょう」
		PRINTFORMW 
		CALL DAILY_EV23_SALESTALK(1)
		FONTBOLD
		PRINTFORMW 「なんと！」
		PRINTFORM 「今回、お가격
		CALL COLORMESSAGE(@"\\{가격}", C_RED, 0, 1)
		PRINTFORMW でのご提供が実現いたしました！」
		FONTREGULAR
		PRINTFORML 「いつ手に入るかわからない希少な品！！」
		PRINTFORM 「この
		CALL COLORMESSAGE("特別価格", C_RED, 0, 1)
		PRINTFORML で買わなければ…」
		PRINTFORMW 「もう二度と手に入らないかもしれませんよ！？」
		PRINTFORML 「さあ、お客さま！」
		PRINTFORM 「充実した性生活を約束する
		CALL COLORMESSAGE("愛欲を独占するミラダの石ミレニアム", C_D_PINK, 0, 1)
		PRINTFORMW ！！！！」
		PRINTFORML 「もちろんっ！　買いますよねッ！？」
		FONTREGULAR
		CALL ASK_M("買った！", MONEY >= 가격, "買わない", 1)
		IF !RESULT
			MONEY -= 가격
			CALL GET_ITEM("미라다의돌", 1, 2)
		ELSE
			거래 = 999
		ENDIF
	CASE 999
	CASEELSE
		CLEARLINE 1
		GOTO INPUT_LOOP2
ENDSELECT
IF 거래 < 999
	CALL PRINT_FACE, 0, "笑顔", "服", "20"
	CALL COLORMESSAGE("「お買い上げありがとうございます！」", 0XFFFFFF, 2, 1)
	PRINTFORMW 「いや～お客さまはいいカモ…」
	CLEARLINE 1
	PRINTFORMW 「ぜひまたお買い上げ頂きたいですね！！」
	PRINTFORML 「では今日はこの辺で失礼します！」
ELSE
	CALL PRINT_FACE, 0, "怒り", "服", "20"
	CALL COLORMESSAGE("「なぁんだ、ただの貧乏人かあ…」", 0XFFFFFF, 1, 2)
	CALL COLORMESSAGE("「やだやだ…時間を損しちゃったわー」", 0XFFFFFF, 1, 2)
	PRINTFORMDW %CALLNAME:MASTER%が何も買わないとわかった途端、販売員の態度が悪くなった…
	CALL COLORMESSAGE(@"「なーんにも買わないヤツ相手するほど%TEXTR("あたい/あたし/ボク/")%も暇じゃないんだよー」", 0XFFFFFF, 1, 2)
	CALL COLORMESSAGE("「じゃあーねー」", 0XFFFFFF, 1, 2)
ENDIF
PRINTFORMDW ウサギの販売員は\@ 거래 < 999 ? # 捨て台詞を残して \@いなくなった
RESETCOLOR
PRINTFORML 
RETURN

;21/10/05 追加
;作った顔グラとお酒に関する何かをねじ込みたいだけのイベント
@タヌキの宴会
#DIM 結果
#DIM 色１
#DIM 色２
#DIM 色３
#DIMS 自称
#DIMS 통칭
結果 = CFLAG:MASTER:오늘의운세 / 150 + TFLAG:행운보정 + TALENT:MASTER:술내성 + RAND:15
LOCAL:0 = RAND:12
LOCAL:1 = LOCAL:0 > 10 ? 1 # LOCAL:0 + 2
LOCAL:2 = LOCAL:1 > 10 ? 1 # LOCAL:1 + 2
色１ = GET_CN_WORD_COLOR(STR:(LOCAL:0 + 2050))
色２ = GET_CN_WORD_COLOR(STR:(LOCAL:1 + 2050))
色３ = GET_CN_WORD_COLOR(STR:(LOCAL:2 + 2050))
自称 = %TEXTR("あたい/あたし/ボク")%
통칭 = %\@ TALENT:MASTER:성별 == 2 ? %TEXTR("お兄さん/兄さん/おにいさん")% # %TEXTR("姐さん/姉さん/お姉さん")%\@%

SETCOLOR 色１
PRINTFORMDW 道中、不意にタヌキが襲い掛かってきた…！？
CALL PRINT_FACE, 0, "通常", "服", "21"
PRINTFORML 「待った、待った！　待っておくれよ！」
PRINTFORML 「%自称%は%통칭%とやり合うつもりなんてないんだからさ！」
PRINTFORMDL 羽織を着た化け狸が慌てた様子で声をかけてきた…本当に敵意はないようだ
PRINTFORMW 
PRINTFORML 「それより%통칭%、いま暇だったりしない？」
PRINTFORML 「%自称%らこれから宴会を始めるとこなんだけどさ」
PRINTFORML 「いっつもおんなじ面子ばっかでつまんないんだよねー」
PRINTFORMW  
PRINTFORML 「だから%통칭%みたいな\@ TALENT:MASTER:성별 == 2 ? イケメン # 美人さん\@の飛び入り大歓迎！」
PRINTFORML 「ぜひぜひ%自称%らと一緒に飲んでいっておくれよ、ね？」
RESETCOLOR
CALL ASK_M("無視して帰る", 1, "参加してみる", 1)
SETCOLOR 色１
IF !RESULT
	PRINTFORMDW わざわざ化かされに行くなんてお断りだ
	PRINTFORML 「えー、いいじゃんいいじゃん！」
	PRINTFORML 「%통칭%もつれないなー」
	PRINTFORMDL なおも話しかけてくるタヌキを無視して%CALLNAME:MASTER%は帰路についた
	PRINTFORMW
	RESETCOLOR
	RETURN
ENDIF
CALL PRINT_FACE, 0, "笑顔", "服", "21"
PRINTFORML 「やったー♪」
PRINTFORMW 「それじゃ%통칭%こっちこっち！　付いてきなよ」
RESETCOLOR
DRAWLINE
PRINTFORMW 案内先では揃いの羽織を着た化け狸たちがすでに宴会を始めていた
CALL PRINT_FACE, 0, "通常", "服", "22"
CALL COLORMESSAGE(@"「おおっ！　ひっさしぶりのお客さんじゃあないか♪」", 色２, 1, 0)
CALL COLORMESSAGE(@"「あははっ、\@ TALENT:MASTER:성별 == 2 ? あんた # お前さん \@どっから来たんだい？　\@ TALENT:MASTER:성별 == 2 ? いい男じゃーん # 美人さんだねぇ \@♥」", 色２, 2, 0)
CALL PRINT_FACE, 0, "笑顔", "服", "23"
CALL COLORMESSAGE(@"「ほらほら～\@ TALENT:MASTER:성별 == 2 ? 兄さん # 姉さん \@、お近づきの印に一杯ど～ぞ」", 色３, 1, 0)
CALL COLORMESSAGE(@"「遠慮なんていらないよ、ぐい～っとやってほしいな～♪」", 色３, 2, 0)
SELECTCASE 結果
	;化かされる
	CASE IS <= 4
		LOCAL = ABL:MASTER:전투능력 * 10000
		PRINTFORMW %CALLNAME:MASTER%は注がれた酒を飲んでみた。口当たりは良いが相当強烈だ…
		CALL COLORMESSAGE(@"「おお～っ！　%통칭%いい飲みっぷり～♪」", 色１, 1, 0)
		CALL COLORMESSAGE(@"「いいじゃんいいじゃん！　うちら\@ TALENT:MASTER:성별 == 2 ? あんた # お前さん \@にもっと飲ませたくなってきたよー」", 色２, 2, 0)
		PRINTFORMW 化け狸たちにちやほやされながら飲んでいると、%CALLNAME:MASTER%はあっという間に酔いつぶれてしまった…
		PRINTW …………
		PRINTW ……
		PRINTW …
		DRAWLINE
		PRINTFORML はっと目を覚ますと%CALLNAME:MASTER%は自分が落ち葉に埋もれて寝ていたことに気づいた
		PRINTFORMW 周りには誰もおらず宴会が開かれていた様子は微塵もない…時間も全然たっていないようだ
		PRINTFORMW やられた！　どうやら狸たちに化かされたらしい……
		PRINTFORML 
		PRINTFORML おまけに財布がすっかり軽くなっている！！
		PRINTFORML %CALLNAME:MASTER%は\@ MONEY > LOCAL ? %금액표시(LOCAL)%も # 有り金すべてを\@取られていた…
		MONEY = MAX(MONEY - LOCAL, 0)
		PRINTFORMW 　現在の所持金:%금액표시(MONEY)%
		PRINTFORML 
		PRINTFORML 最悪の気分のまま%CALLNAME:MASTER%は帰路についた…
		PRINTFORMW 
	;逆レ。パターン欲しいので加筆大歓迎
	CASE 5 TO 9
		PRINTFORML %CALLNAME:MASTER%は周りに勧められるまま酒を飲み始める
		PRINTFORMW 不思議なことにいつもより酒がするすると進み、%CALLNAME:MASTER%はあっという間に酔っ払ってしまった…
		PRINTW …………
		PRINTW ……
		PRINTW …
		DRAWLINE
		;IF RAND:2
			SETCOLOR 色１
			PRINTFORMDW %CALLNAME:MASTER%はいつの間にか寝ていたようだが、なぜか股間から快感が湧き上がってくる…
			IF TALENT:MASTER:성별 == 2
				PRINTFORMDW 目を覚ますと素っ裸になったタヌキがじゅぽじゅぽ音を立ててペニスをしゃぶっていた
				CALL PRINT_FACE, 0, "笑顔", "裸", "21"
				PRINTFORMW 「あははっ、%통칭%やっと目が覚めたんだー」
				PRINTFORML 「おちんぽ舐められながら起きた気分はどうかな？」
				PRINTFORML 「こんなんやってもらったことないでしょー♥」
				PRINTFORMW 
				PRINTFORML 「それにしても…このおちんぽ、長さも形もドンピシャ！　ちょー%自称%好みっ」
				PRINTFORML 「あとはどんだけ保つのか、すっごい楽しみだよ」
			ELSE
				PALAM:MASTER:윤활 = MAX(PALAMLV:4, PALAM:MASTER:윤활)
				PRINTFORMDW 目を覚ますと素っ裸になったタヌキが%SLIT(MASTER,1)%をぴちゃぴちゃと熱心に舐め回していた
				CALL PRINT_FACE, 0, "笑顔", "裸", "21"
				PRINTFORMW 「おおっと%통칭%目は覚めたかい？　こっちはもう勝手に始めてるよっ」
				PRINTFORML 「%통칭%のお股汁が美味しくってさ、もう舌が止まんない止まんない♥」
				PRINTFORML 「すすってるだけで%自称%のおまんこまでヌレヌレなんだー♥」
				PRINTFORMW 
				IF HAS_PENIS(MASTER)
					PRINTFORML 「それにしても%통칭%キレーな顔して\@ TALENT:MASTER:형상 < 4 ? 変な # えっぐい \@もんぶら下げてんねー」
					PRINTFORMW 「ひん剥いてこんなのとご対面したら、そりゃあもうびっくりだよっ！」
					PRINTFORML 「でも、その分お楽しみも増えて%自称%嬉しいんだー」
				ELSE
					PRINTFORML 「でもねー、%自称%もまだ始めたばっかなんだけど」
					PRINTFORMW 「こーんなヌルヌルのひくひくになって誘ってくるんだからさ」
					PRINTFORML 「このおまんこも、%통칭%だってすっごいエッチなんでしょー？」
				ENDIF
			ENDIF
			PRINTFORML 「ホント%통칭%を誘ったのは大正解！　もう、大当たりだねっ♥」
			PRINTFORMW 
			IF HAS_PENIS(MASTER)
				PRINTFORMDL 周りを見るとタヌキたちはみんな裸で%CALLNAME:MASTER%の周りに集まっていた
				PRINTFORMDL 皆獲物を狙う目つきで%CALLNAME:MASTER%と猛々しく反り返るペニスを見つめている…
			ELSE
				PRINTFORMDL 気がつけば周りのタヌキもみんな裸になっており、至るところで百合の花が咲き乱れていた
				PRINTFORMDL こじんまりとした広場は絶え間ない嬌声とむせかえるような淫臭で溢れている…
			ENDIF
			PRINTFORMW 
			PRINTFORMDL こんな状況になっても%CALLNAME:MASTER%は身体を全く動かせない！
			PRINTFORMDL どうやらさっきのお酒に一服盛られていたようだ…
			PRINTFORMW 
			IF HAS_PENIS(MASTER)
				CALL PRINT_FACE, 0, "笑顔", "裸", "21"
				PRINTFORMW 「じゃあじゃあ、%自称%が拾ってきたんだからさ、%自称%が最初でいーよねっ！」
				CALL HPH_PRINT, @"「はーい決定、いっただきまーす……ああんっHPH」", "L"
				PRINTFORMDW 化け狸は%CALLNAME:MASTER%の上に腰を下ろし、ペニスを一気に根本まで咥えこんだ
				CALL HPH_PRINT, @"「あはっHPH　これっ、最高HPH　%통칭%のっ、カッチカチでHPH　サイコーだよっHPH」", "L"
				CALL HPH_PRINT, @"「ほらHPH　ほらっHPH　がーんばれっHPH　%自称%がっ、イくまでっHPH　出しちゃダメだからねっHPHHPH」", "W"
				CALL PRINT_FACE, 0, "笑顔", "裸", "22"
				CALL COLORMESSAGE(@"「おーい、\@ TALENT:MASTER:성별 == 2 ? あんた # お前さん \@も口はちゃんと動くんだろ？」", 色２, 1, 0)
				CALL COLORMESSAGE(@"「順番が回ってくるまで、うちのおマンコしっかりぺろぺろするんだよっ♥」", 色２, 2, 0)
				CALL PRINT_FACE, 0, "笑顔", "裸", "23"
				CALL COLORMESSAGE(@"「ん～と、%랜덤캐릭터_명명(69)%ちゃんは～\@ TALENT:MASTER:성별 == 2 ? 兄さん # 姉さん \@の金玉でもお世話しとこっか～」", 色３, 1, 0)
				CALL COLORMESSAGE(@"「\@ TALENT:MASTER:성별 == 2 ? 兄さん # 姉さん \@をカラッカラまで搾れるよ～に、よ～く揉みほぐしてあげるねっ♥」", 色３, 2, 0)
				RESETCOLOR
				PRINTW ………
				PRINTW ……
				PRINTW …
				PRINTFORML %CALLNAME:MASTER%は盛りのついた化け狸たちに散々弄ばれ空っぽになるまで射精を繰り返した
				PRINTFORML 化け狸たちは%CALLNAME:MASTER%が使えなくなるまで搾り取ると、そのままどこかへ消えてしまった…
				IF !TALENT:MASTER:비동정
					CALL COLORMESSAGE("동정상실(あなた)", 0XFF64FF, 1, 1)
					TALENT:MASTER:비동정 = 1
				ENDIF
				PRINTFORMW 
				EXP:MASTER:M경험 += 15
				EXP:MASTER:펠라티오경험 += 15
				EXP:MASTER:C경험 += 45
				EXP:MASTER:C절정경험 += 30
				EXP:MASTER:삽입경험 += 30
				EXP:MASTER:사정경험 += 30
				EXP:MASTER:절정경험 += 30
				PRINTFORML C경험 +45(%CALLNAME:MASTER%)
				PRINTFORML M경험 +15(%CALLNAME:MASTER%)
				PRINTFORML C절정경험 +30(%CALLNAME:MASTER%)
				PRINTFORML 절정경험 +30(%CALLNAME:MASTER%)
				PRINTFORML 삽입경험 +30(%CALLNAME:MASTER%)
				PRINTFORML 사정경험 +30(%CALLNAME:MASTER%)
				PRINTFORML 펠라티오경험 +15(%CALLNAME:MASTER%)
				PRINTFORMW 
				FLAG:기절중단 = 100 + CFLAG:MASTER:현재위치 / 100
				CFLAG:MASTER:데이트중 = CFLAG:MASTER:현재위치 / 100
				TARGET = 0
				CALL LOST_GYAKURE(MASTER)
			ELSEIF !TALENT:MASTER:처녀
				CALL PRINT_FACE, 0, "笑顔", "裸", "21"
				PRINTFORML 「ほらほら%통칭%、みんな気持ち良さそーでしょ？　%自称%らも始めよっかー♥」
				PRINTFORMDW 化け狸は凶悪な形をした双頭ディルドを取り出し、自分と%CALLNAME:MASTER%の%SLIT(MASTER)%に躊躇なく突っ込んだ…
				CALL HPH_PRINT, @"「あひっHPH　やっぱ、これ、でっかい…HPH　なかっ、ごりごり、してるぅHPH」", "L"
				CALL HPH_PRINT, @"「でもっ、これ、サイコーだよっHPH　%통칭%と、つながるのっ、サイコーに気持ちいいよっHPHHPH」", "W"
				CALL PRINT_FACE, 0, "笑顔", "裸", "22"
				CALL COLORMESSAGE(@"「あーあ、お前さんもこの子を喰われちまうとは大変だねぇ…」", 色２, 2, 0)
				CALL COLORMESSAGE(@"「ここでお前さんのお豆を摘んだらっと……あははっ、いい反応だあ♥」", 色２, 1, 0)
				CALL COLORMESSAGE(@"「もうこうなったらイき狂ってとんじまった方が…楽かもしれないよ♥」", 色２, 2, 0)
				CALL PRINT_FACE, 0, "笑顔", "裸", "23"
				CALL COLORMESSAGE(@"「んふふっ♥　姉さんのおっぱい、先っぽまでビンビンだ～」", 色３, 1, 0)
				CALL COLORMESSAGE(@"「%랜덤캐릭터_명명(69)%ちゃんがいっぱいなめたげるから～、もっともっ～とイっちゃえ～♥」", 色３, 2, 0)
				PRINTFORMDL 化け狸たちに全身余すところなく責められて%CALLNAME:MASTER%は幾度となく絶頂を繰り返す
				PRINTFORMDW やがて喘ぎ声すら出す気力も無くなり、%CALLNAME:MASTER%は再び気を失った…
				RESETCOLOR
				PRINTW ………
				PRINTW ……
				PRINTW …
				PRINTFORML はっと目を覚ますと%CALLNAME:MASTER%は自分が落ち葉に埋もれて寝ていたことに気づいた
				PRINTFORMW 周りで宴会が開かれていた様子は跡形もなく誰かが居る気配もない
				PRINTFORML %CALLNAME:MASTER%の身体に火照りは残るが消耗はしておらず持ち物も無事だ
				PRINTFORML さっきのはタヌキに化かされていたのだろうか、それとも……？
				PRINTFORMW 
				PRINTFORML どうにもモヤモヤとした気分になりながら%CALLNAME:MASTER%は帰路についた
				PRINTFORML +60分
				PRINTFORMW 
				EXP:MASTER:C경험 += 15
				EXP:MASTER:V경험 += 35
				EXP:MASTER:B경험 += 15
				EXP:MASTER:C절정경험 += 10
				EXP:MASTER:V절정경험 += 20
				EXP:MASTER:B절정경험 += 10
				EXP:MASTER:절정경험 += 40
				PRINTFORML C경험 +15(%CALLNAME:MASTER%)
				PRINTFORML V경험 +35(%CALLNAME:MASTER%)
				PRINTFORML B경험 +15(%CALLNAME:MASTER%)
				PRINTFORML C절정경험 +10(%CALLNAME:MASTER%)
				PRINTFORML V절정경험 +20(%CALLNAME:MASTER%)
				PRINTFORML B절정경험 +10(%CALLNAME:MASTER%)
				PRINTFORML 절정경험 +40(%CALLNAME:MASTER%)
				PRINTFORMW 
				PALAM:MASTER:윤활 += 10000
				TIME += 60
			;不意打ちで履歴汚さないように処女は除外
			ELSE
				CALL PRINT_FACE, 0, "通常", "裸", "21"
				PRINTFORML 「さあさあ%통칭%、%自称%らもそろそろ始めよっかー♥」
				PRINTFORMW 「…あれっ？」
				PRINTFORMDW いざ%CALLNAME:MASTER%の%SLIT(MASTER,1)%に指を挿れると、化け狸はぎょっとしたように話しかけてきた
				PRINTFORMW 「……ええっとさ、%통칭%もしかして…おぼこ、なの…？」
				PRINTFORMW 「あちゃー…これ、%自称%がヤっちゃうのも…まずいよねぇ…」
				CALL PRINT_FACE, 0, "通常", "裸", "22"
				CALL COLORMESSAGE(@"「そりゃあ…やっぱり、マズいんじゃない…？」", 色２, 2, 0)
				CALL COLORMESSAGE(@"「うちらもイジめてあげるのは好きだけど…」\n「取り返しつかないことしたいわけじゃないしねぇ…」", 色２, 2, 0)
				CALL PRINT_FACE, 0, "通常", "裸", "23"
				CALL COLORMESSAGE(@"「どうする？　もうすぐさっきのクスリもきれるしさ…」", 色３, 1, 0)
				CALL COLORMESSAGE(@"「このまま、この姉さんにはお引取り願おっか…？」", 色３, 1, 0)
				PRINTFORMW 
				PRINTFORMDL 次の瞬間化け狸たちに落ち葉を被せられ、%CALLNAME:MASTER%の目は塞がれてしまう
				PRINTFORMDL すぐに視界は戻ったが、もう周りには誰もおらず、広場に一人%CALLNAME:MASTER%だけが寝転んでいた…
				PRINTFORMW 
				PRINTFORMDL どうにもモヤモヤとした気分になりながら%CALLNAME:MASTER%は帰路についた
				PRINTFORMDL +30分
				PRINTFORMW 
				TIME += 30
			ENDIF
		;ELSE
		;ENDIF
	;仲良くなる
	CASE IS >= 10
		PRINTFORMW 注がれた酒は口当たりも良く、これならいくらでも飲んでしまえそうだ
		CALL COLORMESSAGE(@"「あれあれっ！？　\@ TALENT:MASTER:성별 == 2 ? 兄さん # 姉さん \@このお酒飲んで平気なのっ？？」", 色３, 2, 0)
		CALL COLORMESSAGE(@"「あっはっは、こりゃあいいや！」\n「\@ TALENT:MASTER:성별 == 2 ? あんた # お前さん \@とうちらはもう飲み仲間ってことで良さそうだねえ♪」", 色２, 2, 0)
		PRINTFORML 化け狸たちはなぜかびっくりしていたが、すぐに調子を取り戻し酒を酌み交わしてくる
		PRINTFORMW お互いに話も弾み宴会の時間はにぎやかに過ぎていった…
		PRINTW …………
		PRINTW ……
		PRINTW …
		DRAWLINE
		SETCOLOR 色１
		CALL PRINT_FACE, 0, "笑顔", "服", "21"
		PRINTFORML 「いやー今日はほんとにありがと」
		PRINTFORMW 「こんなに楽しい宴会は%自称%もひさびさだよー」
		PRINTFORML 「%통칭%はもう帰るんでしょ？　お土産とかもいるんじゃなーい？」
		PRINTFORMW 「だからっ、この辺の余りそうなお酒、もう全部持ってけ持ってけー♪」
		PRINTFORML 
		PRINTFORMDL 化け狸がどぶろく、청주、鬼殺しを５本ずつ渡してくれた
		PRINTFORMDL %CALLNAME:MASTER%は礼を言って宴会場を後にした
		PRINTFORMDL +60分
		PRINTFORMW  
		TIME += 60
		BASE:MASTER:술기운 = MAX(MAXBASE:MASTER:술기운 / 3 * 2, BASE:MASTER:술기운)
		ITEM:탁주 = MIN(ITEM:탁주 + 5, 99)
		ITEM:청주 = MIN(ITEM:청주 + 5, 99)
		ITEM:오니고로시 = MIN(ITEM:오니고로시 + 5, 99)
ENDSELECT
TARGET = 0
RESETCOLOR
RETURN
