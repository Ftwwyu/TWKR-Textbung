
;========================================================================
;각 함수별 역할 정리
;T_SKILL576 : 해당 스킬의 기본 정수 스텟 처리
;T_SKILL576_STR : 해당 스킬의 기본 문자열 스텟 처리
;T_SKILL576_COLOR : 상황에 따른 출력문 색깔 처리
;ONGOING_T_SKILL576 : 스킬 사용 이후 처리 (쿨타임 감소, 지속효과 등)
;AFTER_T_SKILL576 : 스킬 사용 직후 처리
;T_SKILL576_RANGE : 스킬 사거리관련 반환값만 모아둔 처리
;
;시전자 = T_턴주인, 상대방 = T_턴상대
;========================================================================
@T_SKILL576(ARGS)
#FUNCTION
;ARGS = 인자

#DIM DYNAMIC 쿨타임
#DIM DYNAMIC 지속시간
#DIM DYNAMIC 명중률
#DIM DYNAMIC 피해량
#DIM DYNAMIC 타입
#DIM DYNAMIC HP코스트
#DIM DYNAMIC TSP코스트
;-------------------
;성능 입력
;쿨타임 0 = 한 턴에 몇 번이라도, 쿨타임 1 = 한 턴에 한번, 쿨타임 2= 두 턴에 한번
;-------------------
쿨타임 = 7;스킬의 쿨타임을 넣으세요
지속시간 = 0;스킬의 효과가 지속되는 시간을 넣으세요
명중률 = 70; 몇% 확률로 명중하는지 쓰세요
피해량 = 50 * ARG능력치(T_시전자,"지성") + 150;얼마만큼의 데미지를 주는지 쓰세요, 스텟을 참고해도 좋습니다
타입 = 버프형;공격형/방어형/회복형/상태이상형/버프형/해주형/자동형 ;ERH 변수입니다.
HP코스트 = 0;시전시 소모하는 체력의 양입니다
TSP코스트 = TSP_PERCENT(5);시전시 소모하는 TSP의 양입니다, 아나타가 아니면 소모하지 않습니다
;여러 타입을 한꺼번에 입력하고자 하는 경우:
;타입 = 공격형 + 버프형
;과 같이 + 를 포함해 입력하시면 됩니다.
;-------------------
SELECTCASE ARGS
CASE "쿨타임"
    ;쿨타임을 반환, 건들일 없다
    RESULT = 쿨타임
CASE "명중률"
    ;명중률을 반환함
    RESULT = 명중률
CASE "공격력"
    ;피해량을 반환, 공격력을 참조하려면 사용
    RESULT = 피해량
CASE "지속시간"
    RESULT = 지속시간
CASE "타입"
    RESULT = 타입
CASE "HP코스트"
    RESULT = HP코스트
CASE "TSP코스트"
    RESULT = TSP코스트
ENDSELECT
RETURNF RESULT

;문자열부
@T_SKILL576_STR(ARGS)
#FUNCTIONS
#DIMS DYNAMIC 스킬명
#DIMS DYNAMIC 속성
#DIMS DYNAMIC 계수,5
;-------------------
;성능 입력
;-------------------
스킬명 =신령을 부르는 것이 가능한 정도의 능력
속성 = 물리
;계수는 없다면 없음, 아니라면 그냥 평문
계수:0 = 없음
계수:1 = 없음
계수:2 = 없음
계수:3 = 없음
계수:4 = 없음
;-------------------
SELECTCASE ARGS
CASE "스킬명"
    RESULTS '= 스킬명
CASE "속성"
    ;피해속성을 반환
    RESULTS '= 속성
CASE "계수"
    RESULTS:0 '= 계수:0
    RESULTS:1 '= 계수:1
    RESULTS:2 '= 계수:2
    RESULTS:3 '= 계수:3
    RESULTS:4 '= 계수:4
CASEELSE
    THROW 지정되지 않은 인자 %ARGS%
ENDSELECT
RETURNF RESULTS

;표시할 때 다른 색깔을 쓰고 싶은 경우 변경
@T_SKILL576_COLOR(ARGS)
;ARGS = 인자
SELECTCASE ARGS
CASE "색깔_시전가능"
    ;시전가능, 흰색으로 빛남
    RETURN 0xFFFFFF
CASE "색깔_시전불가"
    ;시전불가, 회색임
    RETURN 0x585858
ENDSELECT

@ONGOING_T_SKILL576(사용자, ARGS, ARG = 0)
#DIM DYNAMIC 사용자
#DIM 남은지속시간, 200 ;캐릭터별 지속시간 저장 배열
#DIM 남은쿨타임, 200 ;캐릭터별 지속시간 저장 배열
#DIM DYNAMIC 피해량
#DIMS DYNAMIC 속성
피해량 = T_SKILL576("공격력")
속성 '= T_SKILL576_STR("속성")
SELECTCASE ARGS
;스킬 발동시 남은 지속시간/쿨타임 처리 부분
CASE "임의쿨타임설정"
    남은쿨타임:사용자 = ARG
CASE "임의지속시간설정"
    남은지속시간:사용자 = ARG
CASE "쿨타임설정"
    남은쿨타임:사용자 = T_SKILL576("쿨타임")
CASE "지속시간설정"
    남은지속시간:사용자 = T_SKILL576("지속시간")
CASE "턴엔드"
    ;매 턴이 지날때마다 지속시간과 쿨타임처리를 하는 부분
    IF 남은지속시간:사용자 > 0
        남은지속시간:사용자--
        CALL ONGOING_T_SKILL576(사용자, "지속효과발동")
    ENDIF
    SIF 남은쿨타임:사용자 > 0
        남은쿨타임:사용자 --
CASE "남은지속시간"
    ;남은 지속시간 표시, 건들일 없다
    RETURN 남은지속시간:사용자
CASE "남은쿨타임"
    ;남은쿨타임을 반환, 건들일 없다
    RETURN 남은쿨타임:사용자
CASE "초기화"
    VARSET 남은쿨타임
VARSET 남은지속시간
CASE "시스템체크"
    ;시스템 메시지에 따른 검사, 패시브 스킬등에 이용, 액티브는 딱히 쓸 일이 있나?

CASE "지속효과발동"
    ;지속적으로 효과가 발동하는 스킬의 경우, 여기에 효과를 입력
    IF 남은지속시간:사용자 > 0

    ENDIF
ENDSELECT

@AFTER_T_SKILL576(사용자)
#DIM DYNAMIC 사용자
#DIM DYNAMIC 명중률
#DIM DYNAMIC 피해량
#DIMS DYNAMIC 속성

;쿨타임은 미리 설정함
CALL ONGOING_T_SKILL576(사용자, "쿨타임설정")

;명중 대응, 특정상황에서 명중률을 변동하고 싶으면 사용
IF TB_SUM_REMAIN(사용자,TCV_실명)&&RAND:2 == 0
    명중률 = T_SKILL576("명중률") / 2
ELSE
    명중률 = T_SKILL576("명중률")
ENDIF
RESULT = 0
SIF T_SKILL576("HP코스트") > 0
    TCVAR:T_시전자:T_체력 -= T_SKILL576("HP코스트")
SIF T_SKILL576("TSP코스트") > 0&&T_시전자 == MASTER
    BASE:T_시전자:TSP -= T_SKILL576("TSP코스트")
IF 스킬명중판정(명중률,576)
    ;명중!
    ;----------------
    ;대사는 이쪽
    ;----------------
    
    SELECTCASE RAND:9
        CASE 0
            ;오오마가츠미, 저주
            PRINTFORML 더러움이 주위에 쏟아진다!
            CALL 통합버프(T_시전자반대,TCV_저주,576,1,7)
        CASE 1
            ;아마테라스, 통구이
            PRINTFORML 타오르는 불길이 눈 앞의 모든걸 불태운다!
            CALL T_데미지(T_시전자반대, 50 * ARG능력치(T_시전자,"지성"), "화염")
        CASE 2
            ;아마츠미카보시, 눈뽕?
            PRINTFORML 반짝이는 빛이 주위를 덮는다!
            CALL T_데미지(T_시전자반대, 20 * ARG능력치(T_시전자,"지성"), "마력")
            CALL 통합버프(T_시전자반대,TCV_실명,576,1,3)
        CASE 3
            ;아메노우즈메노미코토, 장거리 공격 면역
            PRINTFORML %T_시전자의% 몸이 흐릿해졌다!
            CALL 통합버프(T_시전자반대,TCV_회피,576,200,3)
        CASE 4
            ;이시코리메노미코토, 반사
            PRINTFORML 거대 거울이 %T_시전자의% 앞을 가린다!
            CALL T_반격기등록(T_시전자,576,3)
        CASE 5
            ;호노이카즈치노카미, 속박 통구이
            PRINTFORML 뇌우가 %T_시전자반대를% 옭아맨다!
            CALL T_데미지(T_시전자반대, 15 * ARG능력치(T_시전자,"지성"), "화염")
            CALL T_데미지(T_시전자반대, 20 * ARG능력치(T_시전자,"지성"), "전격")
            CALL 통합버프(T_시전자반대,TCV_속박,576,1,3)
        CASE 6
            ;카나야마히코노미코토, 금속 조작
            SELECTCASE RAND:2
                CASE 0
                    PRINTFORML 미세 중금속이 모여 생긴 칼날이 %T_시전자반대를% 베어낸다!
                    CALL T_데미지(T_시전자반대, 15 * ARG능력치(T_시전자,"지성"), "물리")
                    CALL 통합버프(T_시전자반대,TCV_출혈,576,1,4)
                CASE 1
                    PRINTFORML 허공에 생겨난 금속 막대가 강렬한 열과 함께 폭발한다!
                    CALL T_데미지(T_시전자반대, 15 * ARG능력치(T_시전자,"지성"), "화염")
                    CALL 통합버프(T_시전자반대,TCV_화상,576,20 * ARG능력치(T_시전자,"지성") + 400,4)
            ENDSELECT
        CASE 7
            ;이즈노메, 정화
            PRINTFORML 부정함이 사라진다!
            CALL T_해주(T_시전자,6,"모두")
            PRINTFORML {RESULT}개의 해로운 효과가 사라졌다
        CASE 8
            ;헤카티아, 와용와용
            PRINTFORML 끔찍한 형상의 티셔츠가 강제로 착용된다!
            CALL T_데미지(T_시전자반대, 17 * ARG능력치(T_시전자,"지성"), "정신")
    ENDSELECT
    ;----------------
    ;처리는 이쪽, 무언가 효과를 넣으려면 여기에 작성
    ;----------------

    피해량 = T_SKILL576("공격력")
    속성 '= T_SKILL576_STR("속성")
    ;CALL T_데미지(T_시전자반대, 피해량, 속성)
    
    RETURN 1
ELSE
    ;!감나빗!
    SETCOLOR 0xFF4444
    PRINTFORML 빗나감!
    RESETCOLOR
    ;----------------
    ;대사는 이쪽
    ;----------------
    PRINTFORML 무언가 힘을 집중한것 같지만 아무런 일도 없었다
    ;----------------
    ;처리는 이쪽, 무언가 효과를 넣으려면 여기에 작성
    ;----------------
    RETURN 0
ENDIF

@HELP_T_SKILL576
;스킬의 설명을 출력하는 함수
;적당히 8줄 이하의 설명을 써두자
PRINTFORML 무녀와 그 비슷한 자들의 능력
PRINTFORML
PRINTFORML 신들을 불러내 전투를 돕게 한다
PRINTFORML
PRINTFORML 무슨 일이 일어날지는 불러낸 신에 달려있다
PRINTFORML
PRINTFORML
PRINTFORML 자고로 신이란 24시간 호출 가능 무급 노예라는 것이다

@IS_UTIMATE576
;TRYCALLFORM으로 호출되어 이 스킬의 습득을 억제하는 함수
;FUCTION으로 고치면 안됨, 1인경우 습득 억제, 0인 경우 습득 허가
RETURN 1

@DENY_SILENCE576
;TRYCALLFORM으로 호출되어 이 스킬이 침묵에 영향을 받지 않게 하는 함수
;FUCTION으로 고치면 안됨, 2이상 인경우 침묵 무시, 0-1인 경우 영향없음, 숫자가 높을 수록 강한 침묵기에도 저항 가능
RETURN 1

@SKILLTYPE_MOVE576
;TRYCALLFORM으로 호출되어 이 스킬이 이동기임을 나타내는 함수
;FUCTION으로 고치면 안됨, 2인경우 통상이동기, 3일경우 순간이동, 0인 경우 이동기 아님
RETURN 0

@RV_CONDITION_T_SKILL576(사용자,피해량,원래피해량,속성)
#DIMS DYNAMIC 속성
#DIM DYNAMIC 원래피해량
#DIM DYNAMIC 사용자
#DIM DYNAMIC 피해량
;반격기의 실행 조건, 2을 리턴하면 시전가능, 아니면 시전 불가능
;반격기가 아니면 0으로
SIF T_턴주인 == 사용자
    RETURN 0
SIF TB_SUM_VALUE(T_시전자,TCV_자율,576) >= 5
    RETURN 0
RETURN 2

@RV_AFTER_T_SKILL576(사용자,받은피해량,받은원래피해량,받은속성)
#DIMS DYNAMIC 받은속성
#DIM DYNAMIC 받은원래피해량
#DIM DYNAMIC 사용자
#DIM DYNAMIC 피해량
#DIM DYNAMIC 받은피해량
#DIMS 속성
피해량 = T_SKILL576("공격력")
속성 '= T_SKILL576_STR("속성")
;반격기의 내용
IF T_반격타이밍 == 1
    CALL PRINT_SPELLNAME(576)
    DRAWLINE
    PRINTFORML 거울에 공격이 튕겨난다!
    IF TB_SUM_REMAIN(T_시전자,TCV_면역) == 0
        CALL 통합버프(T_시전자,TCV_면역,576,1,1)
    ENDIF 
ELSEIF T_반격타이밍 == 2
    CALL PRINT_SPELLNAME(576)
    IF 스킬명중판정(70,576)
        PRINTFORML %T_시전자반대는% 튕겨진 공격에 맞았다!
        CALL T_데미지스택(T_시전자반대,MIN(받은원래피해량, 60 * ARG능력치(T_시전자,"지성")),속성)
    ELSE
        PRINTFORML 튕겨난 공격은 저 멀리 %T_시전자반대의% 뒤로 날아갔다..
    ENDIF
    CALL 통합버프(T_시전자,TCV_자율,576,1,2)
ENDIF
RETURN 0

@T_SKILL576_RANGE(ARGS)
#FUNCTION
#DIM DYNAMIC 최대사거리
#DIM DYNAMIC 최적사거리
#DIM DYNAMIC 범위변동단위
#DIM DYNAMIC 조준속성
#DIM DYNAMIC 유도력
;-------------------
;성능 입력
;-------------------
최대사거리 = 1; 스킬이 닿는 최대 거리
최적사거리 = 1; 명중률 +10% 가 붙는 거리
범위변동단위 = 0; 몇 m마다 명중률이 1단위 변할지, 최적사거리에 이 값이 5번 더해질때 최악의 보정값(-20)이 된다
조준속성 = T_버프기;T_근거리/T_고정탄/T_조준탄/T_유도탄/T_즉발탄/T_버프기
유도력 = 0;유도 타입 스킬이 적과의 속도차이를 무시하는 정도, '유도'타입이 아니면 무시해도 됨
SELECTCASE ARGS
CASE "최대사거리"
    RESULT = 최대사거리
CASE "최적사거리"
    RESULT = 최적사거리
CASE "범위변동단위"
    RESULT = 범위변동단위
CASE "조준속성"
    RESULT = 조준속성
CASE "유도력"
    RESULT = 유도력
ENDSELECT
RETURNF RESULT
