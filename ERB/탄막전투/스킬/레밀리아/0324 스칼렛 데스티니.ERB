
;========================================================================
;각 함수별 역할 정리
;T_SKILL324 : 해당 스킬의 기본 정수 스텟 처리
;T_SKILL324_STR : 해당 스킬의 기본 문자열 스텟 처리
;T_SKILL324_COLOR : 상황에 따른 출력문 색깔 처리
;ONGOING_T_SKILL324 : 스킬 사용 이후 처리 (쿨타임 감소, 지속효과 등)
;AFTER_T_SKILL324 : 스킬 사용 직후 처리
;
;시전자 = T_턴주인, 상대방 = T_턴상대
;========================================================================
@T_SKILL324(ARGS)
#FUNCTION
;ARGS = 인자

#DIM 쿨타임
#DIM 지속시간
#DIM 명중률
#DIM 피해량
#DIM 타입
#DIM HP코스트
#DIM TSP코스트
;-------------------
;성능 입력
;쿨타임 0 = 한 턴에 몇 번이라도, 쿨타임 1 = 한 턴에 한번, 쿨타임 2= 두 턴에 한번
;-------------------
쿨타임 = 8;스킬의 쿨타임을 넣으세요
지속시간 = 0;스킬의 효과가 지속되는 시간을 넣으세요
명중률 = 80; 몇% 확률로 명중하는지 쓰세요
피해량 = 85 * ARG능력치(T_시전자, "힘")+( 65 * ARG능력치(T_시전자, "속도")) + 1500;얼마만큼의 데미지를 주는지 쓰세요, 스텟을 참고해도 좋습니다
타입 = 공격형;공격형/방어형/회복형/상태이상형/버프형/해주형/자동형 ;ERH 변수입니다.
HP코스트 = HP_PERCENT(T_시전자, 5);시전시 소모하는 체력의 양입니다
TSP코스트 = TSP_PERCENT(40) + 250;시전시 소모하는 TSP의 양입니다, 아나타가 아니면 소모하지 않습니다
;여러 타입을 한꺼번에 입력하고자 하는 경우:
;타입 = 공격형 + 버프형
;과 같이 + 를 포함해 입력하시면 됩니다.
;-------------------
SELECTCASE ARGS
CASE "쿨타임"
    ;쿨타임을 반환, 건들일 없다
    RESULT = 쿨타임
CASE "명중률"
    ;명중률을 반환함
    RESULT = 명중률
CASE "공격력"
    ;피해량을 반환, 공격력을 참조하려면 사용
    RESULT = 피해량
CASE "지속시간"
    RESULT = 지속시간
CASE "타입"
    RESULT = 타입
CASE "HP코스트"
    RESULT = HP코스트
CASE "TSP코스트"
    RESULT = TSP코스트
ENDSELECT
RETURNF RESULT

;문자열부
@T_SKILL324_STR(ARGS)
#FUNCTIONS
#DIMS DYNAMIC 스킬명
#DIMS DYNAMIC 속성
#DIMS DYNAMIC 계수,5
;-------------------
;성능 입력
;-------------------
스킬명 = 「스칼렛 데스티니」
속성 = 물리
;계수는 없다면 없음, 아니라면 그냥 평문
계수:0 = 85 * 힘
계수:1 = 65 * 속도
계수:2 = 1500
계수:3 = 없음
계수:4 = 없음
;-------------------
SELECTCASE ARGS
CASE "스킬명"
    RESULTS '= 스킬명
CASE "속성"
    ;피해속성을 반환
    RESULTS '= 속성
CASE "계수"
    RESULTS:0 '= 계수:0
    RESULTS:1 '= 계수:1
    RESULTS:2 '= 계수:2
    RESULTS:3 '= 계수:3
    RESULTS:4 '= 계수:4
CASEELSE
    THROW 지정되지 않은 인자 %ARGS%
ENDSELECT
RETURNF RESULTS

;표시할 때 다른 색깔을 쓰고 싶은 경우 변경
@T_SKILL324_COLOR(ARGS)
;ARGS = 인자
SELECTCASE ARGS
CASE "색깔_시전가능"
    ;시전가능, 흰색으로 빛남
    RETURN 0xFFFFFF
CASE "색깔_시전불가"
    ;시전불가, 회색임
    RETURN 0x585858
ENDSELECT

@ONGOING_T_SKILL324(사용자, ARGS)
#DIM 사용자
#DIM 남은지속시간, 200 ;캐릭터별 지속시간 저장 배열
#DIM 남은쿨타임, 200 ;캐릭터별 지속시간 저장 배열
#DIM 피해량
#DIMS 속성
피해량 = T_SKILL324("공격력")
속성 '= T_SKILL324_STR("속성")
SELECTCASE ARGS
;스킬 발동시 남은 지속시간/쿨타임 처리 부분
CASE "쿨타임설정"
    남은쿨타임:사용자 = T_SKILL324("쿨타임")
CASE "지속시간설정"
    남은지속시간:사용자 = T_SKILL324("지속시간")
CASE "턴엔드"
    ;매 턴이 지날때마다 지속시간과 쿨타임처리를 하는 부분
    IF 남은지속시간:사용자 > 0
        남은지속시간:사용자--
        CALL ONGOING_T_SKILL324(사용자, "지속효과발동")
    ENDIF
    SIF 남은쿨타임:사용자 > 0
        남은쿨타임:사용자 --
CASE "남은지속시간"
    ;남은 지속시간 표시, 건들일 없다
    RETURN 남은지속시간:사용자
CASE "남은쿨타임"
    ;남은쿨타임을 반환, 건들일 없다
    RETURN 남은쿨타임:사용자
CASE "초기화"
    VARSET 남은쿨타임
VARSET 남은지속시간
CASE "시스템체크"
    ;시스템 메시지에 따른 검사, 패시브 스킬등에 이용, 액티브는 딱히 쓸 일이 있나?

CASE "지속효과발동"
    ;지속적으로 효과가 발동하는 스킬의 경우, 여기에 효과를 입력
    IF 남은지속시간:사용자 > 0

    ENDIF
ENDSELECT

@AFTER_T_SKILL324(사용자)
#DIM 사용자
#DIM 명중률
#DIM 피해량
#DIMS 속성

;쿨타임은 미리 설정함
CALL ONGOING_T_SKILL324(사용자, "쿨타임설정")

;명중 대응, 특정상황에서 명중률을 변동하고 싶으면 사용
IF TB_SUM_REMAIN(사용자,TCV_실명)&&RAND:2 == 0
    명중률 = T_SKILL324("명중률") / 2
ELSE
    명중률 = T_SKILL324("명중률")
ENDIF
RESULT = 0
CALLF T_SKILL324("HP코스트")
SIF RESULT > 0
    TCVAR:T_시전자:T_체력 -= RESULT
CALLF T_SKILL324("TSP코스트")
SIF RESULT > 0&&T_시전자 == MASTER
    BASE:T_시전자:TSP -= RESULT
IF 스킬명중판정(명중률,324)
    ;명중!
    ;----------------
    ;대사는 이쪽
    ;----------------
    PRINTFORML 칼날과도 같은 핏줄기를 튀기며 달려든 %T_시전자의% 일격에 %T_시전자반대가% 깊히 베였다! 
    ;----------------
    ;처리는 이쪽, 무언가 효과를 넣으려면 여기에 작성
    ;----------------

    피해량 = T_SKILL324("공격력")
    속성 '= T_SKILL324_STR("속성")
    CALL T_데미지(T_턴반대, 피해량, 속성)
    IF TB_SUM_REMAIN(T_시전자반대,TCV_출혈)
        PRINTFORML %T_시전자반대의% 상처를 찢어발겼다!
        CALL T_데미지(T_턴반대, 피해량/20, "출혈")
    ELSE
        CALL 통합버프(T_시전자반대,TCV_출혈,324,1,6)
    ENDIF
    CALL ONGOING_T_SKILL324(사용자, "지속시간설정")
    CALL TB_돌진(T_시전자,T_시전자반대,-5,T_SKILL324_RANGE("최대사거리"))
    RETURN 1
ELSE
    ;!감나빗!
    SETCOLOR 0xFF4444
    PRINTFORML 빗나감!
    RESETCOLOR
    ;----------------
    ;대사는 이쪽
    ;----------------
    PRINTFORML 고속으로 돌진한 %T_시전자는% 아슬아슬하게 %T_시전자반대의% 옷깃을 스치고 지나갔다...
    CALL TB_돌진(T_시전자,T_시전자반대,5,T_SKILL324_RANGE("최대사거리"))
    ;----------------
    ;처리는 이쪽, 무언가 효과를 넣으려면 여기에 작성
    ;----------------
    RETURN 0
ENDIF

@HELP_T_SKILL324
;스킬의 설명을 출력하는 함수
;적당히 8줄 이하의 설명을 써두자
PRINTFORML 레밀리아의 라스트워드
PRINTFORML
PRINTFORML 초고속으로 적에게 달려들어 찢어발긴다
PRINTFORML 피냄새가 나는 적이라면 그 상처를 노려 후벼파내어 더욱 큰 피해를 줄것이다
PRINTFORML
PRINTFORML 그녀가 그 작은 몸으로도 수많은 승리를 거머쥘수 있는것은 특유의 강렬한 힘때문이다
PRINTFORML 외관에 맞지않는 파괴적인 무력, 홍마관이 지금까지 있을수 있는 이유다
PRINTFORML 그 댓가인지 정신 상태는 심히 모자라졌다는게 문제지

@IS_UTIMATE324
;TRYCALLFORM으로 호출되어 이 스킬의 습득을 억제하는 함수
;FUCTION으로 고치면 안됨, 1인경우 습득 억제, 0인 경우 습득 허가
RETURN 1

@DENY_SILENCE324
;TRYCALLFORM으로 호출되어 이 스킬이 침묵에 영향을 받지 않게 하는 함수
;FUCTION으로 고치면 안됨, 2인경우 침묵 무시, 0인 경우 영향없음
RETURN 0

@SKILLTYPE_MOVE324
;TRYCALLFORM으로 호출되어 이 스킬이 이동기임을 나타내는 함수
;FUCTION으로 고치면 안됨, 2인경우 통상이동기, 3일경우 순간이동, 0인 경우 이동기 아님
RETURN 2

@T_SKILL324_RANGE(ARGS)
#FUNCTION
#DIM DYNAMIC 최대사거리
#DIM DYNAMIC 최적사거리
#DIM DYNAMIC 범위변동단위
#DIM DYNAMIC 조준속성
#DIM DYNAMIC 유도력
;-------------------
;성능 입력
;-------------------
최대사거리 = 40; 스킬이 닿는 최대 거리
최적사거리 = 15; 명중률 +10% 가 붙는 거리
범위변동단위 = 4; 몇 m마다 명중률이 1단위 변할지, 최적사거리에 이 값이 5번 더해질때 최악의 보정값(-20)이 된다
조준속성 = T_조준탄 ;T_근거리/T_고정탄/T_조준탄/T_유도탄/T_즉발탄/T_버프기
유도력 = 0;유도 타입 스킬이 적과의 속도차이를 무시하는 정도, '유도'타입이 아니면 무시해도 됨
SELECTCASE ARGS
CASE "최대사거리"
    RESULT = 최대사거리
CASE "최적사거리"
    RESULT = 최적사거리
CASE "범위변동단위"
    RESULT = 범위변동단위
CASE "조준속성"
    RESULT = 조준속성
CASE "유도력"
    RESULT = 유도력
ENDSELECT
RETURNF RESULT