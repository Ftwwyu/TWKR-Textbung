@T_SPELL_LIST_PRINT(번호,표시여부,메모라이즈)
#DIM DYNAMIC 번호
#DIM DYNAMIC 표시여부
#DIM DYNAMIC 메모라이즈
RESETCOLOR
IF 표시여부 == 1
    IF 메모라이즈
        SETCOLOR 0xFF7F00
    ENDIF
    PRINTFORM [{번호,4}] 
    PRINTFORM - %스펠이름(번호),50,LEFT% 
ELSE
    SETCOLOR 0x808080
    PRINTFORM [{번호,4}] 
    PRINTFORM - ---------------------------------------------------
ENDIF
RESETCOLOR
PRINT │　　　
;IF 메모라이즈
;    PRINT 메모라이즈
;ENDIF
;PRINTL

@메모라이즈
#DIM DYNAMIC 페이지
#DIM DYNAMIC 카운터
#DIM DYNAMIC 입력
#DIM DYNAMIC 출력형태
#DIM DYNAMIC 미획득생략
#DIMS DYNAMIC 검색문구
#DIM DYNAMIC 현재선택
[IF_DEBUG]
    탄막디버그 = 1
[ENDIF]
$메모라이즈초기루프
DRAWLINE
LOCAL:3 = 0
IF 현재선택
    CALLFORMF T_SKILL{현재선택}_STR("스킬명")
    FONTSTYLE 1
    PRINTFORML %RESULTS%
    FONTSTYLE 0
    PRINT 타입　　　　 :
    CALL 타입표시(현재선택)
    CALLFORMF T_SKILL{현재선택}_STR("속성")
    PRINTFORM 속성　　　　 : %RESULTS,20,LEFT%
    CALLFORMF T_SKILL{현재선택}("명중률")
    PRINTFORML 명중률　　　 : {RESULT,10,LEFT}％
    CALLFORMF T_SKILL{현재선택}("쿨타임")
    PRINTFORML 쿨타임　　　 : {RESULT,10,LEFT}턴
    CALLFORMF T_SKILL{현재선택}("공격력")
    PRINTFORML 예상　피해량 : {RESULT,20,LEFT}
    CALLFORMF T_SKILL{현재선택}("HP코스트")
    PRINTFORM ＨＰ소모량　 : {RESULT,20,LEFT}
    CALLFORMF T_SKILL{현재선택}("마력코스트")
    PRINTFORML 마력　소모량 : {RESULT,20,LEFT}
    CALLFORMF T_SKILL{현재선택}_STR("계수")
    IF RESULTS:0 != "없음" && RESULTS:0 != ""
        PRINTFORM 계수　　　　 : 
        FOR LOCAL:1,0,4
            IF RESULTS:(LOCAL:1) != "없음" && RESULTS:(LOCAL:1) != ""
                IF LOCAL:1 != 0
                    PRINTFORM  + 
                ENDIF
                FONTSTYLE 1
                PRINTFORM %RESULTS:(LOCAL:1)%
                FONTSTYLE 0
            ENDIF
        NEXT
        PRINTFORM  / 타입 :
        CALLFORMF T_SKILL{현재선택}_RANGE("조준속성")
        SELECTCASE RESULT
            CASE T_근거리
                PRINT  근거리
            CASE T_고정탄
                PRINT  고정탄
            CASE T_조준탄
                PRINT  조준탄
            CASE T_유도탄
                PRINT  유도탄
            CASE T_즉발탄
                PRINT  즉발
            CASE T_버프기
                PRINT  버프기
        ENDSELECT
    ENDIF
    PRINTL
    DRAWLINE
    FONTSTYLE 2
    CALLFORM HELP_T_SKILL{현재선택}
    FONTSTYLE 0
    DRAWLINE
    LOCAL:3 = 1
ENDIF
PRINTFORML
PRINTFORM 어떤 스킬을 메모라이즈 할까? (현재 메모라이즈 수 : {NUM_MEMORIZED()}/50)   
PRINT 　　
PRINTBUTTON "[프리셋]" , 893893893
PRINT 　　
IF 출력형태 != 1
    PRINTBUTTON "[검색모드]" , 514514
ELSE
    PRINTBUTTON "[통상모드]" , 514514
ENDIF
PRINT 　　
IF !미획득생략
    PRINTBUTTON "[미획득생략]" , 12345678
ELSE
    PRINTBUTTON "[미획득생략해제]" , 12345678
ENDIF
PRINTFORML
DRAWLINE
카운터 = 1
LOCAL:1 = 0
LOCAL:2 = 0
REPEAT 50
    $LOOP
    IF 카운터 >= T_스킬한계값
        CALL T_SPELL_LIST_PRINT(카운터,0,0)
        카운터++
        LOCAL:2 = 1
    ELSEIF TALENTNAME:(500+카운터) == ""
        카운터++
        GOTO LOOP
    ELSEIF STRFIND(TALENTNAME:(500+카운터), 검색문구) == -1 && 출력형태 == 1
        카운터++
        GOTO LOOP
    ELSE
        IF 페이지 > 0
            IF LOCAL:1 < 페이지*25
                LOCAL:1++
                카운터++
                GOTO LOOP
            ENDIF
        ENDIF
        IF HAVE_T_SKILL(MASTER,카운터)
            CALL T_SPELL_LIST_PRINT(카운터,1,IS_MEMORIZED(카운터))
            SIF 카운터 % 2 == 0&&카운터 != 0
                PRINTL
        ELSE
            IF 미획득생략
                카운터++
                GOTO LOOP
            ELSE
                CALL T_SPELL_LIST_PRINT(카운터,0,0)
            ENDIF
        ENDIF
        카운터++
    ENDIF
REND
PRINTL
DRAWLINE
IF 페이지 == 0
    SETCOLOR 0x808080
ELSE
    RESETCOLOR
ENDIF
PRINTLC [9997] - 이전 페이지
RESETCOLOR

PRINTLC [9998] - 종료

IF LOCAL:2 == 1
    SETCOLOR 0x808080
ELSE
    RESETCOLOR
ENDIF
PRINTLC [9999] - 다음 페이지
RESETCOLOR
PRINTFORML [9996] 마도서와 요마서
INPUT
입력 = RESULT
IF 입력 == 9998
    탄막디버그 = 0
    RETURN 1
ELSEIF 입력 == 9997&&페이지 > 0
    페이지 = MAX(0,페이지-1)
ELSEIF 입력 == 9999&&LOCAL:2 == 0
    페이지++
ELSEIF 입력 == 12345678
    미획득생략 = !미획득생략
ELSEIF 입력 == 893893893
    CALL 스킬프리셋생성
ELSEIF 입력 == 514514
    IF 출력형태 == 0
        출력형태 = 1
        $검색어루프
        PRINT 검색어를 입력해 주세요
        INPUTS
        IF STRLENS(RESULTS) >= 20
            PRINTFORMW 너무 깁니다
            CLEARLINE 2
            GOTO 검색어루프
        ELSE
            검색문구 = %RESULTS%
            CLEARLINE 2
        ENDIF
    ELSE
        출력형태 = 0
    ENDIF
ELSEIF 입력 == 9996
    CALL T_마도서설명
    LOCAL += RESULT
    RESULT = 0
    ;CALL T_요마서설명
    LOCAL += RESULT
    WAIT
    CLEARLINE LOCAL
ELSE
    IF 입력 > T_스킬한계값

    ELSEIF HAVE_T_SKILL(MASTER,입력)
        IF IS_MEMORIZED(입력)
            CALL UNMEMORIZESPELL(입력)
            현재선택 = 0
            IF LOCAL:3
                CLEARLINE 17
            ENDIF
        ELSE
            CALL MEMORIZESPELL(입력)
            현재선택 = 입력
        ENDIF
    ENDIF
ENDIF
IF 현재선택 && LOCAL:3
    CLEARLINE 17
ENDIF
CLEARLINE 33
GOTO 메모라이즈초기루프

@스펠이름(ARG)
#FUNCTIONS
CALLFORMF T_SKILL{ARG}_STR("스킬명")
RETURNF RESULTS

@IS_MEMORIZED(ARG)
#FUNCTION
FOR LOCAL,0,50
    IF T_메모라이즈:LOCAL == ARG
        RETURNF 1
    ENDIF
NEXT
RETURNF 0

@MEMORIZESPELL(ARG)
FOR LOCAL,0,50
    IF T_메모라이즈:LOCAL == 0
        T_메모라이즈:LOCAL = ARG
        BREAK
    ENDIF
NEXT

@UNMEMORIZESPELL(ARG)
FOR LOCAL,0,50
    IF T_메모라이즈:LOCAL == ARG
        T_메모라이즈:LOCAL = 0
    ENDIF
NEXT

@NUM_MEMORIZED
#FUNCTION
LOCAL:1=0
FOR LOCAL,0,50
    IF T_메모라이즈:LOCAL != 0
        LOCAL:1 ++
    ENDIF
NEXT
RETURNF LOCAL:1



@SET_PRESET(ARG)
FOR LOCAL,0,50
    IF T_메모라이즈:LOCAL != 0
        SKILL_PRESET:ARG:LOCAL = T_메모라이즈:LOCAL
    ENDIF
NEXT

@CLEAR_PRESET(ARG)
FOR LOCAL,0,50
    SKILL_PRESET:ARG:LOCAL = 0
NEXT

@SET_TO_PRESET(ARG)
FOR LOCAL,0,50
    T_메모라이즈:LOCAL = SKILL_PRESET:ARG:LOCAL
NEXT

@IS_PRESET_(ARG)
#FUNCTION
IF ARG > 5
    RETURNF 0
ENDIF
IF NAME_SKILL_PRESET:ARG != ""
    RETURNF 1
ENDIF
RETURNF 0

@스킬프리셋생성
#DIM CONST 프리셋한계 = 6
PRINTL
PRINTL
PRINTBUTTON "[현재 메모라이즈 된 것을 프리셋으로]" , 0
PRINTL
PRINTBUTTON "[프리셋삭제 모드]" , 6974
PRINTL
PRINTBUTTON "[이름 변경]" , 5882
PRINTL
PRINTL
FOR LOCAL,1,프리셋한계
    IF IS_PRESET_(LOCAL)
        PRINTBUTTON @"[%NAME_SKILL_PRESET:LOCAL%]" , LOCAL
        PRINTL
    ELSE
        SETCOLOR 0x808080
        PRINTFORML [-------------------]
        RESETCOLOR
    ENDIF
NEXT
PRINTL
PRINTL
PRINTBUTTON "[취소]" , 893893
$LOOP
INPUT
IF RESULT == 0
    $입력표시루프
    PRINTL
    PRINTFORML 어디에 저장합니까?
    FOR LOCAL,1,프리셋한계
        IF IS_PRESET_(LOCAL)
            PRINTBUTTON @"[%NAME_SKILL_PRESET:LOCAL%]" , LOCAL
            PRINTL
        ELSE
            SETCOLOR 0x808080
            PRINTBUTTON "[-------------------]" , LOCAL
            PRINTL
            RESETCOLOR
        ENDIF
    NEXT
    PRINTL
    PRINTL
    PRINTBUTTON "[취소]" , 893893
    $입력루프
    INPUT
    LOCAL:1 = RESULT
    IF LOCAL:1 == 893893
        RESTART
    ELSEIF INRANGE(LOCAL:1,1,프리셋한계)
        IF IS_PRESET_(LOCAL:1)
            PRINTFORML 이 프리셋에는 저장된 내용이 있습니다, 덮어씁니까?
            CALL ASK_YN()
            IF RESULT
                GOTO 입력표시루프
            ENDIF
        ENDIF
        PRINTFORML 프리셋의 이름을 지정해 주세요
        $프리셋이름입력루프
        INPUTS
        IF STRLENS(RESULTS) > 20
            PRINTFORMW 전각 10자 이상 등록할 수 없습니다
        ELSEIF RESULTS == ""
            GOTO 프리셋이름입력루프
        ELSE
            NAME_SKILL_PRESET:(LOCAL:1) '= RESULTS
        ENDIF
        CALL SET_PRESET(LOCAL:1)
    ELSE
        GOTO 입력루프
    ENDIF
ELSEIF RESULT == 6974
    $삭제표시루프
    PRINTL
    PRINTFORML 어떤 프리셋을 삭제합니까?
    FOR LOCAL,1,프리셋한계
        IF IS_PRESET_(LOCAL)
            PRINTBUTTON @"[%NAME_SKILL_PRESET:LOCAL%]" , LOCAL
            PRINTL
        ELSE
            SETCOLOR 0x808080
            PRINTFORML [-------------------]
            RESETCOLOR
        ENDIF
    NEXT
    PRINTL
    PRINTL
    PRINTBUTTON "[취소]" , 893893
    $삭제루프
    INPUT
    IF RESULT == 893893
        RESTART
    ELSEIF INRANGE(RESULT,1,프리셋한계)
        CALL CLEAR_PRESET(RESULT)
        VARSET NAME_SKILL_PRESET:RESULT
    ELSE
        GOTO 삭제루프
    ENDIF
    GOTO 삭제표시루프
ELSEIF RESULT == 5882
    $개명표시루프
    PRINTL
    PRINTFORML 어떤 프리셋의 이름을 변경합니까?
    FOR LOCAL,1,프리셋한계
        IF IS_PRESET_(LOCAL)
            PRINTBUTTON @"[%NAME_SKILL_PRESET:LOCAL%]" , LOCAL
            PRINTL
        ELSE
            SETCOLOR 0x808080
            PRINTFORML [-------------------]
            RESETCOLOR
        ENDIF
    NEXT
    PRINTL
    PRINTL
    PRINTBUTTON "[취소]" , 893893
    $개명루프
    INPUT
    LOCAL:1 = RESULT
    IF LOCAL:1 == 893893
        RESTART
    ELSEIF INRANGE(LOCAL:1,1,프리셋한계) && IS_PRESET_(LOCAL:1)
        PRINTFORML 프리셋의 이름을 지정해 주세요
        $프리셋이름개명루프
        INPUTS
        IF STRLENS(RESULTS) > 20
            PRINTFORMW 전각 10자 이상 등록할 수 없습니다
        ELSEIF RESULTS == ""
            GOTO 프리셋이름개명루프
        ELSE
            NAME_SKILL_PRESET:(LOCAL:1) '= RESULTS
        ENDIF
    ELSE
        GOTO 개명루프
    ENDIF
    GOTO 개명표시루프
ELSEIF RESULT == 893893
    RETURN
ELSEIF RESULT <= 프리셋한계 && RESULT > 0 && IS_PRESET_(RESULT)
    LOCAL:1 = RESULT
    PRINTFORML "%NAME_SKILL_PRESET:(LOCAL:1)%" 프리셋으로 변경합니까?
    CALL ASK_YN()
    IF RESULT
        RESTART
    ENDIF
    CALL SET_TO_PRESET(LOCAL:1)
    RETURN
ELSE
    GOTO LOOP
ENDIF
RESTART