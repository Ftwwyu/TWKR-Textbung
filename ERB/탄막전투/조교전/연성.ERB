@연성시스템
#DIM 대상
$연성루프
PRINTFORML 누구의 혼을 이용할까?
DRAWLINE
FOR LOCAL, 0, CHARANUM
    IF 습득한혼:LOCAL >= 1
        PRINTFORML [{LOCAL}] %CALLNAME:LOCAL%의 혼
    ENDIF
NEXT
DRAWLINE
SIF TALENT:MASTER:유령
    PRINTFORML [9997] 스스로의 혼
PRINTFORML [9998] 흐릿한 혼
PRINTFORML [9999] 돌아간다
INPUT
IF RESULT == 9999
    RETURN 1
ELSEIF RESULT == 9998
    CALL 모브코의영혼바치기_경험치()
ELSEIF RESULT == 9997 && TALENT:MASTER:유령
    CALL 셀프부활
ELSE
    IF RESULT < 0||RESULT > 150
        GOTO 연성루프
    ELSE
        IF 습득한혼:RESULT == 0
            GOTO 연성루프
        ELSE
            ;가능
            대상 = RESULT
            PRINTFORML 무엇을 할까?
            DRAWLINE
            PRINTFORMLC [1] 스펠카드 연성
            SIF TALENT:RESULT:사망 == 1
                PRINTFORMLC [2] 환생
            PRINTFORMLC [3] 흡수
            PRINTL
            ;PRINTFORMLC [4] 해방
            PRINTFORMLC [6] 돌아간다
            $연성입력루프
            INPUT
            SELECTCASE RESULT
                CASE 1
                    CALL 연성가능한스킬(대상)
                CASE 2
                    IF TALENT:대상:사망 == 1
                        CALL 부활시키기(대상)
                    ELSE
                        GOTO 연성입력루프
                    ENDIF
                CASE 3
                    CALL 영혼흡수(대상)
                ;CASE 4

                ;CASE 5

                CASE 6
                    GOTO 연성루프
                CASEELSE
                    GOTO 연성입력루프
            ENDSELECT
            
        ENDIF
    ENDIF
ENDIF
GOTO 연성루프

@연성가능한스킬(ARG)
#DIM DYNAMIC 궁극기배열, 3000
#DIM DYNAMIC 배열포인터
#DIM DYNAMIC 모브코사용
#DIM DYNAMIC 모브코요구량
$LOOP
FOR LOCAL,0,T_스킬한계값
    IF HAVE_T_SKILL(ARG,LOCAL) == 1&&HAVE_T_SKILL(MASTER,LOCAL) == 0
        RESULT = 0
        TRYCALLFORM IS_UTIMATE{LOCAL}()
        IF RESULT == 1
            궁극기배열:배열포인터 = LOCAL
            CALLFORMF T_SKILL{LOCAL}_STR("스킬명")
            PRINTFORML [{배열포인터}] %RESULTS%
            배열포인터++
        ENDIF
        RESULT = 0
    ENDIF
NEXT
PRINTFORML [9998] \@모브코사용 ? 흐릿한 혼 사용 # 일반 연성\@
PRINTFORML [9999] 돌아간다
INPUT
IF RESULT == 9999
    RETURN 1
ELSEIF RESULT == 9998
    모브코사용 = !모브코사용
    GOTO LOOP
ELSE
    IF RESULT < 배열포인터&&RESULT >= 0
        IF !모브코사용
            습득한혼:ARG --
        ELSE
            PRINTFORML 대량의 흐릿한 혼과 원래 영혼의 일부를 소모해 연성할 수 있습니다
            모브코요구량 = 500
            PRINTFORML 요구량: 약{모브코요구량,10,LEFT}　소지량: {모브코의영혼총합()}
            IF 모브코요구량 > 모브코의영혼총합()
                PRINTFORMW 흐릿한 혼이 부족합니다
                GOTO LOOP
            ELSE
                PRINTFORML 진행합니까?
                CALL ASK_YN
                IF !RESULT
                    CALL 모브코영혼소모(모브코요구량)
                    CFLAG:ARG:호감도 = 0
                    CFLAG:ARG:신뢰도 = 0
                    ABL:ARG:친밀 = 0
                    ABL:ARG:욕망 = 0
                    ABL:ARG:전투능력 = MAX(0,ABL:ARG:전투능력 - 1)
                    LOCAL = ABL:ARG:레벨
                    ABL:ARG:레벨 = MAX(0,ABL:ARG:레벨 * 1 / 2 - 1)
                    LOCAL = LOCAL - ABL:ARG:레벨
                    $LOOP1
                    IF LOCAL && CFLAG:ARG:인내 >= 2
                        CFLAG:ARG:인내 -= 1
                        LOCAL--
                    ENDIF
                    IF LOCAL && CFLAG:ARG:힘 >= 2
                        CFLAG:ARG:힘 -= 1
                        LOCAL--
                    ENDIF
                    IF LOCAL && CFLAG:ARG:속도 >= 2
                        CFLAG:ARG:속도 -= 1
                        LOCAL--
                    ENDIF
                    IF LOCAL && CFLAG:ARG:운 >= 2
                        CFLAG:ARG:운 -= 1
                        LOCAL--
                    ENDIF
                    IF LOCAL && CFLAG:ARG:지성 >= 2
                        CFLAG:ARG:지성 -= 1
                        LOCAL--
                    ENDIF
                    IF LOCAL > 0
                        IF CFLAG:ARG:인내 <= 1 &&CFLAG:ARG:힘 <= 1 &&CFLAG:ARG:지성 <= 1 &&CFLAG:ARG:속도 <= 1 &&CFLAG:ARG:운 <= 1
                            CFLAG:ARG:물리내성 -= 레벨업내성증가
                            LOCAL--
                        ENDIF
                        GOTO LOOP1
                    ENDIF
                    IF ABL:ARG:레벨 < 10
                        PRINTFORMW 한계까지 쥐어짜내진 %ARG의% 영혼은 그 힘을 잃고 소멸했습니다
                        습득한혼:ARG --
                    ENDIF
                ELSE
                    RETURN
                ENDIF
            ENDIF
        ENDIF
        TALENT:MASTER:(500+(궁극기배열:RESULT)) = 1
    ENDIF
ENDIF
@셀프부활
#DIM DYNAMIC 소모코스트
소모코스트 = 5000 + (MAXBASE:MASTER:TSP/10)
PRINTFORML 사라진 몸을 대신할 새 몸을 연성합니까?
PRINTFORML 대가로 최대 TSP 보유량이 {소모코스트} 만큼 감소합니다
PRINTFORML 현재 최대 TSP: ({MAXBASE:MASTER:TSP})
PRINTFORML 소생 후 최대 TSP: ({MAXBASE:MASTER:TSP - 소모코스트})
IF MAXBASE:MASTER:TSP - 소모코스트 <= 10
    PRINTL
    PRINTFORMW TSP가 부족합니다, TSP는 지불후 최소 10 이상 남아야 합니다
    RETURN
ENDIF
CALL ASK_YN
IF !RESULT
    PRINTFORML TSP를 소비해 인간회귀를 합니다
    MAXBASE:MASTER:TSP -= 소모코스트
    IF MAXBASE:MASTER:TSP > 2500
        MONEY:1 = 20000
    ELSEIF MAXBASE:MASTER:TSP > 1500
        MONEY:1 = 7500
    ELSEIF MAXBASE:MASTER:TSP > 800
        MONEY:1 = 3400
    ELSEIF MAXBASE:MASTER:TSP > 500
        MONEY:1 = 1125
    ELSEIF MAXBASE:MASTER:TSP > 200
        MONEY:1 = 325
    ELSEIF MAXBASE:MASTER:TSP > 100
        MONEY:1 = 125
    ELSEIF MAXBASE:MASTER:TSP <= 100
        MONEY:1 = 50
    ENDIF
    CALL 종족초기화(MASTER)
    CALL 변화의술보너스초기화()
    CALL 종족변화("인간")

ELSE
    RETURN
ENDIF

@부활시키기(ARG)
#DIM DYNAMIC 소모코스트
PRINTFORM %ARG를% 되살립니까?

소모코스트 += (ABL:ARG:레벨 + (ABL:ARG:순종 * 10)) * 500
소모코스트 += (ABL:ARG:전투능력) * 25000
소모코스트 += (TALENT:ARG:연인 * 30000) + (TALENT:ARG:연모 *10000)
소모코스트 += 250000

PRINTFORML (요구량 : {소모코스트})

PRINTFORMLC [1] TSP로 지불
PRINTFORMLC [2] 돈으로 지불
PRINTFORMLC [9] 그만둔다
PRINTL
$환생입력루프
INPUT
IF RESULT == 9
    RETURN 1
ELSE
    SELECTCASE RESULT
        CASE 1
            
            IF BASE:MASTER:TSP * 200 < 소모코스트
                PRINTFORMW TSP가 부족합니다 (현재의 TSP로 지불할수 있는 양 : {BASE:MASTER:TSP * 200})
            ELSE
                PRINTFORML {소모코스트/200} TSP 소모합니다, 지불합니까?
                CALL ASK_YN
                IF !RESULT
                    BASE:MASTER:TSP -= 소모코스트/200
                    CALL T_ARG부활(ARG)
                    PRINTFORML %ARG의% 몸이 서서히 복원되기 시작한다...
                    PRINTFORML %ARG의% 몸 조각의 세포로부터, 서서히 분열하기 시작한 그것은 어린 %ARG의% 모습으로 변해갔다...
                    PRINTFORML 이윽고 익숙한 %ARG의% 모습으로 변해가는 그것에 혼을 집어넣자, 천천히 숨을 쉬기 시작한다
                    PRINTFORML 
                    PRINTFORML %ARG는% 부활했습니다
                    CALL 노말라이제이션(ARG, 10)
                    CALL 종속도상승(ARG, 25)
                    습득한혼:ARG--
                ELSE
                    GOTO 환생입력루프
                ENDIF
            ENDIF
        CASE 2
            IF MONEY < 소모코스트
                PRINTFORMW 돈이 부족합니다 (현재 소지금 : {MONEY})
            ELSE
                PRINTFORML {소모코스트}원 소모합니다, 지불합니까?
                CALL ASK_YN
                IF !RESULT
                    MONEY -= 소모코스트
                    CALL T_ARG부활(ARG)
                    PRINTFORML 다른 이의 시체를 사들여, 그 육신에 %ARG의% 혼을 집어넣었다...
                    PRINTFORML 조교전의 힘인지, 점차 익숙한 %ARG의% 모습으로 변해가는 그것은 천천히 숨을 쉬기 시작하며 몸을 일으켰다
                    PRINTFORML 
                    PRINTFORML %ARG는% 부활했습니다
                    CALL 노말라이제이션(ARG, 10)
                    CALL 종속도상승(ARG, 25)
                    습득한혼:ARG--
                ELSE
                    GOTO 환생입력루프
                ENDIF
            ENDIF
        CASEELSE
            GOTO 환생입력루프
    ENDSELECT
ENDIF

@영혼흡수(ARG)
#DIM DYNAMIC 획득경험치

획득경험치 = 50
획득경험치 += (ABL:ARG:레벨*5) + (ABL:ARG:전투능력 * 250) + (TALENT:ARG:연인 * 800) +(TALENT:ARG:연모 * 300) + (TALENT:ARG:사모 * 100) + (TALENT:ARG:애욕 * 200)
획득경험치 += (TALENT:ARG:임신 * 100) + (TALENT:ARG:손님의아이 * 100)
획득경험치 += MIN(CFLAG:ARG:호감도,500) + MIN(CFLAG:ARG:신뢰도,500)
IF GROUPMATCH(ARG,5,6,7,13,14,18,19,111,118)
    ;요정은 경험치 감소
    획득경험치 /= 5
ENDIF
IF TALENT:ARG:스침
    ;
    획득경험치 = 1000
ENDIF
SELECTCASE TALENT:ARG:사망경험
    CASE IS > 100
        TIMES 획득경험치, 0.1
        TIMES 획득경험치, 0.5
        ;결과적으론 5%
    CASE IS > 90
        TIMES 획득경험치, 0.1
    CASE IS > 80
        TIMES 획득경험치, 0.2
    CASE IS > 70
        TIMES 획득경험치, 0.3
    CASE IS > 60
        TIMES 획득경험치, 0.4
    CASE IS > 50
        TIMES 획득경험치, 0.5
    CASE IS > 40
        TIMES 획득경험치, 0.6
    CASE IS > 30
        TIMES 획득경험치, 0.7
    CASE IS > 20
        TIMES 획득경험치, 0.8
    CASE IS > 10
        TIMES 획득경험치, 0.9
    CASE IS > 0
        ;처리없음
ENDSELECT
PRINTFORML %ARG의% 혼을 흡수하여 {획득경험치}만큼의 경험치를 얻을수 있습니다
PRINTFORML 진행합니까?
CALL ASK_YN
IF !RESULT
    습득한혼:ARG--
    흡령의수++
    CALL 경험치처리(MASTER,획득경험치)
ELSE
    RETURN 1
ENDIF

@스킬확인(ARG)
FOR LOCAL,0,T_스킬한계값
    IF HAVE_T_SKILL(ARG,LOCAL) == 1
        CALLFORMF T_SKILL{LOCAL}_STR("스킬명")
        PRINTFORML {LOCAL}번 스킬, %RESULTS%
    ENDIF
NEXT

@파편합치기()
#DIM DYNAMIC 획득경험치
$연성루프
PRINTFORML 가진 모든 파편을 흡수했다...
FOR LOCAL, 0, CHARANUM
    IF 습득한혼의파편:LOCAL >= 1
        획득경험치 = 50
        획득경험치 += (ABL:LOCAL:레벨*5) + (ABL:LOCAL:전투능력 * 250) + (TALENT:LOCAL:연인 * 800) +(TALENT:LOCAL:연모 * 300) + (TALENT:LOCAL:사모 * 100) + (TALENT:LOCAL:애욕 * 200)
        획득경험치 += (TALENT:LOCAL:임신 * 100) + (TALENT:LOCAL:손님의아이 * 100)
        획득경험치 += MIN(CFLAG:LOCAL:호감도,500) + MIN(CFLAG:LOCAL:신뢰도,500)
        IF GROUPMATCH(LOCAL,5,6,7,13,14,18,19,111,118)
            ;요정은 경험치 감소
            획득경험치 /= 5
        ENDIF
        IF TALENT:LOCAL:스침
            ;
            획득경험치 = 1000
        ENDIF
        SELECTCASE TALENT:LOCAL:사망경험
            CASE IS > 100
                TIMES 획득경험치, 0.1
                TIMES 획득경험치, 0.5
                ;결과적으론 5%
            CASE IS > 90
                TIMES 획득경험치, 0.1
            CASE IS > 80
                TIMES 획득경험치, 0.2
            CASE IS > 70
                TIMES 획득경험치, 0.3
            CASE IS > 60
                TIMES 획득경험치, 0.4
            CASE IS > 50
                TIMES 획득경험치, 0.5
            CASE IS > 40
                TIMES 획득경험치, 0.6
            CASE IS > 30
                TIMES 획득경험치, 0.7
            CASE IS > 20
                TIMES 획득경험치, 0.8
            CASE IS > 10
                TIMES 획득경험치, 0.9
            CASE IS > 0
                ;처리없음
        ENDSELECT
        획득경험치 /= 3
        PRINTFORML %CALLNAME:LOCAL%의 혼의 파편을 모두 흡수해 {획득경험치}만큼의 경험치를 얻었다
        습득한혼의파편:RESULT = 0
    ENDIF
NEXT



@모브코의혼등록(ARG)

FOR LOCAL,0,149
    IF 모브코의혼:LOCAL == 0
        모브코의혼:LOCAL = ARG
        RETURN 0
    ENDIF
NEXT
RETURN 1


@모브코의능력(ARG)
#FUNCTION
LOCAL = 50
LOCAL += (ABL:ARG:레벨*5) + (ABL:ARG:전투능력 * 250) + (TALENT:ARG:연인 * 800) +(TALENT:ARG:연모 * 300) + (TALENT:ARG:사모 * 100) + (TALENT:ARG:애욕 * 200)
LOCAL += (TALENT:ARG:임신 * 100) + (TALENT:ARG:손님의아이 * 100)
LOCAL += MIN(CFLAG:ARG:호감도,500) + MIN(CFLAG:ARG:신뢰도,500)
LOCAL /= 10
LOCAL -= 45
LOCAL = MAX(LOCAL,1)
RETURNF LOCAL

@모브코의영혼총합
#FUNCTION
#DIM DYNAMIC 총합
FOR LOCAL,0,149
    총합 += 모브코의혼:LOCAL
NEXT
RETURNF 총합

@모브코의영혼바치기_경험치
#DIM DYNAMIC 획득경험치
PRINTFORML 흐릿한 혼들을 흡수해 경험치를 얻겠습니까? (현재 습득 가능량 : {모브코의영혼총합()})
IF 모브코의영혼총합() <= 0
    PRINTFORMW 획득할 포인트가 없습니다
    RETURN
ENDIF
CALL ASK_YN()
IF RESULT

ELSE
    CALL 모브코영혼소모()
    획득경험치 = RESULT
    SIF 획득경험치 > 0
        CALL 경험치처리(MASTER,획득경험치, 1)
ENDIF

@모브코의영혼바치기_포인트
#DIM DYNAMIC 획득경험치
PRINTFORML 흐릿한 혼들을 흡수해 포인트를 얻겠습니까? (현재 습득 가능량 : {모브코의영혼총합()/ 100})
IF 모브코의영혼총합() <= 100
    PRINTFORMW 획득할 포인트가 없습니다
    RETURN
ENDIF
CALL ASK_YN()
IF RESULT

ELSE
    CALL 모브코영혼소모()
    획득경험치 = RESULT
    획득경험치 /= 100
    조교포인트 += 획득경험치
ENDIF

@모브코영혼소모(ARG = -1)
FOR LOCAL,0, 149
    IF 모브코의혼:(149 - LOCAL) > 0
        흡령의수++
        LOCAL:1 += 모브코의혼:(149 - LOCAL)
        모브코의혼:(149 - LOCAL) = 0
        SIF LOCAL:1 >= ARG && ARG != -1
            BREAK
    ENDIF
NEXT
RETURN LOCAL:1