@연성시스템
#DIM 대상
$연성루프
PRINTFORML 누구의 혼을 이용할까?
DRAWLINE
FOR LOCAL, 0, CHARANUM
    IF 습득한혼:LOCAL >= 1
        PRINTFORML [{LOCAL}] %CALLNAME:LOCAL%의 혼
    ENDIF
NEXT
DRAWLINE
PRINTFORML [9998] 흐릿한 혼
PRINTFORML [9999] 돌아간다
INPUT
IF RESULT == 9999
    RETURN 1
ELSEIF RESULT == 9998
    CALL 모브코의영혼바치기_경험치()
ELSE
    IF RESULT < 0||RESULT > 150
        GOTO 연성루프
    ELSE
        IF 습득한혼:RESULT == 0
            GOTO 연성루프
        ELSE
            ;가능
            대상 = RESULT
            PRINTFORML 무엇을 할까?
            DRAWLINE
            PRINTFORMLC [1] 스펠카드 연성
            SIF TALENT:RESULT:사망 == 1
                PRINTFORMLC [2] 환생
            PRINTFORMLC [3] 흡수
            PRINTL
            ;PRINTFORMLC [4] 해방
            PRINTFORMLC [6] 돌아간다
            $연성입력루프
            INPUT
            SELECTCASE RESULT
                CASE 1
                    CALL 연성가능한스킬(대상)
                CASE 2
                    IF TALENT:대상:사망 == 1
                        CALL 부활시키기(대상)
                    ELSE
                        GOTO 연성입력루프
                    ENDIF
                CASE 3
                    CALL 영혼흡수(대상)
                ;CASE 4

                ;CASE 5

                CASE 6
                    GOTO 연성루프
                CASEELSE
                    GOTO 연성입력루프
            ENDSELECT
            
        ENDIF
    ENDIF
ENDIF
GOTO 연성루프

@연성가능한스킬(ARG)
#DIM DYNAMIC 궁극기배열, 3000
#DIM DYNAMIC 배열포인터
FOR LOCAL,0,T_스킬한계값
    IF HAVE_T_SKILL(ARG,LOCAL) == 1&&HAVE_T_SKILL(MASTER,LOCAL) == 0
        RESULT = 0
        TRYCALLFORM IS_UTIMATE{LOCAL}()
        IF RESULT == 1
            궁극기배열:배열포인터 = LOCAL
            CALLFORMF T_SKILL{LOCAL}_STR("스킬명")
            PRINTFORML [{배열포인터}] %RESULTS%
            배열포인터++
        ENDIF
        RESULT = 0
    ENDIF
NEXT
PRINTFORML [9999] 돌아간다
INPUT
IF RESULT == 9999
    RETURN 1
ELSE
    IF RESULT < 배열포인터&&RESULT >= 0
        TALENT:MASTER:(500+(궁극기배열:RESULT)) = 1
        습득한혼:ARG --
    ENDIF
ENDIF

@부활시키기(ARG)
#DIM DYNAMIC 소모코스트
PRINTFORM %ARG를% 되살립니까?

소모코스트 += (ABL:ARG:레벨 + ABL:ARG:순종) * 100
소모코스트 += (TALENT:ARG:연인 * 5000) + (TALENT:ARG:연모 *3000)
소모코스트 += 150000

PRINTFORML (요구량 : {소모코스트})

PRINTFORMLC [1] TSP로 지불
PRINTFORMLC [2] 돈으로 지불
PRINTFORMLC [9] 그만둔다
PRINTL
$환생입력루프
INPUT
IF RESULT == 9
    RETURN 1
ELSE
    SELECTCASE RESULT
        CASE 1
            
            IF BASE:MASTER:TSP * 200 < 소모코스트
                PRINTFORMW TSP가 부족합니다 (현재의 TSP로 지불할수 있는 양 : {BASE:MASTER:TSP * 200})
            ELSE
                PRINTFORML {소모코스트/200} TSP 소모합니다, 지불합니까?
                CALL ASK_YN
                IF !RESULT
                    BASE:MASTER:TSP -= 소모코스트/200
                    CALL T_ARG부활(ARG)
                    PRINTFORML %ARG의% 몸이 서서히 복원되기 시작한다...
                    PRINTFORML %ARG의% 몸 조각의 세포로부터, 서서히 분열하기 시작한 그것은 어린 %ARG의% 모습으로 변해갔다...
                    PRINTFORML 이윽고 익숙한 %ARG의% 모습으로 변해가는 그것에 혼을 집어넣자, 천천히 숨을 쉬기 시작한다
                    PRINTFORML 
                    PRINTFORML %ARG는% 부활했습니다
                    CALL 노말라이제이션(ARG, 10)
                    CALL 종속도상승(ARG, 25)
                    습득한혼:ARG--
                ELSE
                    GOTO 환생입력루프
                ENDIF
            ENDIF
        CASE 2
            IF MONEY < 소모코스트
                PRINTFORMW 돈이 부족합니다 (현재 소지금 : {MONEY})
            ELSE
                PRINTFORML {소모코스트}원 소모합니다, 지불합니까?
                CALL ASK_YN
                IF !RESULT
                    MONEY -= 소모코스트
                    CALL T_ARG부활(ARG)
                    PRINTFORML 다른 이의 시체를 사들여, 그 육신에 %ARG의% 혼을 집어넣었다...
                    PRINTFORML 조교전의 힘인지, 점차 익숙한 %ARG의% 모습으로 변해가는 그것은 천천히 숨을 쉬기 시작하며 몸을 일으켰다
                    PRINTFORML 
                    PRINTFORML %ARG는% 부활했습니다
                    CALL 노말라이제이션(ARG, 10)
                    CALL 종속도상승(ARG, 25)
                    습득한혼:ARG--
                ELSE
                    GOTO 환생입력루프
                ENDIF
            ENDIF
        CASEELSE
            GOTO 환생입력루프
    ENDSELECT
ENDIF

@영혼흡수(ARG)
#DIM DYNAMIC 획득경험치

획득경험치 = 50
획득경험치 += (ABL:ARG:레벨*5) + (ABL:ARG:전투능력 * 250) + (TALENT:ARG:연인 * 800) +(TALENT:ARG:연모 * 300) + (TALENT:ARG:사모 * 100) + (TALENT:ARG:애욕 * 200)
획득경험치 += (TALENT:ARG:임신 * 100) + (TALENT:ARG:손님의아이 * 100)
획득경험치 += MIN(CFLAG:ARG:호감도,500) + MIN(CFLAG:ARG:신뢰도,500)
IF GROUPMATCH(ARG,5,6,7,13,14,18,19,111,118)
    ;요정은 경험치 감소
    획득경험치 /= 5
ENDIF
IF TALENT:ARG:스침
    ;
    획득경험치 = 1000
ENDIF
PRINTFORML %ARG의% 혼을 흡수하여 {획득경험치}만큼의 경험치를 얻을수 있습니다
PRINTFORML 진행합니까?
CALL ASK_YN
IF !RESULT
    습득한혼:ARG--
    흡령의수++
    CALL 경험치처리(MASTER,획득경험치)
ELSE
    RETURN 1
ENDIF

@스킬확인(ARG)
FOR LOCAL,0,T_스킬한계값
    IF HAVE_T_SKILL(ARG,LOCAL) == 1
        CALLFORMF T_SKILL{LOCAL}_STR("스킬명")
        PRINTFORML {LOCAL}번 스킬, %RESULTS%
    ENDIF
NEXT

@파편합치기()
#DIM 대상
$연성루프
PRINTFORML 누구의 혼을 만들까?
DRAWLINE
FOR LOCAL, 0, CHARANUM
    IF 습득한혼의파편:LOCAL >= 1
        PRINTFORML [{LOCAL}] %CALLNAME:LOCAL%의 혼의 파편 * {습득한혼의파편:LOCAL}
    ENDIF
NEXT
DRAWLINE
PRINTFORML [9999] 돌아간다
INPUT
IF RESULT == 9999
    RETURN 1
ELSE
    IF RESULT < 0||RESULT > 150
        GOTO 연성루프
    ELSE
        IF 습득한혼의파편:RESULT < 3
            PRINTFORML 혼의 파편 3개가 모여야 혼 1개를 만들수 있습니다
            GOTO 연성루프
        ELSE
            ;가능
            습득한혼의파편:RESULT -= 3
            습득한혼:RESULT++
            PRINTFORMW 흐릿한 영혼의 조각을 모아, %CALLNAME:RESULT%의 혼을 합성해냈습니다
        ENDIF
    ENDIF
ENDIF
GOTO 연성루프


@모브코의혼등록(ARG)

FOR LOCAL,0,149
    IF 모브코의혼:LOCAL == 0
        모브코의혼:LOCAL = ARG
        RETURN 0
    ENDIF
NEXT
RETURN 1


@모브코의능력(ARG)
#FUNCTION
LOCAL = 50
LOCAL += (ABL:ARG:레벨*5) + (ABL:ARG:전투능력 * 250) + (TALENT:ARG:연인 * 800) +(TALENT:ARG:연모 * 300) + (TALENT:ARG:사모 * 100) + (TALENT:ARG:애욕 * 200)
LOCAL += (TALENT:ARG:임신 * 100) + (TALENT:ARG:손님의아이 * 100)
LOCAL += MIN(CFLAG:ARG:호감도,500) + MIN(CFLAG:ARG:신뢰도,500)
LOCAL /= 10
LOCAL -= 45
LOCAL = MAX(LOCAL,1)
RETURNF LOCAL

@모브코의영혼총합
#FUNCTION
#DIM DYNAMIC 총합
FOR LOCAL,0,149
    총합 += 모브코의혼:LOCAL
NEXT
RETURNF 총합

@모브코의영혼바치기_경험치
#DIM DYNAMIC 획득경험치
PRINTFORML 흐릿한 혼들을 흡수해 경험치를 얻겠습니까? (현재 습득 가능량 : {모브코의영혼총합()})
IF 모브코의영혼총합() <= 0
    PRINTFORMW 획득할 포인트가 없습니다
    RETURN
ENDIF
CALL ASK_YN()
IF RESULT

ELSE
    FOR LOCAL,0, 150
        IF 모브코의혼:(149 - LOCAL) > 0
            흡령의수++
            획득경험치 += 모브코의혼:(149 - LOCAL)
            모브코의혼:(149 - LOCAL) = 0
        ENDIF
    NEXT
    SIF 획득경험치 > 0
        CALL 경험치처리(MASTER,획득경험치, 1)
ENDIF

@모브코의영혼바치기_포인트
#DIM DYNAMIC 획득경험치
PRINTFORML 흐릿한 혼들을 흡수해 포인트를 얻겠습니까? (현재 습득 가능량 : {모브코의영혼총합()/ 100})
IF 모브코의영혼총합() <= 100
    PRINTFORMW 획득할 포인트가 없습니다
    RETURN
ENDIF
CALL ASK_YN()
IF RESULT

ELSE
    FOR LOCAL,0, 149
        IF 모브코의혼:(149 - LOCAL) > 0
            흡령의수++
            획득경험치 += 모브코의혼:(149 - LOCAL)
            모브코의혼:(149 - LOCAL) = 0
        ENDIF
    NEXT
    획득경험치 /= 100
    조교포인트 += 획득경험치
ENDIF