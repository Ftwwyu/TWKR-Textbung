@제물시스템
#DIM DYNAMIC 획득포인트
$LOOP
PRINTFORML 누구의 혼을 제물로 바칠까?
DRAWLINE
FOR LOCAL, 0, CHARANUM
    IF 습득한혼:LOCAL >= 1
        PRINTFORML [{LOCAL}] %CALLNAME:LOCAL%의 혼
    ENDIF
NEXT
DRAWLINE
PRINTFORML [9998] 흐릿한 혼
PRINTFORML [9999] 돌아간다
INPUT
IF RESULT == 9999
    RETURN 1
ELSEIF RESULT == 9998
    CALL 모브코의영혼바치기_포인트()
ELSE
    IF RESULT < 0||RESULT > CHARANUM
        GOTO LOOP
    ELSE
        IF 습득한혼:RESULT == 0
            GOTO LOOP
        ELSE
            ;가능
            습득한혼:RESULT--
            PRINTFORML %CALLNAME:RESULT%의 혼을 조교전에 바쳤다...
            획득포인트 = 1
            획득포인트 += (ABL:RESULT:레벨/10) + (ABL:RESULT:전투능력 * 5) + (TALENT:RESULT:연인 * 10) +(TALENT:RESULT:연모 * 5) + (TALENT:RESULT:사모) + (TALENT:RESULT:애욕 * 3)
            획득포인트 += (TALENT:RESULT:임신) + (TALENT:RESULT:손님의아이)
            획득포인트 += MIN(CFLAG:RESULT:호감도/500,5) + MIN(CFLAG:RESULT:신뢰도/500,5)
            IF GROUPMATCH(RESULT,5,6,7,13,14,18,19,111,118)
                ;요정은 경험치 감소
                획득포인트 /= 2
            ENDIF
            IF TALENT:RESULT:스침
                ;산시타들은 감소
                획득포인트 = 2
            ENDIF
            흡령의수++
            조교포인트 += 획득포인트
            PRINTFORMW 조교포인트를 {획득포인트} 얻었다
            RETURN
        ENDIF
    ENDIF
ENDIF
GOTO LOOP