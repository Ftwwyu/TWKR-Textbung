@통합버프(대상,상태이상,S_ID,강도,지속시간)
#DIM DYNAMIC 대상
#DIM DYNAMIC 상태이상
#DIM DYNAMIC S_ID
#DIM DYNAMIC 강도
#DIM DYNAMIC 지속시간
FOR LOCAL, 0, TB버프한계
    IF T_버프:TB남은시간:LOCAL == 0
        CALLF TB_RESET(LOCAL)
        T_버프:TB대상:LOCAL = 대상
        T_버프:TB내용:LOCAL = 상태이상
        T_버프:TBID:LOCAL = S_ID
        T_버프:TB값:LOCAL = 강도
        T_버프:TB남은시간:LOCAL = 지속시간
        BREAK
    ENDIF
NEXT
CALL 공방세팅(MASTER,TARGET)

@TB_RESET(ARG = -1)
;ARG번 버프를 해제하거나 모든 버프를 해제
#FUNCTION
IF ARG < -1||ARG > TB버프한계
    RETURNF -1
ENDIF
IF ARG == -1
    FOR LOCAL, 0, TB버프한계
        T_버프:TB대상:LOCAL = 0
        T_버프:TB내용:LOCAL = 0
        T_버프:TBID:LOCAL = 0
        T_버프:TB값:LOCAL = 0
        T_버프:TB남은시간:LOCAL = 0
    NEXT
ELSE
    T_버프:TB대상:ARG = 0
    T_버프:TB내용:ARG = 0
    T_버프:TBID:ARG = 0
    T_버프:TB값:ARG = 0
    T_버프:TB남은시간:ARG = 0
ENDIF
RETURNF 1

@TB_RESET_WHO(ARG)
;ARG의 버프 해제
#FUNCTION
IF ARG < -1||ARG > CHARANUM
    RETURNF -1
ENDIF

FOR LOCAL,0,TB버프한계
    IF T_버프:TB대상:LOCAL == ARG
        T_버프:TB대상:LOCAL = 0
        T_버프:TB내용:LOCAL = 0
        T_버프:TBID:LOCAL = 0
        T_버프:TB값:LOCAL = 0
        T_버프:TB남은시간:LOCAL = 0
    ENDIF
NEXT
RETURNF 1

@TB_RESET_ID(ARG,TYPE,S_ID)
;ARG의 버프 해제
#FUNCTION
#DIM DYNAMIC TYPE
#DIM DYNAMIC S_ID
IF ARG < -1||ARG > CHARANUM
    RETURNF -1
ENDIF

FOR LOCAL,0,TB버프한계
    IF T_버프:TB대상:LOCAL == ARG&&T_버프:TB내용:LOCAL == TYPE&&T_버프:TBID:LOCAL == S_ID
        T_버프:TB대상:LOCAL = 0
        T_버프:TB내용:LOCAL = 0
        T_버프:TBID:LOCAL = 0
        T_버프:TB값:LOCAL = 0
        T_버프:TB남은시간:LOCAL = 0
    ENDIF
NEXT
RETURNF 1

@TB_MINUS
;턴 종료시 모든 버프의 지속시간을 1 내리는 함수
FOR LOCAL, 0, TB버프한계
    IF T_버프:TB남은시간:LOCAL != 0
        T_버프:TB남은시간:LOCAL --
        CALL TB_효과적용(LOCAL)
        IF T_버프:TB남은시간:LOCAL == 0||T_버프:TB값:LOCAL == 0
            CALLF TB_RESET(LOCAL)
        ENDIF
    ENDIF
NEXT

@TB_MINUS_ID(대상,S_ID,NUM,TYPE = -1)
#DIM DYNAMIC 대상
#DIM DYNAMIC NUM
#DIM DYNAMIC S_ID
#DIM DYNAMIC TYPE
FOR LOCAL, 0, TB버프한계
    IF T_버프:TB남은시간:LOCAL != 0
        IF T_버프:TB대상:LOCAL == 대상&&T_버프:TBID:LOCAL == S_ID
            IF TYPE == -1||TYPE == T_버프:TB내용:LOCAL
                T_버프:TB값:LOCAL --
                IF T_버프:TB값:LOCAL < 0
                    T_버프:TB값:LOCAL = 0
                ENDIF
                IF T_버프:TB값:LOCAL == 0
                    CALLF TB_RESET(LOCAL)
                ENDIF
                NUM--
                CALL TB_효과적용(LOCAL)
                IF NUM <= 0
                    RETURN 1
                ENDIF
            ENDIF
        ENDIF
    ENDIF
NEXT

@TB_MINUS_TYPE(대상,S_ID,NUM,TYPE)
#DIM DYNAMIC 대상
#DIM DYNAMIC NUM
#DIM DYNAMIC NUMBUFFER
#DIM DYNAMIC S_ID
#DIM DYNAMIC TYPE
FOR LOCAL, 0, TB버프한계
    IF T_버프:TB남은시간:LOCAL != 0
        IF T_버프:TB대상:LOCAL == 대상&&(T_버프:TBID:LOCAL == S_ID||S_ID == -1)
            IF TYPE == T_버프:TB내용:LOCAL
                NUMBUFFER = NUM - T_버프:TB값:LOCAL
                T_버프:TB값:LOCAL -= NUM
                NUM = 0
                IF T_버프:TB값:LOCAL < 0
                    ;T_버프:TB값:LOCAL < NUM
                    NUM += NUMBUFFER 
                    T_버프:TB값:LOCAL = 0
                ENDIF
                IF T_버프:TB값:LOCAL == 0
                    CALLF TB_RESET(LOCAL)
                ENDIF
                IF NUM <= 0
                    RETURN 0
                ENDIF
            ENDIF
        ENDIF
    ENDIF
NEXT
RETURN NUM

@TB_MINUS_TYPE_TIME(대상,S_ID,NUM,TYPE)
#DIM DYNAMIC 대상
#DIM DYNAMIC NUM
#DIM DYNAMIC NUMBUFFER
#DIM DYNAMIC S_ID
#DIM DYNAMIC TYPE
FOR LOCAL, 0, TB버프한계
    IF T_버프:TB남은시간:LOCAL != 0
        IF T_버프:TB대상:LOCAL == 대상&&(T_버프:TBID:LOCAL == S_ID||S_ID == -1)
            IF TYPE == T_버프:TB내용:LOCAL
                NUMBUFFER = NUM - T_버프:TB남은시간:LOCAL
                T_버프:TB남은시간:LOCAL -= NUM
                NUM = 0
                IF T_버프:TB남은시간:LOCAL < 0
                    ;T_버프:TB값:LOCAL < NUM
                    NUM += NUMBUFFER 
                    T_버프:TB남은시간:LOCAL = 0
                ENDIF
                IF T_버프:TB남은시간:LOCAL == 0
                    CALLF TB_RESET(LOCAL)
                ENDIF
                IF NUM <= 0
                    RETURN 0
                ENDIF
            ENDIF
        ENDIF
    ENDIF
NEXT
RETURN NUM


@TB_SUM_VALUE(대상, 상태이상,S_ID = -1)
;특정 상태이상의 값의 총 합을 구하는 함수
#FUNCTION
#DIM DYNAMIC 대상
#DIM DYNAMIC 상태이상
#DIM DYNAMIC S_ID
#DIM DYNAMIC 출력
FOR LOCAL, 0,TB버프한계
    IF T_버프:TB대상:LOCAL == 대상&&T_버프:TB내용:LOCAL == 상태이상
        IF S_ID == -1||T_버프:TBID:LOCAL == S_ID
            출력 += T_버프:TB값:LOCAL
        ENDIF
    ENDIF
NEXT
RETURNF 출력

@TB_SUM_REMAIN(대상, 상태이상,S_ID = -1)
;특정 상태이상의 남은시간의 총 합을 구하는 함수
#FUNCTION
#DIM DYNAMIC 대상
#DIM DYNAMIC 상태이상
#DIM DYNAMIC S_ID
#DIM DYNAMIC 출력
FOR LOCAL, 0,TB버프한계
    IF T_버프:TB대상:LOCAL == 대상&&T_버프:TB내용:LOCAL == 상태이상
        IF S_ID == -1||T_버프:TBID:LOCAL == S_ID
            출력 += T_버프:TB남은시간:LOCAL
        ENDIF
    ENDIF
NEXT
RETURNF 출력


@TB_TYPERESET(ARG,TYPE,NUM)
;ARG의 특정 타입 버프를 해제
#DIM DYNAMIC TYPE
#DIM DYNAMIC NUM
#DIM DYNAMIC NUM2
#DIM DYNAMIC 삭제개수
IF TYPE == -3
    ;버프만 해제
    IF NUM == -1
        FOR LOCAL, 0, TB버프한계
            IF T_버프:TB대상:LOCAL == ARG
                IF IS_BUFF(LOCAL) == 2
                    CALLF TB_RESET(LOCAL)
                    삭제개수++
                ENDIF
            ENDIF
        NEXT
    ELSE
        FOR LOCAL, 0, TB버프한계
            IF T_버프:TB대상:LOCAL == ARG
                IF IS_BUFF(LOCAL) == 2
                    CALLF TB_RESET(LOCAL)
                    삭제개수++
                    NUM2 ++
                ENDIF
            ENDIF
            IF NUM2 >= NUM
                BREAK
            ENDIF
        NEXT
    ENDIF
ELSEIF TYPE == -2
    IF NUM == -1
        FOR LOCAL, 0, TB버프한계
            IF T_버프:TB대상:LOCAL == ARG
                CALLF TB_RESET(LOCAL)
                삭제개수++
            ENDIF
        NEXT
    ELSE
        FOR LOCAL, 0, TB버프한계
            IF T_버프:TB대상:LOCAL == ARG
                CALLF TB_RESET(LOCAL)
                NUM2 ++
                삭제개수++
            ENDIF
            IF NUM2 >= NUM
                BREAK
            ENDIF
        NEXT
    ENDIF
ELSEIF TYPE == -1
    ;디버프만 해제
    IF NUM == -1
        FOR LOCAL, 0, TB버프한계
            IF T_버프:TB대상:LOCAL == ARG
                IF IS_BUFF(LOCAL) == 1
                    CALLF TB_RESET(LOCAL)
                    삭제개수++
                ENDIF
            ENDIF
        NEXT
    ELSE
        FOR LOCAL, 0, TB버프한계
            IF T_버프:TB대상:LOCAL == ARG
                IF IS_BUFF(LOCAL) == 1
                    CALLF TB_RESET(LOCAL)
                    삭제개수++
                    NUM2 ++
                ENDIF
            ENDIF
            IF NUM2 >= NUM
                BREAK
            ENDIF
        NEXT
    ENDIF
ELSE
    FOR LOCAL, 0, TB버프한계
        IF T_버프:TB대상:LOCAL == ARG&&T_버프:TB내용:LOCAL == TYPE
            CALLF TB_RESET(LOCAL)
            삭제개수++
            NUM2 ++
        ENDIF
        IF NUM2 >= NUM
            BREAK
        ENDIF
    NEXT
ENDIF
RETURN 삭제개수

@TB_MINUS_WHO(ARG)
;턴 종료시 모든 버프의 지속시간을 1 내리는 함수
FOR LOCAL, 0, TB버프한계
    IF T_버프:TB대상:LOCAL == ARG
        IF T_버프:TB남은시간:LOCAL != 0
            T_버프:TB남은시간:LOCAL --
            CALL TB_효과적용(LOCAL)
            IF T_버프:TB남은시간:LOCAL == 0||T_버프:TB값:LOCAL == 0
                CALLF TB_RESET(LOCAL)
            ENDIF
        ENDIF
    ENDIF
NEXT

@TB_ADD_ID(ARG,ARG:1,ARG:2,ARG:3,ARG:4,ARG:5 = 0)
;ARG의 ARG:1번 스킬로 생긴 ARG:2 타입 버프를 ARG:3만큼 값 증가 및 ARG:4만큼 지속시간 증가
FOR LOCAL,0,TB버프한계
    IF T_버프:TB대상:LOCAL == ARG&&(T_버프:TBID:LOCAL == ARG:1||ARG:1 == 0)&&(T_버프:TB내용:LOCAL == ARG:2||ARG:2 == 0)
        T_버프:TB값:LOCAL += ARG:3
        T_버프:TB남은시간:LOCAL += ARG:4
        LOCAL:1 = 1
        BREAK
    ENDIF
NEXT
IF LOCAL:1 == 0&&ARG:5 == 0
    CALL 통합버프(ARG,ARG:2,ARG:1,ARG:3,ARG:4)
ENDIF

@IS_BUFF(ARG)
#FUNCTION
SIF T_버프:TB내용:ARG == TCV_기절
    RETURNF 1
SIF T_버프:TB내용:ARG == TCV_실명
    RETURNF 1
SIF T_버프:TB내용:ARG == TCV_침묵
    RETURNF 1
SIF T_버프:TB내용:ARG == TCV_혼란
    RETURNF 1
SIF T_버프:TB내용:ARG == TCV_취약
    RETURNF 1
SIF T_버프:TB내용:ARG == TCV_독
    RETURNF 1
SIF T_버프:TB내용:ARG == TCV_출혈
    RETURNF 1
SIF T_버프:TB내용:ARG == TCV_저주
    RETURNF 1
SIF T_버프:TB내용:ARG == TCV_약화
    RETURNF 1
SIF T_버프:TB내용:ARG == TCV_회피
    RETURNF 2
SIF T_버프:TB내용:ARG == TCV_면역
    RETURNF 2
SIF T_버프:TB내용:ARG == TCV_증폭
    RETURNF 2
SIF T_버프:TB내용:ARG == TCV_폭주
    RETURNF 2
SIF T_버프:TB내용:ARG == TCV_보호
    RETURNF 2
IF T_버프:TB내용:ARG == TCV_물리내성
    IF T_버프:TB값:ARG > 0
        RETURNF 2
    ELSEIF T_버프:TB값:ARG == 0
        RETURNF 0
    ELSE
        RETURNF 1
    ENDIF
ENDIF
IF T_버프:TB내용:ARG == TCV_화염내성
    IF T_버프:TB값:ARG > 0
        RETURNF 2
    ELSEIF T_버프:TB값:ARG == 0
        RETURNF 0
    ELSE
        RETURNF 1
    ENDIF
ENDIF
IF T_버프:TB내용:ARG == TCV_빙결내성
    IF T_버프:TB값:ARG > 0
        RETURNF 2
    ELSEIF T_버프:TB값:ARG == 0
        RETURNF 0
    ELSE
        RETURNF 1
    ENDIF
ENDIF
IF T_버프:TB내용:ARG == TCV_전격내성
    IF T_버프:TB값:ARG > 0
        RETURNF 2
    ELSEIF T_버프:TB값:ARG == 0
        RETURNF 0
    ELSE
        RETURNF 1
    ENDIF
ENDIF
IF T_버프:TB내용:ARG == TCV_질풍내성
    IF T_버프:TB값:ARG > 0
        RETURNF 2
    ELSEIF T_버프:TB값:ARG == 0
        RETURNF 0
    ELSE
        RETURNF 1
    ENDIF
ENDIF
IF T_버프:TB내용:ARG == TCV_마력내성
    IF T_버프:TB값:ARG > 0
        RETURNF 2
    ELSEIF T_버프:TB값:ARG == 0
        RETURNF 0
    ELSE
        RETURNF 1
    ENDIF
ENDIF
IF T_버프:TB내용:ARG == TCV_수격내성
    IF T_버프:TB값:ARG > 0
        RETURNF 2
    ELSEIF T_버프:TB값:ARG == 0
        RETURNF 0
    ELSE
        RETURNF 1
    ENDIF
ENDIF
IF T_버프:TB내용:ARG == TCV_정신내성
    IF T_버프:TB값:ARG > 0
        RETURNF 2
    ELSEIF T_버프:TB값:ARG == 0
        RETURNF 0
    ELSE
        RETURNF 1
    ENDIF
ENDIF
IF T_버프:TB내용:ARG == TCV_힘
    IF T_버프:TB값:ARG > 0
        RETURNF 2
    ELSEIF T_버프:TB값:ARG == 0
        RETURNF 0
    ELSE
        RETURNF 1
    ENDIF
ENDIF
IF T_버프:TB내용:ARG == TCV_지성
    IF T_버프:TB값:ARG > 0
        RETURNF 2
    ELSEIF T_버프:TB값:ARG == 0
        RETURNF 0
    ELSE
        RETURNF 1
    ENDIF
ENDIF
IF T_버프:TB내용:ARG == TCV_인내
    IF T_버프:TB값:ARG > 0
        RETURNF 2
    ELSEIF T_버프:TB값:ARG == 0
        RETURNF 0
    ELSE
        RETURNF 1
    ENDIF
ENDIF
IF T_버프:TB내용:ARG == TCV_속도
    IF T_버프:TB값:ARG > 0
        RETURNF 2
    ELSEIF T_버프:TB값:ARG == 0
        RETURNF 0
    ELSE
        RETURNF 1
    ENDIF
ENDIF
IF T_버프:TB내용:ARG == TCV_운
    IF T_버프:TB값:ARG > 0
        RETURNF 2
    ELSEIF T_버프:TB값:ARG == 0
        RETURNF 0
    ELSE
        RETURNF 1
    ENDIF
ENDIF

@TB_효과적용(ARG)
LOCAL = ARG
IF T_버프:TB내용:LOCAL == TCV_독
    CALL T_데미지((T_버프:TB대상:LOCAL),50,"독")
ELSEIF T_버프:TB내용:LOCAL == TCV_출혈
    CALL T_데미지((T_버프:TB대상:LOCAL),(TCVAR:(T_버프:TB대상:LOCAL):T_최대체력 * /100),"출혈")
ELSEIF T_버프:TB내용:LOCAL == TCV_저주
    SIF TB_SUM_REMAIN((T_버프:TB대상:LOCAL),TCV_저주) == 0
        CALL T_데미지((T_버프:TB대상:LOCAL),TCVAR:(T_버프:TB대상:LOCAL):T_최대체력,"저주")
ELSEIF T_버프:TB내용:LOCAL == TCV_화상
    CALL T_데미지((T_버프:TB대상:LOCAL),(T_버프:TB값:LOCAL),"화염")
ENDIF