;간이 블랙 잭
;스킬이라든지 사용하지 않는 완전한 운빨 게임입니다, 시간정지로 홀 카드를 들여다 봐도 괜찮을지도요
;승률은 룰이 본가와 다르기 때문에 어떨까요……. 테스트 플레이에서는 게임 속에서 195분 동안 해서―250등을 했다(자코)
;카드의 추가는 항상 1회 밖에 할 수 없기 때문에 더블의 디메리트가 없는 것은 플레이어에게 유리하단 생각이 듭니다
;P_HAND   ×광역 변수화, 플레이어의 패, 7까지 있지만 지금은 3까지 밖에 사용하지 않습니다
;D_HAND   ×광역 변수화, 딜러의 패, 위와 같다
;DECK     ×광역 변수화, 52매의 카드를 랜덤에 늘어놓고 있습니다, 그렇지만 사용할 것은 위로부터 6매만, 아래 쪽 카드를 뽑거나 혹은 타짜짓에 응용할 수 있을지도

;카지노 작자(morph)에 의한 개조
;·HIT의 가능 회수를 늘렸다
;·카드 관련 변수의 광역 변수화＆처리를 독립시켜 범용화
;　(판정 관련은 각 함수에 맡길 수 있으므로 이 쪽이 편리, 포커가 실장되었다)
;·카운팅이 가능하며, 플레이어 스킬이 영향을 준다!
;·사양 상으론 조커도 혼합할 수 있게 되었지만 처리 측에서 대응하고 있지 않다(계산 귀찮아)
@CASINO_BLACKJACK
#DIM P_BET        ;판돈
#DIM ORI_BET      ;최초의 판돈, 더블을 선택하면 P_BET가  2배가 되고, 속행하면 다음의 게임에서도  2배가 된 채로이므로 보존용입니다
#DIM INS          ;인슈어런스의 보험금, 걸고 있으면 재차 인슈어런스 할 수 없다
#DIM LPCOUNT      ;루프 카운트
#DIM TOTAL, 3     ;
                  ;TOTAL:1  판돈 합계, 봉은 마지막에 정리해서 정산했습니다
                  ;TOTAL:2  배당 합계
#DIM P_WIN        ;플레이어의 승리 판정용
#DIM CONST DL = 0 ;정수, 딜러의 카드 플레이어 번호
#DIM CONST PL = 1 ;정수, 플레이어의 카드 플레이어 번호
;참가 플레이어는 두 사람
NUM_CARD_PL = 2

PRINTFORML 
IF MONEY:2 <= 0
	PRINTFORML 메이드 요정
	PRINTFORML 　손님, 당 카지노 점내에서는 전용 통화 “카리스마”만을 사용해 놀이를 하고 있습니다
	PRINTFORMW 　수고스럽겠지만 카리스마의 구입을 부탁 드립니다
	RETURN
ELSE
	PRINTFORML 메이드 요정
	PRINTFORML 　어서 오십시오
	PRINTFORML 　이쪽은 블랙 잭 코너 입니다
	PRINTFORML 　잔재주나 속임수는 일절 무용, 순수하게 카드의 솜씨를 겨루는 게임입니다
	PRINTFORMW 　우선은 게임의 판돈을 제시해 주세요
	PRINTFORML 
ENDIF
CALL CASINO_INPUTLOOP("카드 판돈")
P_BET = RESULT
ORI_BET = RESULT
VARSET TOTAL
IF P_BET
	CALL CASINO_MONEY("카리스마", -P_BET)
	TOTAL:1 += P_BET
	CALL PLAY_CARDS("SHUFFLE")
	PRINTFORML 
	PRINTFORMW 메이드 요정은 신품의 덱을 꺼내 셔플했다
ENDIF

WHILE P_BET
	IF NEW_CARD > 44
		CALL PLAY_CARDS("SHUFFLE")
		PRINTFORML 
		PRINTFORML 메이드 요정
		PRINTFORMW 　남은 카드가 적어졌으므로 셔플합니다
	ENDIF
	TIME += 5
	;패를 리셋 해 2매씩 당깁니다, INS도 리셋
	CALLF CARD_PLAYER("CLEAR_BJ_ALL")
	CALL PLAY_CARDS("RESET_HAND")
	CALL PLAY_CARDS("HIT", DL)
	CALL PLAY_CARDS("HIT", PL)
	CALL PLAY_CARDS("HIT", DL)
	CALL PLAY_CARDS("HIT", PL)
	CALLF CARD_PLAYER("RENEW_BJ_POINT_ALL")
	INS = 0
	P_WIN = 0
	;초기 반면, 서로 패는 2매, 홀 카드 1매
	DO
		CALL CASINO_DISP("BJ_CLOSE")
		SIF NEW_CARD < NUM_CARD
			PRINTL [1] - 히트
		PRINTL [2] - 스탠드
		SIF NEW_CARD < NUM_CARD && NUM_HAND:PL == 2 && MONEY:2 >= P_BET
			PRINTL [3] - 더블
		IF NUM_HAND:PL == 2 && CARD_CHECK("RANK", CARD_HAND:DL:0) == 1
			IF CARD_PLAYER("BJ_BJ", PL)
				PRINTL [4] - 이븐 머니
			ELSEIF !INS && MONEY:2 >= P_BET / 2
				PRINTL [4] - 인슈어런스
			ENDIF
		ENDIF
		PRINTL [0] - 서렌더
		INPUT
		DRAWLINEFORM ━
		PRINTFORML 메이드 요정
		SELECTCASE RESULT
		CASE 1
		;히트 매수를 무제한으로 변경
			SIF NEW_CARD >= NUM_CARD
				GOTO SURRENDER
			PRINTFORMW 　그럼 카드를 부디
			CALL PLAY_CARDS("HIT_BJ", PL)
			SIF CARD_PLAYER("GET_BJ_POINT", PL) >= 21
				BREAK
			CONTINUE
		CASE 2
		;스탠드, 바로 승부합니다
		CASE 3
		;더블, 판돈을 2배로 해서 카드 1매 추가, 판돈을 준비할 수 있을 때만
			SIF MONEY:2 < P_BET || NUM_HAND:PL > 2 || NEW_CARD >= NUM_CARD
				GOTO SURRENDER
			PRINTFORML 　잘 알았습니다
			PRINTFORML 　걸고 돈은{P_BET * 2}카리스마가 됩니다
			PRINTFORMW 　그럼 카드를 부디
			CALL PLAY_CARDS("HIT_BJ", PL)
			TOTAL:1 += P_BET
			CALL CASINO_MONEY("카리스마", -P_BET)
			P_BET = P_BET * 2
		CASE 4
			IF NUM_HAND:PL == 2 && CARD_CHECK("RANK", CARD_HAND:DL:0) == 1
				IF CARD_PLAYER("GET_BJ_BJ", PL)
				;이븐 머니, 무승부는 위험하기 때문에 2배의 배당으로 참고, 신경이 쓰이므로 홀 카드를 보여 준다
				;겉의 카드가 A로 당신의 패가 21점일 때만
					PRINTFORML 　잘 알았습니다
					PRINTFORML 　이쪽이 배당의{P_BET * 2}카리스마가 됩니다
					PRINTFORMW 　부디 받아 주세요
					TOTAL:2 += P_BET * 2
					CALL CASINO_MONEY("카리스마", P_BET * 2)
					PRINTFORMW 　덧붙여서 홀 카드는……
					CALL CARD_DISP("CARD", CARD_HAND:DL:1)
					PRINTW
					IF CARD_PLAYER("GET_BJ_BJ", DL)
						PRINTFORMW 후후후, 현명한 판단이었지요
					ELSE
						PRINTFORMW 어머어머, 유감이었지요
					ENDIF
					GOTO CHECK_NEXTGAME
				ELSEIF !INS && MONEY:2 >= P_BET / 2
				;인슈어런스, 블랙잭이 위험하기 때문에 보험금을 추가 BET 한다
				;겉의 카드가 A로 INS를 하고 있지 않고 보험금 분의 카리스마를 준비할 수 있을 때만
				;그 후 재차 선택지를 선택한다
					PRINTFORML 　잘 알았습니다
					PRINTFORMW 　판돈의 반,{P_BET / 2}카리스마를 보험금으로 해서 추가합니다
					INS = P_BET / 2
					TOTAL:1 += P_BET / 2
					CALL CASINO_MONEY("카리스마", -P_BET / 2)
					CONTINUE
				ELSE
					GOTO SURRENDER
				ENDIF
			ELSE
				GOTO SURRENDER
			ENDIF
		CASEELSE
		;손입력하거나 하면 서렌더, 게임을 그만두고 반액 돌려 받는다
			$SURRENDER
			PRINTFORML 　어머나, 그만두시나요
			PRINTFORMW 　그러면 배당의{P_BET / 2}카리스마를 받아 주세요
			TOTAL:2 += P_BET / 2
			CALL CASINO_MONEY("카리스마", P_BET / 2)
			GOTO CHECK_NEXTGAME
		ENDSELECT
		BREAK
	LOOP 1
	
	IF NUM_HAND:PL > 2
		CALL CASINO_DISP("BJ_CLOSE")
		IF CARD_PLAYER("GET_BJ_BUST", PL)
			PRINTW 
			PRINTFORML 메이드 요정
			PRINTFORML 　21을 넘어 버렸어요
			GOTO YOULOSE
		ENDIF
	ENDIF

	;홀 카드를 공개해 반면을 본다
	PRINTW 
	PRINTFORML 메이드 요정
	PRINTFORMW 　그럼 카드를 넘깁니다
	PRINTFORML 
	WHILE CARD_PLAYER("GET_BJ_POINT", DL) < 17
		IF NEW_CARD >= 52
			PRINTFORML 메이드 요정
			PRINTFORMW 　이제 카드가 없겠네요
			BREAK
		ENDIF
		CALL CASINO_DISP("BJ_OPEN")
		PRINTFORML 메이드 요정
		PRINTFORMW 　17점미만이므로 카드를 추가합니다
		CALL PLAY_CARDS("HIT_BJ", DL)
	WEND

	CALL CASINO_DISP("BJ_OPEN")
	;결과의 판정
	IF CARD_PLAYER("GET_BJ_BJ", DL)
	;디라의 BJ
		PRINTFORML 메이드 요정
		PRINTFORML 　블랙잭이었습니다
		IF INS
		;INS 하고 있으면 세이프
			PRINTFORMW 　인슈어런스를 걸고 있었으므로 배당의{P_BET + INS}카리스마를 받아 주세요
			TOTAL:2 += P_BET + INS
			CALL CASINO_MONEY("카리스마", P_BET + INS)
		ELSEIF CARD_PLAYER("GET_BJ_BJ", PL)
			PRINTFORML 　블랙잭끼리이므로 무승부입니다
			PRINTFORMW 　배당의{P_BET}카리스마를 받아 주세요
			TOTAL:2 += P_BET
			CALL CASINO_MONEY("카리스마", P_BET)
		ELSEIF CARD_PLAYER("GET_BJ_POINT", PL) == 21
		;21에서도 BJ에는 이길 수 없다
			PRINTFORML 　이쪽은 블랙잭인 것에서 당신의 패배입니다
			PRINTFORMW 　판돈은 전부 이쪽이 받아가겠습니다
		ELSE
			GOTO YOULOSE
		ENDIF
	ELSEIF CARD_PLAYER("GET_BJ_BUST", DL)
	;딜러 BUST(여기까지 온 시점에서 당신은 BUST 하지 않았다)
		PRINTFORML 메이드 요정
		PRINTFORML 　21을 넘어 버렸으므로 당신의 승리입니다
		P_WIN = 1
	ELSE
	;보통 결과
		PRINTFORML 메이드 요정
		IF CARD_PLAYER("GET_BJ_BJ", PL)
		;21 POINT이지만 PL측만 BJ가 성립했다
			PRINTFORML 　그 쪽이 블랙잭이므로 당신의 승리입니다
			P_WIN = 1
		ELSEIF CARD_PLAYER("GET_BJ_POINT", PL) > CARD_PLAYER("GET_BJ_POINT", DL)
			PRINTFORML 　당신의 승리입니다
			P_WIN = 1
		ELSEIF CARD_PLAYER("GET_BJ_POINT", PL) == CARD_PLAYER("GET_BJ_POINT", DL)
		;그 외의무승부
			PRINTFORML 　무승부입니다
			PRINTFORMW 　배당의{P_BET}카리스마를 받아 주세요
			TOTAL:2 += P_BET
			CALL CASINO_MONEY("카리스마", P_BET)
		ELSE
		;패배는 여기
			$YOULOSE
			PRINTFORML 　당신의 패배입니다
			PRINTFORMW 　판돈은 전부 이쪽이 받아가겠습니다
		ENDIF
	ENDIF

	IF P_WIN
		IF CARD_PLAYER("GET_BJ_BJ", PL)
			PRINTFORML 　블랙잭이었으므로 배당은{P_BET * 5 / 2}카리스마가 됩니다
			PRINTFORMW 　부디 받아 주세요
			TOTAL:2 += P_BET * 5 / 2
			CALL CASINO_MONEY("카리스마", P_BET * 5 / 2)
		ELSE
			PRINTFORML 　배당은{P_BET * 2}카리스마가 됩니다
			PRINTFORMW 　부디 받아 주세요
			TOTAL:2 += P_BET * 2
			CALL CASINO_MONEY("카리스마", P_BET * 2)
		ENDIF
	ENDIF
	
	;게임의 속행 판정
	$CHECK_NEXTGAME
	IF NEMUKE() >= 700
		P_BET = 0
	ELSEIF MONEY:2 <= 0
		P_BET = 0
	ELSEIF 500000 - FLAG:카리스마유통 - P_BET * 25 / 10 < 10000
	;여기만 BREAK
		BREAK
	ELSE
		PRINTFORML 　그런데, 승부를 계속합니까?
		PRINTFORML 
		CALL CASINO_INPUTLOOP("카드 속행")
		SELECTCASE RESULT
		CASE 0
			P_BET = 0
		CASE 1
			IF MONEY:2 < P_BET
				PRINTFORML 메이드 요정
				PRINTFORML 　수중의 카리스마가 부족한 것 같네요
				PRINTFORML 　판돈을 재설정 해주세요
				PRINTFORML 
				CALL CASINO_INPUTLOOP("카드 판돈")
				P_BET = RESULT
				ORI_BET = RESULT
				SIF RESULT
					PRINTFORML 
			ELSE
				P_BET = ORI_BET
			ENDIF
		CASE 2
			PRINTFORML 메이드 요정
			PRINTFORML 　그럼 판돈을 입력해 주세요
			PRINTFORML 
			CALL CASINO_INPUTLOOP("카드 판돈")
			P_BET = RESULT
			ORI_BET = RESULT
			SIF RESULT
				PRINTFORML 
		ENDSELECT
	ENDIF

	IF P_BET
		TOTAL:1 += P_BET
		CALL CASINO_MONEY("카리스마", -P_BET)
		PRINTFORML 메이드 요정
		PRINTFORML 　네, 판돈{P_BET}카리스마 확실히 받았습니다
		PRINTFORMW 　그럼 이어서 잘 부탁 드리겠습니다
	ENDIF
WEND

PRINTFORML 
IF NEMUKE() >= 700
	PRINTFORML 메이드 요정
	PRINTFORML 　저기, 손님?
	PRINTFORML 　상당히 피곤하신 것 같네요
	PRINTFORML 　오늘은 이만 게임을 끝내는 편이 좋지 않을까요
	PRINTFORMW 　다음 행차를 기다리고 있겠어요
ELSEIF MONEY:2 <= 0
	PRINTFORML 메이드 요정
	PRINTFORML 　손에 가지고 있는 카리스마가 다 떨어진 모양이네요
	PRINTFORMW 　다음 행차를 기다리고 있겠어요
ELSEIF P_BET
;환불료에 점포가 감당할 수 없을 때
	PRINTFORML 메이드 요정
	PRINTFORML 　죄송하지만 이제 폐점 해야할 모양이네요
	PRINTFORMW 　다음 행차를 기다리고 있겠어요
ELSE
	PRINTFORML 메이드 요정
	PRINTFORML 　잘 알았습니다
	PRINTFORMW 　다음 행차를 기다리고 있겠어요
ENDIF
CALLF CARD_PLAYER("CLEAR_BJ_ALL")

IF TOTAL:1
	CALL CASINO_MONEY("것", TOTAL:1)
	CALL CASINO_DISP("RESULT", TOTAL:1, TOTAL:2)
ENDIF
VARSET TOTAL
