
@TSC_12
IF CFLAG:MASTER:현재위치 == GET_MAP_REPLACEMENT(126)
	PRINTFORML %마스터는% 자리를 잡고 명상에 들어갔다...

	TIME += 30
	DOWNBASE:MASTER:기력 += 200

	SELECTCASE RAND:100
		CASE 0 TO 10
			;양생술 획득
			IF 양생술:0 < 5
				PRINTFORMW 양생술에 대한 비결을 얻은 느낌이다
				양생술:1 += (RAND:20 * 2) + 10
				IF 양생술:1 >= 100
					PRINTFORMW 오랜 공부의 결과로 인해 [양생술]에 대해 깨우치게 되었다!
					양생술:0 ++
					양생술:1 = 0
				ENDIF
			ELSEIF 양생술 == 5 && !TALENT:MASTER:S_단전호흡
				PRINTFORML 실전에서 쓸 수 있을만큼 빠르고 정확하게 단전호흡을 할 수 있게 되었다!
				TALENT:MASTER:S_단전호흡 = 1
			ELSE
				PRINTFORML 무언가 깨달음이 있었다
				CALL AddEXP("학습경험",MASTER,3)
			ENDIF
		CASE 11 TO 25
			PRINTFORML 탄막의 형성과 운동 궤적에 대한 심도있는 이미지 트레이닝을 하였다
			CALL AddEXP("전투경험",MASTER,2)
		CASE 26 TO 35
			PRINTFORML ...깊은 단전 호흡이 몸을 치유했다
			DOWNBASE:MASTER:체력 -= 100
		CASE 86 TO 90
			PRINTFORML ...잠깐 잠들어 버렸다
			TIME += 15
			DOWNBASE:MASTER:기력 -= 50
		CASE 91 TO 94
			PRINTFORML ...아무것도 모르겠다
		CASE 95 TO 99
			PRINTFORML ...다리에 쥐가 났다
			DOWNBASE:MASTER:기력 += 10
		CASEELSE
			;예비용
			PRINTFORML 무언가 깨달음이 있었다
			CALL AddEXP("학습경험",MASTER,2)
	ENDSELECT
ELSEIF CFLAG:MASTER:현재위치 == GET_MAP_REPLACEMENT(436)
	PRINTFORML 자리를 잡고 명상을 시작했다
	TIME += 30
	IF !TALENT:MASTER:요괴
		DOWNBASE:MASTER:체력 += 50
		DOWNBASE:MASTER:기력 += 200
		SELECTCASE RAND:100
			CASE 0 TO 9
				;요괴화의 길
				IF 요괴화의길 < 5
					PRINTFORMW ...인간성이 조금씩 소실되는 느낌이다
					요괴화의길++
					IF 요괴화의길 == 5
						PRINTFORML 요괴에 가까워진듯한 기분이 든다
						PRINTFORML 종족 변화의 술을 걸면 요괴가 될 확률이 높아질 것만 같다
					ENDIF
				ELSE
					PRINTFORML 무언가 깨달음이 있었다
					CALL AddEXP("학습경험",MASTER,3)
				ENDIF
			CASE 10
				PRINTFORML ...버려진 아이들의 울음소리가 환청으로 들리고 있다
				DOWNBASE:MASTER:기력 += 50
			CASE 11 TO 25
				PRINTFORML 탄막의 형성과 운동 궤적에 대한 심도있는 이미지 트레이닝을 하였다
				CALL AddEXP("전투경험",MASTER,2)
			CASE 26 TO 35
				PRINTFORML ...은방울 꽃의 독기가 몸을 상하게 한다
				DOWNBASE:MASTER:체력 += 100
			CASE 86 TO 90
				PRINTFORML ...잠깐 잠들어 버렸다
				TIME += 15
				DOWNBASE:MASTER:기력 -= 50
			CASE 91 TO 94
				PRINTFORML ...아무것도 모르겠다
			CASE 95 TO 99
				PRINTFORML ...다리에 쥐가 났다
				DOWNBASE:MASTER:기력 += 10
			CASEELSE
				;예비용
				PRINTFORML 무언가 깨달음이 있었다
				CALL AddEXP("학습경험",MASTER,2)
		ENDSELECT
	ELSE
		PRINTFORML ...지루하다
	ENDIF
ENDIF
RETURN 1

@TSC_NAME12
#DIMS DYNAMIC 커맨드이름
커맨드이름 = 수련한다
RESULTS = %커맨드이름%

@TSC_ABLE12

IF CFLAG:MASTER:현재위치 == GET_MAP_REPLACEMENT(126) ;도교 수련
ELSEIF CFLAG:MASTER:현재위치 == GET_MAP_REPLACEMENT(436) ;요괴 수련
ELSE
    RETURN 0
ENDIF
;기력 0
SIF BASE:MASTER:기력 <= 0
	RETURN 0
SIF CFLAG:우후후 == 2
	RETURN 0
;정지 중에는 불가
SIF FLAG:70
	RETURN 0
RETURN 2



