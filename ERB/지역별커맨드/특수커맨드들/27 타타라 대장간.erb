
@TSC_27
PRINTL
SELECTCASE RAND:3
	CASE 0
		PRINTFORML 「대장간 일? 뭐를 해줘?」
	CASE 1
		PRINTFORML 「으음- 뭘 할건지 들어나 볼까나...」
	CASE 2
		PRINTFORML 「간단한거라면야...」
ENDSELECT
DRAWLINE
PRINTFORML [0] 봉마침 연마
PRINTFORML [1] 냉장상자 제작
DRAWLINE
PRINTFORML [999] 돌아간다
INPUT
IF RESULT == 999
	RETURN
ELSE
	SELECTCASE RESULT
		CASE 0
			IF ITEM:녹슨봉마침
				PRINTFORML 「음...{TATARAFORGING_MONEY(5000)}￦ 정도에 해줄게」
				IF MONEY >= TATARAFORGING_MONEY(5000)
					PRINTFORML 코가사한테 봉마침의 연마를 맡길까?
					CALL ASK_YN
					IF !RESULT
						MONEY -= TATARAFORGING_MONEY(5000)
						ITEM:녹슨봉마침--
						ITEM:예리한봉마침++
						TIME += 30
						CALL TATARAFORGING_EFFECT
						PRINTFORML 「자, 이걸로 다시 예리해졌어, 소중하게 다뤄줘」
						PRINTFORMW 「라고 해도 많이 약해졌으니 한 번 던지면 아예 부서지겠지만...」
					ENDIF
				ELSE
					PRINTFORMW ...그럴 돈은 없다
				ENDIF
			ELSE
				PRINTFORMW 고쳐 쓸만한 봉마침을 가지고 있지 않다
			ENDIF
		CASE 1
			IF ITEM:냉장고
				PRINTFORMW 냉장고는 이미 가지고 있다
			ELSE
				PRINTFORML 「음...{TATARAFORGING_MONEY(75000)}￦ 정도에 해줄게」
				IF MONEY >= TATARAFORGING_MONEY(75000)
					PRINTFORML 코가사한테 냉장상자의 제작을 맡길까?
					CALL ASK_YN
					IF !RESULT
						MONEY -= TATARAFORGING_MONEY(75000)
						ITEM:냉장고 = 1
						TIME += 30
						CALL TATARAFORGING_EFFECT
						PRINTFORML 「자, 냉장상자야, 위에 얼음 넣는데 있고, 아래에 음식 넣는데 있고」
						PRINTFORML 「...얼음 안 넣고 고장났다고 하면 안돼?」
						PRINTFORMW 방에 냉장상자를 설치했다!
					ENDIF
				ELSE
					PRINTFORMW ...그럴 돈은 없다
				ENDIF
			ENDIF
		CASEELSE
			RESTART
	ENDSELECT
ENDIF
RESTART

@TATARAFORGING_MONEY(ARG)
#FUNCTION
LOCAL = ARG
IF MARK:[[코가사]]:반발각인
	TIMES LOCAL, 1.5
ENDIF
IF TALENT:[[코가사]]:연모
	TIMES LOCAL, 0.75
ENDIF
RETURNF LOCAL

@TATARAFORGING_EFFECT
PRINTFORM .
TWAIT 550,1
CLEARLINE 1
PRINTFORM ..
TWAIT 550,1
CLEARLINE 1
PRINTFORM ...
TWAIT 550,1
PRINTFORML

@TSC_NAME27
#DIMS DYNAMIC 커맨드이름
커맨드이름 = 타타라 대장간
RESULTS = %커맨드이름%

@TSC_ABLE27

IF TARGET != [[코가사]]
    RETURN 0
ENDIF
;기력 0
SIF FLAG:70 == 1
	RETURN 0
;실행 판정
SIF !TFLAG:100
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(443)
	RETURN RESULT
SIF TARGET <= 0
	RETURN 0
SIF !SHIRAHU(TARGET)
	RETURN 0
SIF CFLAG:우후후
	RETURN 0
SIF MARK:TARGET:반발각인 > 1
	RETURN 0
SIF CFLAG:TARGET:신뢰도 <= 300
	RETURN 0
SIF CFLAG:빡침
	RETURN 0

RETURN 2
