
@TSC_33
#DIM CONST 우편요금 = 4000
PRINTL
PRINTFORML 우체국에 들어섰다...
DRAWLINE
PRINTFORML 우편 및 소포 당일 특급 요금 : {우편요금}￦/1통
DRAWLINE
PRINTFORMC [001] 우편 및 소포 발송
;PRINTFORMC [002] 택배 발송
PRINTL
DRAWLINE
PRINTFORML [999] 나간다
INPUT
SELECTCASE RESULT
	CASE 999
		RETURN
	CASE 1
		IF MONEY <우편요금
			PRINTFORMW 돈이 부족하다...
			RESTART
		ENDIF
		PRINTFORML 받으시는 분의 우편번호를 적어주세요
		INPUT
		IF !INRANGE(RESULT,1,CHARANUM)
			PRINTFORMW 옳지 않은 우편번호 입니다
		ELSEIF TALENT:RESULT:사망 || CFLAG:RESULT:출금
			PRINTFORMW 말소된 우편번호 입니다
		ELSE
			LOCAL = RESULT
			PRINTFORML 무엇을 보내시겠습니까?
			DRAWLINE
			PRINTFORML [001] 거점으로 불러오기
			IF P_특정의뢰있음("보고 필요",LOCAL)
				PRINTFORML [002] 의뢰 완료 보고
			ENDIF
			IF P_특정의뢰있음("전령(인물)",LOCAL)
				PRINTFORML [003] 편지/소포 전달
			ENDIF
			IF P_특정의뢰있음("조달",LOCAL)
				PRINTFORML [004] 조달 의뢰품 배송
			ENDIF
			DRAWLINE
			PRINTFORML [999] 돌아가기
			$우편루프
			INPUT
			IF RESULT == 999
				RESTART
			ELSEIF RESULT == 1
				IF CFLAG:LOCAL:신사거주
					PRINTFORML 이미 같은 곳에 살고 있어 부를 필요가 없다.
					GOTO 우편루프
				ELSEIF CFLAG:LOCAL:내방불능
					PRINTFORML 안 올거 같다
					GOTO 우편루프
				ELSE
					PRINTFORMW %CALLNAME:LOCAL%에게 방문을 요청하는 편지를 보냈다...
					CFLAG:LOCAL:다음날내방 = 1
				ENDIF
			ELSEIF P_특정의뢰있음("보고 필요",LOCAL) && RESULT == 2
				CALL IRAI_REPORT(LOCAL)

			ELSEIF P_특정의뢰있음("전령(인물)",LOCAL) && RESULT == 3
				TARGET = LOCAL
				CALL P_의뢰실행(LOCAL)
				TARGET = 0

			ELSEIF P_특정의뢰있음("조달",LOCAL) && RESULT == 4
				IF ITEM:IRAI_SLOT_TO_ITEM(IRAI_CLIENT_TO_SLOT(LOCAL)) < IRAI_SUB:(IRAI_CLIENT_TO_SLOT(LOCAL))
					PRINTFORML 해당하는 물건이 없거나 부족하다
					GOTO 우편루프
				ELSE
					TARGET = LOCAL
					CALL P_의뢰실행(LOCAL)
					CALL IRAI_REPORT(LOCAL)
					TARGET = 0
				ENDIF
			ELSE
				GOTO 우편루프
			ENDIF
			MONEY -= 우편요금
		ENDIF
	CASE 2

ENDSELECT
RESTART

@P_의뢰실행(ARG)
#DIM CHK_SLOT
#DIM IRAI_NUM
#DIM IRAI_RESULT
IRAI_NUM = COUNT_IRAI_SLOT()
FOR CHK_SLOT, 1, IRAI_NUM + 1
	IF ARG == IRAI_SLOT_TO_PERSON(CHK_SLOT)
		BREAK
	ENDIF
NEXT

LOCAL = ARG
CALL IRAI_CARRYOUT(LOCAL, IRAI_SLOT:CHK_SLOT, "의뢰실행시")
SELECTCASE RESULT
	CASE IRAI_CONST_RESULT("캔슬")
		;여기는―1이 돌아간다
		;이외는 실행 처리를 했을 것이므로 모두 1이상을 돌려준다
		RETURN IRAI_RESULT
	CASE IRAI_CONST_RESULT("실행해 성공")
		CALL SET_IRAI_PROGRESS(IRAI_SLOT_TO_CLIENT(CHK_SLOT), "의뢰 달성")
	CASE IRAI_CONST_RESULT("실행해 실패")
		CALL SET_IRAI_PROGRESS(IRAI_SLOT_TO_CLIENT(CHK_SLOT), "의뢰 실패")
	CASE IRAI_CONST_RESULT("실행해 미해결")
ENDSELECT

@IRAI_CLIENT_TO_SLOT(ARG)
#FUNCTION
#DIM CHK_SLOT
#DIM IRAI_NUM
IRAI_NUM = COUNT_IRAI_SLOT()
FOR CHK_SLOT, 1, IRAI_NUM + 1
	IF IRAI_SLOT:CHK_SLOT / 1000 == ARG
		RETURNF CHK_SLOT
	ENDIF
NEXT

;작업,조달,전령(인물),전령(장소),수색(인물),수색(장소),토벌,그 외
@P_특정의뢰있음(ARGS,ARG = 0,ARGS:1 = "")
#FUNCTION
#DIM CHK_SLOT
#DIM IRAI_NUM
IRAI_NUM = COUNT_IRAI_SLOT()
FOR CHK_SLOT, 1, IRAI_NUM + 1
	IF ARGS == IRAI_SLOT_TO_TASKNAME(CHK_SLOT)
		SELECTCASE ARGS
			CASE "보고 필요"
				IF STRLENS(STR_DATA_IRAI(IRAI_SLOT:CHK_SLOT, "의뢰 보고 불필요", IRAI_SLOT_TO_CLIENT(CHK_SLOT)))
					CONTINUE
				ENDIF
				IF ARG != IRAI_SLOT_TO_CLIENT(CHK_SLOT)
					CONTINUE
				ENDIF
				IF IRAI_SLOT_TO_PROGRESS(CHK_SLOT) != "의뢰 달성"
					CONTINUE
				ENDIF
			CASE "전령(인물)"
				IF ARG != 0
					IF ARG != IRAI_SLOT_TO_PERSON(CHK_SLOT)
						CONTINUE
					ENDIF
				ENDIF
			CASE "조달"
				IF !ITEM:IRAI_SLOT_TO_ITEM(CHK_SLOT)
					CONTINUE
				ENDIF
				IF ARG != IRAI_SLOT_TO_CLIENT(CHK_SLOT)
					CONTINUE
				ENDIF
		ENDSELECT
		RETURNF 1
	ENDIF
NEXT
RETURNF 0

@TSC_NAME33
#DIMS DYNAMIC 커맨드이름
커맨드이름 = 우체국
RESULTS = %커맨드이름%

@TSC_ABLE33

IF !GROUPMATCH(CFLAG:MASTER:현재위치,GET_MAP_REPLACEMENT(205))
    RETURN 0
ENDIF
;기력 0
SIF BASE:MASTER:기력 <= 0
	RETURN 0
SIF CFLAG:우후후 == 2
	RETURN 0
;정지 중에는 불가
SIF FLAG:70
	RETURN 0
RETURN 2


