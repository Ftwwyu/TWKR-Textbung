
@TSC_9
#DIM DYNAMIC 주문내용
IF FLAG:전화
	PRINTFORML 어디에 전화를 걸까?
	PRINTFORML [0] 음식점
	SIF MAIN_MAP != 4
		PRINTFORML [1] 영원정
	SIF !TALENT:[[레밀리아]]:사망 && !TALENT:[[사쿠야]]:사망 && MAIN_MAP != 3
		PRINTFORML [2] 홍마관 서비스
	IF 노예명수() > 0
		PRINTFORML [3] 노예한테 전화
	ENDIF
	PRINTFORML [4] 누군가한테 전화
	PRINTFORML [99] 그만둔다
	INPUT
	IF RESULT == 99
		RETURN -1
	ELSE
		IF RESULT == 0
			CALL 지역별메뉴판("마을_일품요리집")
			PRINTFORML [9999] 돌아간다
			$LOOP
			INPUT
			주문내용 = RESULT
			IF 주문내용 == 9999
				RETURN -1
			ENDIF
			IF 주문메뉴표:주문내용 == 0
				GOTO LOOP
			ENDIF
			CALL 주문정보생성(주문내용)
			IF 주문정보:1 > MONEY
				PRINTW 돈이 부족하다...
				RESTART
			ENDIF
			MONEY -= 주문정보:1
			CALL 주문요리생성
			PRINTFORMW %조사처리(주문명,"가")% 배달되었다...
			TIME += 30
		ELSEIF RESULT == 1
			;일하러 가버리는 문제를 해결해야 한다
			IF 전화가능("영원정")
				PRINTFORML 집으로 영원정 사람을 불렀다...
				IF CFLAG:[[우동게]]:수면
					CFLAG:[[우동게]]:현재위치 = CFLAG:MASTER:현재위치
					TCVAR:[[우동게]]:귀가지연 = 3
				ELSE
					CFLAG:[[에이린]]:현재위치 = CFLAG:MASTER:현재위치
					TCVAR:[[에이린]]:귀가지연 = 3
				ENDIF
				TIME += 5
			ELSE
				PRINTFORMW ...안 받는다
			ENDIF
		ELSEIF RESULT == 2
			IF 전화가능("홍마관서비스")
				CALL COM630
			ELSE
				PRINTFORMW ...안 받는다
			ENDIF
		ELSEIF RESULT == 3 && 노예명수() > 0
			PRINTFORML 누구한테?
			FOR LOCAL,0,CHARANUM
				IF TALENT:LOCAL:당신의노예 == 1&&!TALENT:LOCAL:사망 && !CFLAG:LOCAL:출금 && !CFLAG:LOCAL:감금됨 && CFLAG:LOCAL:현재위치 != CFLAG:MASTER:현재위치
					PRINTFORML [{LOCAL}] %CALLNAME:LOCAL%
				ENDIF
			NEXT
			PRINTFORML [999] 그만둔다
			$노예전화루프
			INPUT
			LOCAL:1 = RESULT
			IF LOCAL:1 == 999
				RETURN -1
			ELSEIF TALENT:(LOCAL:1):당신의노예 == 1&&!TALENT:(LOCAL:1):사망 && !CFLAG:(LOCAL:1):출금 && !CFLAG:(LOCAL:1):감금됨 && CFLAG:(LOCAL:1):현재위치 != CFLAG:MASTER:현재위치
				PRINTFORM 잠시 동안 연결음이 들리더니 
				IF CFLAG:(LOCAL:1):수면
					PRINTFORML 살짝 졸린듯한 %CALLNAME:(LOCAL:1)%의 목소리가 들려왔다
				ELSE
					PRINTFORML 수화기 너머로 용건을 묻는 %CALLNAME:(LOCAL:1)%의 목소리가 들려왔다
				ENDIF
				PRINTFORML [0] 여기로 부른다
				PRINTFORML [9] 끊는다
				$노예전화루프1
				INPUT
				IF RESULT == 9
					PRINTFORMW 아무것도 아니라 하고 전화를 끊었다
					RETURN
				ELSE
					IF RESULT == 0
						PRINTFORML 이쪽으로 오라는 말에 수긍하고는 전화가 끊겼다
						TIME += 30
						PRINTFORML 잠시뒤, %조사처리(CALLNAME:(LOCAL:1),"이")% 찾아왔다
						IF TALENT:(LOCAL:1):연인 ||TALENT:(LOCAL:1):연모 || TALENT:(LOCAL:1):사모
							PRINTFORML 어쩐지 곱게 차려입고 왔다
						ENDIF
						PRINTFORMW %조사처리(CALLNAME:(LOCAL:1),"은")% 무슨일로 불렀는지 묻고 있다...
						CFLAG:(LOCAL:1):현재위치 = CFLAG:MASTER:현재위치
						TCVAR:(LOCAL:1):귀가지연 = 5
						CFLAG:(LOCAL:1):졸음쫒기강도 = TIME - CFLAG:(LOCAL:1):취침시간 + 75
						CFLAG:(LOCAL:1):수면 = 0
					ELSE
						GOTO 노예전화루프1
					ENDIF
				ENDIF
			ELSE
				GOTO 노예전화루프
			ENDIF
		ELSEIF RESULT == 4
			PRINTFORML 누구한테?
			FOR LOCAL,1,CHARANUM
				IF !TALENT:LOCAL:사망 && !CFLAG:LOCAL:출금 && !CFLAG:LOCAL:감금됨 && CFLAG:LOCAL:현재위치 != CFLAG:MASTER:현재위치
					CONTINUE
				ENDIF
				PRINTFORMLC [{LOCAL,3}] %CALLNAME:LOCAL,12%
				IF LOCAL % 3 == 0
					PRINTL
				ENDIF
			NEXT
			PRINTL
			PRINTFORML [999] 그만둔다
			$통화루프
			INPUT
			IF RESULT == 999
			ELSEIF RESULT <= 0 || RESULT >= CHARANUM
				GOTO 통화루프
			ELSE
				IF !TALENT:RESULT:사망 && !CFLAG:RESULT:출금 && !CFLAG:RESULT:감금됨 && CFLAG:RESULT:현재위치 != CFLAG:MASTER:현재위치
					GOTO 통화루프
				ENDIF
				IF 소득수준:RESULT <= M_저소득층
					PRINTFORMW ...전화가 없을 것이다
				ELSEIF (CFLAG:RESULT:호감도 <= 500 && CFLAG:RESULT:신뢰도 <= 200) || !CFLAG:RESULT:안면
					PRINTFORMW 통화하기엔 그다지 친하지 않다...
				ELSE
					LOCAL = RESULT
					PRINTFORML 잠시 기다리니 %CALLNAME:LOCAL%의 목소리가 들렸다
					PRINTFORML [0] 이쪽으로 부른다
					PRINTFORML [9] 끊는다
					INPUT
					IF RESULT == 9

					ELSEIF RESULT == 0
						IF RAND:3 == 0 && !TALENT:LOCAL:연모
							PRINTFORML %조사처리(CALLNAME:LOCAL,"은")% %TEXTR("바쁘다며/지금은 곤란하다며/귀찮다며")% 전화를 끊었다
						ELSE
							PRINTFORML 이쪽으로 오라는 말에 수긍하고는 전화가 끊겼다
							TIME += 30
							PRINTFORML 잠시뒤, %조사처리(CALLNAME:(LOCAL),"이")% 찾아왔다
							IF TALENT:(LOCAL):연인 ||TALENT:(LOCAL):연모 || TALENT:(LOCAL):사모
								PRINTFORML 어쩐지 곱게 차려입고 왔다
							ENDIF
							PRINTFORMW %조사처리(CALLNAME:(LOCAL),"은")% 무슨일로 불렀는지 묻고 있다...
							CFLAG:(LOCAL):현재위치 = CFLAG:MASTER:현재위치
							TCVAR:(LOCAL):귀가지연 = 5
							CFLAG:(LOCAL):졸음쫒기강도 = TIME - CFLAG:(LOCAL):취침시간 + 75
							CFLAG:(LOCAL):수면 = 0
						ENDIF
					ENDIF
				ENDIF
			ENDIF
		ENDIF
	ENDIF
	RETURN 1
ELSE
	PRINTFORML 전화를 쓰려면 요금을 내야 한다...
	PRINTFORML 내겠습니까? (5000원)
	CALL ASK_YN
	IF RESULT
		RETURN -1
	ELSE
		IF MONEY > 5000
			MONEY -= 5000
			FLAG:전화 = 1
			PRINTFORMW 전화를 개통했습니다
		ELSE
			PRINTFORMW 돈이 부족하다...
			RETURN -1
		ENDIF
	ENDIF
ENDIF
		
@TSC_NAME9
#DIMS DYNAMIC 커맨드이름
커맨드이름 = 전화한다
RESULTS = %커맨드이름%

@TSC_ABLE9

IF !ITEM:전화 || CFLAG:MASTER:현재위치 != CFLAG:MASTER:초기위치
    RETURN 0
ENDIF
;기력 0
SIF BASE:MASTER:기력 <= 0
	RETURN 0
SIF CFLAG:우후후 == 2
	RETURN 0
;정지 중에는 불가
SIF FLAG:70
	RETURN 0
RETURN 2



@전화가능(ARGS)
#FUNCTION
IF ARGS == "홍마관서비스"
	SIF TALENT:[[레밀리아]]:사망 ||TALENT:[[레밀리아]]:창녀||TALENT:[[레밀리아]]:렌탈창녀
		RETURNF -1
	SIF TALENT:[[사쿠야]]:사망 ||TALENT:[[사쿠야]]:창녀||TALENT:[[사쿠야]]:렌탈창녀
		RETURNF -1
	SIF (CFLAG:[[사쿠야]]:수면) || CFLAG:[[사쿠야]]:취침시간 <= TIME + 30
		RETURNF -1
	SIF (CFLAG:[[사쿠야]]:현재위치 != SUKIMA())
		RETURNF -1
	SIF MAIN_MAP == 3
		RETURNF -1
	RETURNF 1
ELSEIF ARGS == "영원정"
	SIF TALENT:[[에이린]]:사망||TALENT:[[에이린]]:창녀||TALENT:[[에이린]]:렌탈창녀
		RETURNF -1
	SIF TALENT:[[우동게]]:사망||TALENT:[[우동게]]:창녀||TALENT:[[우동게]]:렌탈창녀
		RETURNF -1
	SIF (CFLAG:[[에이린]]:수면&&CFLAG:[[우동게]]:수면) || (CFLAG:[[우동게]]:취침시간 <= TIME + 30 && CFLAG:[[에이린]]:취침시간 <= TIME + 30)
		RETURNF -1
	SIF (CFLAG:[[에이린]]:현재위치 != SUKIMA()&&CFLAG:[[우동게]]:현재위치 != SUKIMA())
		RETURNF -1
	SIF MAIN_MAP == 4
		RETURNF -1
	RETURNF 1
ENDIF