
@TSC_14
#DIM DYNAMIC 판매력
#DIM DYNAMIC 판매가
IF TARGET && 도시락판매도우미(TARGET)
	PRINTFORM %타겟과% 함께 
ENDIF
PRINTFORML %조사처리(DISH_NAME,"을")% 들고 돌아다니며 장사를 시작했다
PRINTFORM .
TWAIT 1500,1
PRINTFORM .
TWAIT 1500,1
PRINTFORM .
TWAIT 1500,1
PRINTFORML

판매력 = 1 + MIN(ABL:MASTER:화술기능/2, 5)
IF TARGET && 도시락판매도우미(TARGET)
	판매력 += 1 + MIN(ABL:화술기능/2, 5)
ENDIF

IF 판매력 < RAND:4
	PRINTFORMW 팔리지 않았다...
	DOWNBASE:MASTER:기력 += 100
	IF TARGET && 도시락판매도우미(TARGET)
		DOWNBASE:기력 += 100
	ENDIF
ELSE
	판매가 = DISH_RANK * (DISH_BASEPOINT / 2) + 500
	판매가 += (DISH_BASEPOINT / 10) * 판매력
	IF DISH_DRG || DISH_DRG_2
		판매가 /= 50
	ENDIF
	IF TASTE_REACTION("매운") || TASTE_REACTION("단") || TASTE_REACTION("시큼한") || TASTE_REACTION("고추냉이")
		판매가 /= 50
	ENDIF
	PRINTFORML %조사처리(DISH_NAME,"은")% {판매가}원에 팔렸다...
	MONEY += 판매가
	DOWNBASE:MASTER:기력 += 75
	IF TARGET && 도시락판매도우미(TARGET)
		DOWNBASE:기력 += 75
	ENDIF
	CALL RESET_DISH
ENDIF
TIME += 30

@TSC_NAME14
#DIMS DYNAMIC 커맨드이름
커맨드이름 = 도시락 장사
RESULTS = %커맨드이름%

@TSC_ABLE14

;IF 1
;    RETURN 0
;ENDIF
IF DISH_NAME == ""
	RETURN 0
ENDIF
IF !AT_HOME(MASTER) && !GROUPMATCH(CFLAG:MASTER:현재위치,10,120,210,220,230,610,840,930,1050,1070)
	RETURN 0
ENDIF
IF AT_HOME(MASTER) && !GROUPMATCH(CFLAG:MASTER:현재위치,1,2,101,102,202,203,204,601,803,804,931,933,1002,1003)
	RETURN 0
ENDIF
IF !INRANGE(TIME,600,1320)
	RETURN 0
ENDIF
;기력 0
SIF BASE:MASTER:기력 <= 0
	RETURN 0
SIF CFLAG:우후후 == 2
	RETURN 0
;정지 중에는 불가
SIF FLAG:70
	RETURN 0
RETURN 2

@도시락판매도우미(ARG)
#FUNCTION
IF TCVAR:ARG:친해져 < 1
	RETURNF 0
ENDIF
IF !TALENT:ARG:당신의노예 && !TALENT:ARG:연모
	RETURNF 0
ENDIF
IF 업무중체크(ARG)
	RETURNF 0
ENDIF

RETURNF 0