@주도도상승(ARG,ARG:1)
;ARG = 대상
;ARG:1 = 상승량

IF ARG == MASTER
	CFLAG:ARG:주도도 = 0
	CFLAG:ARG:종속도 = 0
	RETURN -1
ENDIF
IF ARG >= CHARANUM || ARG <= 0
	RETURN -1
ENDIF
IF CFLAG:ARG:종속도 > 0
	IF CFLAG:ARG:종속도 < ARG:1
		CFLAG:ARG:주도도 += (ARG:1 - CFLAG:ARG:종속도)
		CFLAG:ARG:종속도 = 0
	ELSE
		CFLAG:ARG:종속도 -= ARG:1
	ENDIF
ELSE
	CFLAG:ARG:주도도 += ARG:1
ENDIF
SIF CFLAG:ARG:종속도 > 100
	CFLAG:ARG:종속도 = 100
SIF CFLAG:ARG:주도도 > 100
	CFLAG:ARG:주도도 = 100

@종속도상승(ARG,ARG:1)
;ARG = 대상
;ARG:1 = 상승량
IF ARG == MASTER
	CFLAG:ARG:주도도 = 0
	CFLAG:ARG:종속도 = 0
	RETURN -1
ENDIF
IF CFLAG:ARG:주도도 > 0
	IF CFLAG:ARG:주도도 < ARG:1
		CFLAG:ARG:종속도 += (ARG:1 - CFLAG:ARG:주도도)
		CFLAG:ARG:주도도 = 0
	ELSE
		CFLAG:ARG:주도도 -= ARG:1
	ENDIF
ELSE
	CFLAG:ARG:종속도 += ARG:1
ENDIF
SIF CFLAG:ARG:종속도 > 100
	CFLAG:ARG:종속도 = 100
SIF CFLAG:ARG:주도도 > 100
	CFLAG:ARG:주도도 = 100

@노말라이제이션(ARG,ARG:1)
;주도,종속을 0까지만 내린다
IF ARG == MASTER
	CFLAG:ARG:주도도 = 0
	CFLAG:ARG:종속도 = 0
	RETURN -1
ENDIF
IF CFLAG:ARG:주도도 > 0
	IF CFLAG:ARG:주도도 < ARG:1
		CFLAG:ARG:주도도 = 0
	ELSE
		CFLAG:ARG:주도도 -= ARG:1
	ENDIF
ELSEIF CFLAG:ARG:종속도 > 0
	IF CFLAG:ARG:종속도 < ARG:1
		CFLAG:ARG:종속도 = 0
	ELSE
		CFLAG:ARG:종속도 -= ARG:1
	ENDIF
ENDIF
SIF CFLAG:ARG:종속도 > 100
	CFLAG:ARG:종속도 = 100
SIF CFLAG:ARG:주도도 > 100
	CFLAG:ARG:주도도 = 100


@노예화(ARG)
#DIM 검사용변수
#DIM 오늘거절
검사용변수 = 0
SIF ARG == MASTER
	RETURN 0
SIF ARG == RANDOM_CHARANUM
	RETURN 0
SIF TALENT:ARG:사망
	RETURN 0
IF CFLAG:ARG:종속도 == 100&&!TALENT:ARG:당신의노예&&RAND:3 == 0 && 오늘거절 != DAY
	PRINTFORML 한밤중, %마스터의% 방에 찾아온 %ARG가% %마스터의% 앞에 엎드렸다
	PRINTFORMW 아무래도 마음이 꺾여버린듯 하다...
	PRINTFORML 
	PRINTFORML %ARG를% 노예로 삼겠습니까?
	PRINTFORML [0] 네
	PRINTFORML [1] 그건 아냐
	PRINTFORML [2] 거절하고 오늘은 문을 잠근다
	INPUT
	IF RESULT == 0
		PRINTFORML %ARG는% %마스터의% [노예]가 되었습니다
		TALENT:ARG:당신의노예 = 1
		TALENT:ARG:주인 = 0
		FOR LOCAL, 1, CHARANUM
			;주인이 몇 명 있는가 검사
			SIF TALENT:LOCAL:주인 == 1
				검사용변수++
		NEXT
		;한명도 없으면 노예상태는 탈출
		SIF 검사용변수 == 0
			TALENT:MASTER:노예 = 0
		RETURN 1
	ELSEIF RESULT == 1
		PRINTFORMW 냉담히 문을 닫았다
		CALL 주도도상승(ARG,20) ; 연속발동 억제용
		RETURN 0
	ELSEIF RESULT == 2
		PRINTFORMW 냉담히 문을 걸어잠갔다
		CALL 주도도상승(ARG,20) ; 연속발동 억제용
		오늘거절 = DAY
		RETURN 0
	ENDIF
ELSEIF CFLAG:ARG:종속도 == 0&&TALENT:ARG:당신의노예
	PRINTFORML 최근, %ARG는% %마스터를% 업신여기게 된 것 같다...
	PRINTFORMW 이제는 주인 행세도 못할 정도로...
	PRINTFORML
	PRINTFORML %ARG는% %마스터의% 노예 상태에서 벗어나버렸습니다...
	PRINTFORMW %ARG는% [노예]를 잃었습니다
	TALENT:ARG:당신의노예 = 0
	SIF CFLAG:ARG:감금됨 == 1
		CALL 타겟감금해제()
	RETURN 1
ELSEIF CFLAG:ARG:주도도 == 100&&!TALENT:ARG:당신의노예&&!TALENT:ARG:주인
	PRINTFORML %마스터는% 최근 %CALLNAME:ARG%에게 당한 일을 떠올렸다...
	PRINTFORMW 갖은 수모를 당해왔었고, 날이 갈수록 %ARG의% 행위는 %마스터를% 압박하고 있었다
	PRINTFORML
	PRINTFORML %마스터는% 더이상 저항의 의사가 없어졌다
	PRINTFORMW 마음속 깊이 %ARG의% 노예인걸 인정해버렸다...
	PRINTFORML
	PRINTFORMW %마스터는% %ARG의% [노예]가 되었습니다
	TALENT:ARG:주인 = 1
	TALENT:MASTER:노예 = 1
	FOR LOCAL, 1, CHARANUM
		;주인이 몇 명 있는가 검사
		SIF TALENT:LOCAL:주인 == 1
			검사용변수++
	NEXT
	SIF 검사용변수 != 0
		PRINTFORML 주인이 {검사용변수}명이나 되어버렸다...
	RETURN 1
ELSEIF CFLAG:ARG:주도도 == 0&&TALENT:ARG:주인
	PRINTFORML 최근, %ARG는% %마스터를% 함부로 대할수 없게 된 것 같다...
	PRINTFORMW 이제는 주인 행세도 못할 정도로...
	PRINTFORML
	PRINTFORML %마스터는% %ARG의% 노예 상태에서 벗어나버렸습니다...
	PRINTFORMW %ARG는% [주인]를 잃었습니다
	TALENT:ARG:주인 = 0
	FOR LOCAL, 1, CHARANUM
		;주인이 몇 명 있는가 검사
		SIF TALENT:LOCAL:주인 == 1
			검사용변수++
	NEXT
	;한명도 없으면 노예상태는 탈출
	SIF 검사용변수 == 0
		TALENT:MASTER:노예 = 0
	RETURN 1
ENDIF
RETURN 0

@노예명수
#FUNCTION
#DIM DYNAMIC 노예의수
FOR LOCAL,1,CHARANUM
	IF TALENT:LOCAL:당신의노예 == 1&&!TALENT:LOCAL:사망 && !CFLAG:LOCAL:출금 && !CFLAG:LOCAL:감금됨
		노예의수++
	ENDIF
NEXT
RETURNF 노예의수