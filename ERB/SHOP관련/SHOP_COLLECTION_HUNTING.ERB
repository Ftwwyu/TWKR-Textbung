;-----------------------------------------------------------
;사냥한 조수 목록 표시
;-----------------------------------------------------------
@SHOW_COLLECTION_HUNTED
#DIM 선택한짐승
#DIM 전체종수
#DIM 사냥한종수
#DIM 사냥한총수
#DIM 소거행수
#DIM 개행

VARSET LOCAL
전체종수 = 0
사냥한종수 = 0
사냥한총수 = 0
소거행수 = 0
개행 = 0

CLEARLINE 1
PRINTFORML 지금까지 사냥한 조수 목록
DRAWLINE
FOR LOCAL, 1, 61
	CALL CRITTER_DATA(LOCAL)
	;獣が無名でない
	IF CritterName != ""
		전체종수 ++
		SIF LOCAL < 10
			PRINTFORM  
		IF CAPTURE_RECORD:LOCAL
			사냥한종수 ++
			PRINTFORM [{LOCAL}] %CritterName, 20, LEFT%
			SETCOLOR C_L_GRAY
			PRINTFORM （사냥한 개체 수：
			SELECTCASE CAPTURE_RECORD:LOCAL
				CASE IS < 10
					PRINTFORM     
				CASE IS < 100
					PRINTFORM    
				CASE IS < 1000
					PRINTFORM   
				CASE IS < 10000
					PRINTFORM  
			ENDSELECT
			사냥한총수 += CAPTURE_RECORD:LOCAL
			IF STRCOUNT(CritterTribe, "새/")
				PRINTFORM {CAPTURE_RECORD:LOCAL}마리 ）
			ELSE
				PRINTFORM {CAPTURE_RECORD:LOCAL}필 ）
			ENDIF
		ELSE
			PRINTFORM [{LOCAL}] ????????????                                
		ENDIF
		개행 ++
		IF 개행 == 2
			PRINTL 
			개행 = 0
		ELSE
			PRINT 　　　
		ENDIF
	ENDIF
	RESETCOLOR
	;獣関連の変数リセット
	CALL CRITTER_V_RESET
NEXT
SETCOLOR 0x00FF00
PRINTFORM 사냥한 조수의 종류：{사냥한종수}／{전체종수}
IF 사냥한총수
	PRINTFORML 　사낭한 조수의 총수：{사냥한총수}두・마리
ELSE
	PRINTL 
ENDIF
RESETCOLOR
DRAWLINE
PRINT [0] - 돌아간다

$LOOP
INPUT

선택한짐승 = RESULT
SELECTCASE 선택한짐승
	CASE 1 TO 61
		CALL CRITTER_DATA(선택한짐승)
		IF CritterName != ""
			CLEARLINE 1
			CALL PRINT_CRITTERINFO(선택한짐승)
			IF CAPTURE_RECORD:선택한짐승
				소거행수 = 5
			ELSE
				소거행수 = 2
			ENDIF
			CLEARLINE 소거행수
			GOTO LOOP
		ELSE
			CLEARLINE 1
			GOTO LOOP
		ENDIF
	CASE 0
		RETURN
		;獣関連の変数リセット
		CALL CRITTER_V_RESET
	CASEELSE
		CLEARLINE 1
		GOTO LOOP
ENDSELECT

;-----------------------------------------------------------
;選択した獣の説明／ヒント表記
;ARG …선택한짐승番号
;-----------------------------------------------------------
@PRINT_CRITTERINFO(ARG)

DRAWLINE
IF CAPTURE_RECORD:ARG
	FONTSTYLE 1
	PRINTPLAINFORM [{ARG}] %CritterName%
	FONTREGULAR
	IF STRCOUNT(CritterTribe, "새/")
		PRINTFORML （사냥한 수：{CAPTURE_RECORD:ARG}마리 ）
	ELSE
		PRINTFORML （사냥한 수：{CAPTURE_RECORD:ARG}두 ）
	ENDIF
	IF ABL:MASTER:교양 < 3
		CALL COLORMESSAGE(@"　　（교양Lv을 올리면、분포 영역·사냥 시기를 알수 있겠다…）",C_L_GRAY,1)
	ELSE
		PRINTFORML 　　%CritterHint%
	ENDIF
	PRINTFORML 　　◇평균적인 수확물： %ITEMNAME:CritterMeat% × {CritterMeat_Value} ◇
	PRINTFORML 　　%CritterIntro%
ELSE
	PRINTPLAINFORM [{ARG}] ?????????????
	PRINTFORML 　　%CritterHint%
ENDIF
WAIT


